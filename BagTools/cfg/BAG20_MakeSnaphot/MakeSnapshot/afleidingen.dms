////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                                                                                    //
//              Configuratie voor project: BAG 2.0 XML bronfiles > FSS                //
//                                                                                    //
//                              Object Vision 2022                                    //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
// In deze configuratie worden afleidingen geconfigureerd voor attributen die         //
// aan de snapshot worden toegevoegd                                                  //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////

container afleidingen: Using = "eenheden"
{
	container pand_attributen
	{
		container hoogte
		{
			unit<uint32> PHN
			:	StorageName     = "%SourceDataDir%/PHN/PHN_20210101.fss" //, te downloaden via: https://www.geodms.nl/downloads/PHN/PHN_20210101.fss.zip, unzip het bestand en plaats het in de %SourceDataDir%/PHN folder
			,	StorageReadOnly = "true"
			{
				attribute<string>    identificatie;
				attribute<uint16>    AHN_inwinjaar; // uint16
				attribute<cm_uint16> hoogte;        // uint16
				attribute<bool>      isAHN3;
			}

			attribute<eenheden/cm_uint16> AHN_hoogte    (pand/result) := rjoin(pand/result/identificatie, PHN/identificatie, PHN/hoogte);
			attribute<uint16>             AHN_inwinjaar (pand/result) := rjoin(pand/result/identificatie, PHN/identificatie, PHN/AHN_inwinjaar);
		}

		container geometrie
		{
			container results
			{
				attribute<uint32>        nr_buurpanden     (pand/result) := functioneel_pand/NeighbourCount[invert(functioneel_pand/org_rel)]; 
				attribute<type_woonpand> type_woonpand_rel (pand/result) := functioneel_pand/type_woonpand_rel[invert(functioneel_pand/org_rel)];
			}

			unit<uint8> type_woonpand: nrofrows = 6
			{
				attribute<string> label : ['geen woonpand','vrijstaand','twee onder 1 kap','hoekwoning','tussenwoning','meergezinspand'], DialogType = "LabelText";
				attribute<string> name  := replace(label, ' ', '_');
				container V := for_each_nedv(name, String(ID(.))+'[..]', void, .);
			}

			container extents
			{
				attribute<geometries/rdc_mm> NW (pand/result) := lower_bound(pand/result/geometry_mm);
				attribute<geometries/rdc_mm> SE (pand/result) := upper_bound(pand/result/geometry_mm);
				attribute<int32> X_ext          (pand/result) := PointCol(SE) - PointCol(NW);
				attribute<int32> Y_ext          (pand/result) := PointRow(SE) - PointRow(NW);
			}

			unit<uint32> functioneel_pand := select_with_org_rel(pand/result/vbo_count > 0 && extents/X_ext < 10000000i && extents/Y_ext < 10000000i), DialogType = "Map", DialogData = "geometry_mm"
			{
				attribute<geometries/rdc_mm> geometry_mm (poly) := pand/result/geometry_mm[org_rel];
				attribute<geometries/rdc_mm> NW                 := lower_bound(geometry_mm);
				attribute<geometries/rdc_mm> SE                 := upper_bound(geometry_mm);

				// results collection
				attribute<uint32> NeighbourCount  := pcount(unique_neighbour_pair/F1) + pcount(unique_neighbour_pair/F2);
				attribute<bool>   HasOneNeighbour := NeighbourCount == 1;
				attribute<bool>   IsSemiAttached  := HasOneNeighbour &&
					HasOneNeighbour[
						MakeDefined(
							unique_neighbour_pair/F2[invert(unique_neighbour_pair/F1)],
							unique_neighbour_pair/F1[invert(unique_neighbour_pair/F2)]
						)
					];

				attribute<uint32> nr_woning := sum(uint32(vbo/result/gebruiksdoelen/woon), vbo/result/pand_rel)[org_rel];

				attribute<type_woonpand> type_woonpand_rel :=
					  nr_woning == 0      ? type_woonpand/V/geen_woonpand
					: nr_woning >  1      ? type_woonpand/V/meergezinspand
					: NeighbourCount == 0 ? type_woonpand/V/vrijstaand
					: HasOneNeighbour
						? IsSemiAttached
							? type_woonpand/V/twee_onder_1_kap
							: type_woonpand/V/hoekwoning
					: type_woonpand/V/tussenwoning;
			}

			parameter<uint32>  nr_x         :=   7;
			parameter<uint32>  nr_y         :=  13;
			parameter<Float64> inflate_size := 200.0;

			unit<uint32> rectangle := range(uint32, 0, nr_x * nr_y)
			{
				attribute<uint32> seq_y  := id(.) / nr_x;
				attribute<uint32> seq_x  := id(.) % nr_x;
				attribute<string> name   := 'R'+string(seq_y)+'_C'+string(seq_x);

				parameter<uint32> size_x := uint32(pointcol(BoundRange(geometries/rdc_mm))) / uint32(nr_x);
				parameter<uint32> size_y := uint32(pointrow(BoundRange(geometries/rdc_mm))) / uint32(nr_y);

				attribute<uint32> min_x  := uint32(pointcol(lowerbound(geometries/rdc_mm))) + seq_x * size_x;
				attribute<uint32> max_x  := min_x + size_x;
				attribute<uint32> min_y  := uint32(pointrow(lowerbound(geometries/rdc_mm))) + seq_y * size_y;
				attribute<uint32> max_y  := min_y + size_y;
			}

			container rectangles := for_each_ne(rectangle/name, 'connect_neighbours('+string(ID(rectangle))+')');

			template connect_neighbours
			{
				// begin case parameters
				parameter<rectangle> rectangle_sel;
				// end case parameters

				unit<uint32> domain :=
					select_with_org_rel(
						uint32(PointCol(functioneel_pand/NW)) <= rectangle/max_x[rectangle_sel]
					&&	uint32(PointRow(functioneel_pand/NW)) <= rectangle/max_y[rectangle_sel]
					&&	uint32(PointCol(functioneel_pand/SE)) >= rectangle/min_x[rectangle_sel]
					&&	uint32(PointRow(functioneel_pand/SE)) >= rectangle/min_y[rectangle_sel]
					)
					,	DialogType = "Map", DialogData = "geometry"
				{
					attribute<geometries/rdc_mm> geometry_mm (poly) := functioneel_pand/geometry_mm[org_rel];
				}
				attribute<geometries/rdc_mm> i4HV_mm (domain, polygon) := polygon_I4HV(domain/geometry_mm, inflate_size);

				unit<uint32> neighbours := polygon_connectivity(i4HV_mm)
				{
					attribute<upoint> coded_pair := point(domain/org_rel[F1], domain/org_rel[F2]);

					// nice to see
					attribute<geometries/rdc_mm> joint(., arc):=
						points2sequence_ps(
							union_data(combine(., bool), centroid(domain/geometry_mm)[F1], centroid(domain/geometry_mm)[F2]),
							union_data(combine(., bool), id(.), id(.))
						);
				}

				// results collection
				unit<uint32> unique_neighbour_pair := unique(neighbours/coded_pair), FreeData = "False", DialogType = "Map", DialogData = "joint"
				{
					attribute<functioneel_pand> F1 := pointrow(values)[functioneel_pand];
					attribute<functioneel_pand> F2 := pointcol(values)[functioneel_pand];

					// nice to see
					attribute<geometries/rdc_mm> joint (.,arc):=
						points2sequence_ps(
							union_data(combine(., bool), centroid(functioneel_pand/geometry_mm)[F1], centroid(functioneel_pand/geometry_mm)[F2]),
							union_data(combine(., bool), ID(.), ID(.))
						);
				}
				// Additional info for one rectangle, should not be used for final derivations
				attribute<uint32> NeighbourCount (domain) := (pcount(unique_neighbour_pair/F1) + pcount(unique_neighbour_pair/F2))[domain/org_rel];
				/*
				attribute<bool>   HasOneNeighbour(domain) := NeighbourCount == 1;
				attribute<bool>   IsSemiAttached (domain) := HasOneNeighbour &&
					HasOneNeighbour[
						MakeDefined(
							unique_neighbour_pair/F2[invert(unique_neighbour_pair/F1)], 
							unique_neighbour_pair/F1[invert(unique_neighbour_pair/F2)]
						)[domain/org_rel]
					];
				attribute<uint32> nr_woning(domain) := sum(uint32(vbo/result/gebruiksdoelen/woon), vbo/result/pand_rel)[functioneel_pand/org_rel][domain/org_rel];
				
				attribute<woonpand_type> woontype_rel(domain) :=
				
					nr_woning == 0          ? woonpand_type/V/geen_woonpand
				:	nr_woning >  1          ? woonpand_type/V/meergezinspand
				:	NeighbourCount == 0     ? woonpand_type/V/vrijstaand
				:	HasOneNeighbour
					? IsSemiAttached 
						?	woonpand_type/V/twee_onder_1_kap 
						:	woonpand_type/V/hoekwoning
				:	woonpand_type/V/tussenwoning
				;
				*/
			}
			unit<uint32> neighbour_pair := ='union_unit('+AsItemList('rectangles/'+rectangle/name+'/neighbours')+')'
			{
				attribute<upoint> coded_pair := ='union_data(., '+AsItemList('rectangles/'+rectangle/name+'/neighbours/coded_pair')+')';
			}
			unit<uint32> unique_neighbour_pair := unique(neighbour_pair/coded_pair), DialogType = "Map", DialogData = "joint"
			{
				attribute<functioneel_pand> F1 := pointrow(values)[functioneel_pand];
				attribute<functioneel_pand> F2 := pointcol(values)[functioneel_pand];

				// nice to see
				attribute<geometries/rdc_mm> joint(.,arc):=
					points2sequence_ps(
						union_data(Combine(., bool), centroid(functioneel_pand/geometry_mm)[F1], centroid(functioneel_pand/geometry_mm)[F2]),
						union_data(Combine(., bool), id(.), id(.))
					);
			}
		}
	}

	unit<uint32> adres := impl/nummeraanduiding_met_geom
	,	StorageName = "=SnapshotDir + '/adres.fss'"
	{
		attribute<string>            nummeraanduiding_id := impl/nummeraanduiding_met_geom/nummeraanduiding_id;
		attribute<string>            straatnaam          := rjoin(nummeraanduiding/result/openbareruimte_id, openbareruimte/result/identificatie, openbareruimte/result/naam);
		attribute<string>            postcode            := impl/nummeraanduiding_met_geom/postcode;
		attribute<int32>             huisnummer          := impl/nummeraanduiding_met_geom/huisnummer;
		attribute<string>            huisletter          := impl/nummeraanduiding_met_geom/huisletter;
		attribute<string>            toevoeging          := impl/nummeraanduiding_met_geom/toevoeging;

		attribute<geometries/rdc_mm> geometry_mm         := impl/nummeraanduiding_met_geom/geometry_mm, DisableStorage = "True";
		attribute<geometries/rdc>    geometry            := geometry_mm[geometries/rdc];
		attribute<float32>           X                   := pointcol(geometry_mm[geometries/rdc]);
		attribute<float32>           Y                   := pointrow(geometry_mm[geometries/rdc]);

		attribute<string>            woonplaatsnaam      := woonplaats/result/woonplaatsNaam[point_in_polygon(geometry_mm,woonplaats/result/geometry_mm)];
	}

	container impl
	{
		unit<uint32> nummeraanduiding_met_geom := nummeraanduiding/result, DialogType = "Map", DialogData = "geometry"
		{
			attribute<string> nummeraanduiding_id := nummeraanduiding/result/identificatie;
			attribute<string> postcode            := nummeraanduiding/result/postcode;
			attribute<int32>  huisnummer          := nummeraanduiding/result/huisnummer;
			attribute<string> huisletter          := nummeraanduiding/result/huisletter;
			attribute<string> toevoeging          := nummeraanduiding/result/toevoeging;
	
			// Ook locaties van alleen nevenadressen meenemen
			attribute<geometries/rdc_mm> vbo_point_hoofadres  := rjoin(nummeraanduiding_id, vbo/result/nummeraanduiding_id, vbo/result/geometry_mm);
			attribute<string>            vbo_id_nevenadres    := rjoin(nummeraanduiding_id, vbo/result/nevenadres/nummeraanduiding_id, vbo/result/nevenadres/identificatie);
			attribute<geometries/rdc_mm> vbo_point_nevenadres := rjoin(vbo_id_nevenadres,   vbo/result/identificatie, vbo/result/geometry_mm);
			attribute<geometries/rdc_mm> vbo_point            := MakeDefined(vbo_point_hoofadres,vbo_point_nevenadres);
	
			container lp // ligplaatsen
			{
				attribute<geometries/rdc_mm> point_src (nummeraanduiding_met_geom) := rjoin(nummeraanduiding_id, ligplaats/result/nummeraanduiding_id   , centroid(ligplaats/result/geometry_mm));
				attribute<int32>             X         (nummeraanduiding_met_geom) := PointCol(point_src);
				attribute<int32>             Y         (nummeraanduiding_met_geom) := PointRow(point_src);
				attribute<geometries/rdc_mm> point     (nummeraanduiding_met_geom) := 
					   X >=  -8000000i &&  X <= 300000000i
					&& Y >= 289000000i &&  Y <= 629000000i
						? point_src
						: point(0i/0i, 0i/0i, geometries/rdc_mm);
			}
			container sp // standplaatsen
			{
				attribute<geometries/rdc_mm> point_src (nummeraanduiding_met_geom) := rjoin(nummeraanduiding_id, standplaats/result/nummeraanduiding_id , centroid(standplaats/result/geometry_mm));
				attribute<int32> X                     (nummeraanduiding_met_geom) := PointCol(point_src);
				attribute<int32> Y                     (nummeraanduiding_met_geom) := PointRow(point_src);
				attribute<geometries/rdc_mm> point     (nummeraanduiding_met_geom) :=
					   X >=  -8000000i &&  X <= 300000000i
					&& Y >= 289000000i &&  Y <= 629000000i
						? point_src
						: point(0i/0i, 0i/0i, geometries/rdc_mm);
			}

			attribute<geometries/rdc_mm> geometry_mm := MakeDefined(MakeDefined(MakeDefined(vbo_point,lp/point),sp/point));

			attribute<int32> X                       := PointCol(geometry_mm);
			attribute<int32> Y                       := PointRow(geometry_mm);

			attribute<string> postcode_huisnr        := postcode + '_' + string(huisnummer);
			attribute<string> postcode4              := substr(postcode, 0, 4);
		}
	
		unit<uint32> vbo_met_adres := vbo/result
		{
			attribute<string>            openbareruimte_id := rjoin(vbo/result/nummeraanduiding_id, nummeraanduiding/result/identificatie, nummeraanduiding/result/openbareruimte_id);

			attribute<string>            straatnaam        := rjoin(openbareruimte_id, openbareruimte/result/identificatie, openbareruimte/result/naam);
			attribute<int32>             huisnummer        := rjoin(vbo/result/nummeraanduiding_id, nummeraanduiding/result/identificatie, nummeraanduiding/result/huisnummer);
			attribute<string>            huisletter        := rjoin(vbo/result/nummeraanduiding_id, nummeraanduiding/result/identificatie, nummeraanduiding/result/huisletter);
			attribute<string>            toevoeging        := rjoin(vbo/result/nummeraanduiding_id, nummeraanduiding/result/identificatie, nummeraanduiding/result/toevoeging);
			attribute<string>            postcode          := rjoin(vbo/result/nummeraanduiding_id, nummeraanduiding/result/identificatie, nummeraanduiding/result/postcode);

			attribute<string>            woonplaatsnaam    := woonplaats/result/woonplaatsNaam[point_in_polygon(geometry,woonplaats/result/geometry_mm)];
			attribute<geometries/rdc_mm> geometry          := vbo/result/geometry_mm;
		}
	}
}