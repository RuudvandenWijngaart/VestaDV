////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                                                                                    //
//              Configuratie voor project: BAG FSS Files > Make Snapshot              //
//                                                                                    //
//                              Object Vision 2018                                    //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
// Met deze configuratie kan uit een historische BAG database (in FSS formaat)        //
// een snapshot op een bepaalde datum gegenereerd worden (ook in FSS formaat)         //
//                                                                                    //
// De snapshot kan ook voor een ruimtelijke selectie gemaakt worden                   //
//                                                                                    //
// Voor de BAG object tabellen geldt dat in resultaat van de snapshot                 //
// de identificatie attributen uniek zijn (kandidaat primaire sleutel)                //
//                                                                                    //
// Tevens kunnen een aantal extra attributen op het snapshot moment afgeleid:         //
// 1) woonpandtypering                                                                //
// 2) pandhoogte obv PHN (afgeleid van AHN). Deze afleiding is optioneel.             //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////

container BAG_MakeSnapshot
{
	parameter<string> FSSDir          := '%SourceDataDir%/BAG/20210319/fss';
	parameter<string> SnapshotBaseDir := '%SourceDataDir%/BAG/snapshots.ext';

	container snapshot_date_nl_status_selectie_zonder_hoogte_20120101 := MakeSnapshot(20120101[eenheden/datum], False ,True , False);
	container snapshot_date_nl_status_selectie_zonder_hoogte_20160101 := MakeSnapshot(20160101[eenheden/datum], False ,True , False);
	container snapshot_date_nl_status_selectie_zonder_hoogte_20180101 := MakeSnapshot(20180101[eenheden/datum], False ,True , False);
	container snapshot_date_nl_status_selectie_zonder_hoogte_20190701 := MakeSnapshot(20190701[eenheden/datum], False ,True , False);

	container snapshot_date_zh_zonder_hoogte_20180101     := MakeSnapshot(20180101[eenheden/datum], False ,False , False);
	container snapshot_date_geosel_zonder_hoogte_20170101 := MakeSnapshot(20170701[eenheden/datum], True ,False , False);

//	container snapshot_date_kl_met_hoogte_20190101 := MakeSnapshot(20190101[eenheden/datum],True ,True ,True);
	container snapshot_date_leiden_met_hoogte_20200101   := MakeSnapshot(20200101[eenheden/datum],True ,True ,True);
	container snapshot_date_waalwijk_met_hoogte_20210101 := MakeSnapshot(20210101[eenheden/datum],True ,True ,True);

	container snapshot_date_schiedam_met_hoogte_20200101 := MakeSnapshot(20200101[eenheden/datum],True ,True ,True);

	container snapshot_date_gooiNH_met_hoogte_20200101 := MakeSnapshot(20200101[eenheden/datum],True ,True ,True);

	container snapshot_date_lansingerland_met_hoogte_20190701 := MakeSnapshot(20190701[eenheden/datum],True ,True ,True);
	container snapshot_date_kl_met_hoogte_20200101 := MakeSnapshot(20200101[eenheden/datum],True ,True ,True);

	container snapshot_date_nl_alle_statussen_zonder_hoogte_20200101_geoparaat := MakeSnapshot(20200101[eenheden/datum], False ,False , False);
	container snapshot_date_nl_alle_statussen_zonder_hoogte_20200401_geoparaat := MakeSnapshot(20200401[eenheden/datum], False ,False , False);
	container snapshot_date_nl_alle_statussen_zonder_hoogte_20200701_geoparaat := MakeSnapshot(20200701[eenheden/datum], False ,False , False);

	container snapshot_date_nl_alle_statussen_zonder_hoogte_20210301_geoparaat := MakeSnapshot(20210301[eenheden/datum], False ,False , False);

	container eenheden
	{
		unit<int32>   datum;
		unit<int16>   jaar;
		unit<int32>   mm        := int32(0.001 * m);
		unit<uint16>  cm_uint16 := uint16(0.01 * m);
		unit<float32> m         := baseunit('m', float32);
	}
	container geometries
	{
		#include <ngr_layer.dms>

		unit<fpoint> rdc_base
		: format     = "EPSG:28992"
		, DialogData = "ngr_layer";
		unit<fpoint> rdc    := range(rdc_base, point(300000f,0f), point(625000f,280000f));
		unit<ipoint> rdc_mm := gridset(rdc, point(0.001,0.001), point(0.0, 0.0), ipoint);
	}

	unit<uint32> geo_selectie
// 	:	nrofrows = 1
// 	{
// 		// Voorbeeld voor het configureren van een geo_selectie met een rechthoek, pas de pointset/X en pointset/Y attributen aan voor een nieuwe rechthoek
// 		parameter<string>         Label           := 'NieuwegeinRechthoek';
// 		attribute<geometries/rdc> geometry (poly) := points2sequence(pointset/point, pointset/sequence, pointset/ordinal);

// 		unit<uint32> pointset: nrofrows = 5
// 		{
// 			parameter<eenheden/m>     X_min    := 132750[eenheden/m];
// 			parameter<eenheden/m>     X_max    := 133750[eenheden/m];
// 			parameter<eenheden/m>     Y_min    := 410750[eenheden/m];
// 			parameter<eenheden/m>     Y_max    := 412000[eenheden/m];

// 			attribute<eenheden/m>     X        := union_data(., X_min, X_max, X_max, X_min, X_min);
// 			attribute<eenheden/m>     Y        := union_data(., Y_min, Y_min, Y_max, Y_max, Y_min);
// 			attribute<geometries/rdc> point    := point(Y, X, geometries/rdc);
// 			attribute<geo_selectie>   sequence := const(0[geo_selectie],.);
// 			attribute<.>              ordinal  := id(.);
// 		}
// 	}

	// Voorbeeld voor het lezen van een geo_selectie uit een shape file
	//:	StorageName     = "%LocalDataDir%/Lansingerland/website/gemeente.shp"
	//:	StorageName     = "%LocalDataDir%/kennemerland/website/gemeente.shp"
	//:	StorageName     = "%LocalDataDir%/Leiden/website/gemeente.shp"
	//	StorageName     = "%SourceDataDir%/GooiNH/gem.shp"
	:	StorageName     = "%SourceDataDir%/Waalwijk/Waalwijk.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	{
		parameter<string>         Label := 'Waalwijk';
		attribute<geometries/rdc> geometry (poly);
	}

	container typeringen
	{
		unit<uint8> objecttypes: nrofrows = 7
		{
			attribute<string> name:
			[
				'ligplaats','standplaats','pand','vbo','nummeraanduiding','openbareruimte','woonplaats'
			];
		}

		unit<uint8> gebruiksdoel_vbo: nrofrows = 11
		{
			attribute<string> label:
			[
				'bijeenkomstfunctie','celfunctie','gezondheidszorgfunctie','industriefunctie','kantoorfunctie','logiesfunctie'
				,'onderwijsfunctie','overige gebruiksfunctie','sportfunctie','winkelfunctie','woonfunctie'
			];
			attribute<string> name := replace(replace(label,' ','_'), 'functie','');
		}

		#include<status.dms>
	}


	#include<MakeSnapshot.dms>
	
	template meta_src_template // Voor het inlezen van de generieke BAG attributen
	{
		// begin case parameters
		unit<uint32> domain;
		unit<uint8>  statustype;
		// end case parameters

		attribute<string>     STATUS     (domain);
		attribute<statustype> status_rel (domain) := rlookup(lowercase(STATUS), statustype/label);
		attribute<bool>       status_sel (domain) := statustype/Selected[status_rel];

		attribute<int32 > BEGINDATUM     (domain);
		attribute<int32>  EINDDATUM      (domain);

		attribute<int32>  DOCUMENTDATUM  (domain);
		attribute<string> DOCUMENTNUMMER (domain);

		attribute<bool>   IS_ONDERZOEK   (domain);
		attribute<bool>   IS_CORRECTIE   (domain);
		attribute<bool>   IS_INACTIEF    (domain);
		attribute<bool>   IS_OFFICIEEL   (domain);
	}

	template meta_template // Voor het aggregeren van de generieke BAG attributen
	{
		// begin case parameters
		unit<uint32> src_domain;
		unit<uint32> target_domain;
		// end case parameters

		attribute<string>         Status         (target_domain) := AsItemList(selectie/meta/Status         , selectie/unique_rel);
		attribute<eenheden/datum> BEGINDATUM     (target_domain) :=       last(selectie/meta/BEGINDATUM     , selectie/unique_rel);
		attribute<eenheden/datum> EINDDATUM      (target_domain) :=       last(selectie/meta/EINDDATUM      , selectie/unique_rel);
		attribute<eenheden/datum> DOCUMENTDATUM  (target_domain) :=       last(selectie/meta/DOCUMENTDATUM  , selectie/unique_rel);
		attribute<string>         DOCUMENTNUMMER (target_domain) := AsItemList(selectie/meta/DOCUMENTNUMMER , selectie/unique_rel);

		attribute<bool>           IS_ONDERZOEK   (target_domain) := any(selectie/meta/IS_ONDERZOEK, selectie/unique_rel);
		attribute<bool>           IS_CORRECTIE   (target_domain) := any(selectie/meta/IS_CORRECTIE, selectie/unique_rel);
		attribute<bool>           IS_INACTIEF    (target_domain) := any(selectie/meta/IS_INACTIEF , selectie/unique_rel);
		attribute<bool>           IS_OFFICIEEL   (target_domain) := any(selectie/meta/IS_OFFICIEEL , selectie/unique_rel);
	}

	template datum_selectie
	{
		// begin case parameters
		unit<uint32>              domain;
		parameter<eenheden/datum> prik_datum;
		parameter<bool>           overig_filter_actief;
		attribute<bool>           overig_filter(domain) := const(true, domain);
		// einde case parameters

		attribute<bool> WasGestart   (domain) := domain/meta/BEGINDATUM <= prik_datum;
		attribute<bool> WasBeeindigd (domain) := domain/meta/EINDDATUM  <= prik_datum && domain/meta/EINDDATUM > 0i;

		unit<uint32> selectie :=
			= 'Subset(WasGestart && not(WasBeeindigd) && Not(domain/Meta/IS_INACTIEF) ' + 
				(met_status_sel 
					? '&& domain/Meta/status_sel ' 
					: '') +
				(overig_filter_actief 
					? '&& overig_filter' 
					: '') 
			+ ')'
		,	DialogType = "Map"
		,	DialogData = "geometry_mm"
		{
			container meta
			{
				attribute<string>         STATUS         (selectie) := domain/meta/STATUS[nr_OrgEntity];
				attribute<eenheden/datum> BEGINDATUM     (selectie) := domain/meta/BEGINDATUM[nr_OrgEntity];
				attribute<eenheden/datum> EINDDATUM      (selectie) := domain/meta/EINDDATUM[nr_OrgEntity];
				attribute<eenheden/datum> DOCUMENTDATUM  (selectie) := domain/meta/DOCUMENTDATUM[nr_OrgEntity];
				attribute<string>         DOCUMENTNUMMER (selectie) := domain/meta/DOCUMENTNUMMER[nr_OrgEntity];

				attribute<bool>           IS_ONDERZOEK   (selectie) := domain/meta/IS_ONDERZOEK[nr_OrgEntity];
				attribute<bool>           IS_CORRECTIE   (selectie) := domain/meta/IS_CORRECTIE[nr_OrgEntity];
				attribute<bool>           IS_INACTIEF    (selectie) := domain/meta/IS_INACTIEF[nr_OrgEntity];
				attribute<bool>           IS_OFFICIEEL   (selectie) := domain/meta/IS_OFFICIEEL[nr_OrgEntity];
			}
		}
	}

	Template polygon_selectie
	{
		// begin case parameters
		unit<uint32> domain;
		attribute<geometries/rdc_mm> geometry_mm (polygon, domain);
		// end case parameters

		container bbox := bbox_min_max(domain,geometry_mm);

		attribute<geometries/rdc> x_min_y_min (domain) := point(bbox/y_min[eenheden/m], bbox/x_min[eenheden/m], geometries/rdc);
		attribute<geometries/rdc> x_min_y_max (domain) := point(bbox/y_max[eenheden/m], bbox/x_min[eenheden/m], geometries/rdc);
		attribute<geometries/rdc> x_max_y_max (domain) := point(bbox/y_max[eenheden/m], bbox/x_max[eenheden/m], geometries/rdc);
		attribute<geometries/rdc> x_max_y_min (domain) := point(bbox/y_min[eenheden/m], bbox/x_max[eenheden/m], geometries/rdc);
		attribute<geometries/rdc> centroid    (domain) := centroid(geometry_mm)[geometries/rdc];

		attribute<bool> result (domain) :=
			   isDefined(point_in_polygon(x_min_y_min, /geo_selectie/geometry))
			|| isDefined(point_in_polygon(x_min_y_max, /geo_selectie/geometry))
			|| isDefined(point_in_polygon(x_max_y_max, /geo_selectie/geometry))
			|| isDefined(point_in_polygon(x_max_y_min, /geo_selectie/geometry))
			|| isDefined(point_in_polygon(   centroid, /geo_selectie/geometry));
	}

	Template nl_selectie
	{
	}

	Template bbox_min_max
	{
		// begin case parameters
		unit<uint32> domain;
		attribute<geometries/rdc_mm> geometry_mm (polygon, domain);
		// end case parameters

		attribute<geometries/rdc_mm> lowerbound (domain) := lower_bound(geometry_mm);
		attribute<geometries/rdc_mm> upperbound (domain) := upper_bound(geometry_mm);

		attribute<eenheden/mm>       x_min      (domain) := PointCol(lowerbound)[eenheden/mm];
		attribute<eenheden/mm>       x_max      (domain) := PointCol(upperbound)[eenheden/mm];
		attribute<eenheden/mm>       y_min      (domain) := PointRow(lowerbound)[eenheden/mm];
		attribute<eenheden/mm>       y_max      (domain) := PointRow(upperbound)[eenheden/mm];
	}
}
