//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door stam.dms                                  //
//       In deze configuratiefile wordt de brondata voor de GlasTuinBouw geconfigureerd //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container GlasTuinBouw
{
	unit<uint32> bestaand:
		StorageName     = "%sourceDataProjDir%/vraag/glastuinbouw/gltb_180219.dbf",
		DialogData      = "point",
		DialogType      = "Map",
		StorageReadOnly = "True",
		SyncMode        = "All",
		url             = "%sourceDataProjDir%/vraag/glastuinbouw/Werkwijze_aanmaak_huidige_glastuinbouw_tbv_Vesta33.pdf",
		FreeData        = "False"
	{
		attribute<rdc_meter> point:
			StorageName     = "%sourceDataProjDir%/vraag/glastuinbouw/gltb_180219.shp",
			StorageReadOnly = "True"
		{
			// parameter<Float64> SymbolSize:      DialogType = "SymbolSize",      expr = "Float64(16)";
			// parameter<m>       SymbolWorldSize: DialogType = "SymbolWorldSize", expr = "value(100, m)";
			// parameter<int16>   SymbolIndex:     DialogType = "SymbolIndex",     expr = "int16(81)";
		}
		
		attribute<Invoer/RuimtelijkeData/StudieGebied/buurt>    buurt_rel    := point_in_polygon(point, Invoer/RuimtelijkeData/StudieGebied/buurt/geometry);
		attribute<Invoer/RuimtelijkeData/StudieGebied/gemeente> gemeente_rel := point_in_polygon(point, Invoer/RuimtelijkeData/StudieGebied/gemeente/geometry);

		attribute<Classifications/GLTB> GLTB_Code;
		attribute<m2>                   Opp_M2;
		
	}
	container Af :=
		for_each_ndva(Classifications/Zichtjaar/HasMutations ? Classifications/Zichtjaar/name : '',
			Geography/rdc_grids/m100, bool,
			SpecifiekeInstellingen/WloScenarioFolder+'/'+Classifications/Zichtjaar/period_name+'/GLTB/Af.tif'
		);

	container Bij :=
		for_each_ndva(Classifications/Zichtjaar/HasMutations ? Classifications/Zichtjaar/name : '',
			Geography/rdc_grids/m100, bool,
			SpecifiekeInstellingen/WloScenarioFolder+'/'+Classifications/Zichtjaar/period_name+'/GLTB/Bij.tif'
		);

	unit<uint32> bestaand_studiegebied:= Subset(IsDefined(bestaand/gemeente_rel))
	,	DialogData  = "point"
	,	DialogType  = "Map"
	{
		attribute<rdc_meter>             point     := bestaand/point[nr_OrgEntity];
		attribute<Studiegebied/buurt>    buurt_rel := bestaand/buurt_rel[nr_OrgEntity];
		attribute<Classifications/GLTB>  GLTB_Code := bestaand/GLTB_Code[nr_OrgEntity];
		attribute<m2>                    Opp_M2    := bestaand/Opp_M2[nr_OrgEntity];
	}
	container Af_studiegebied :=
		for_each_nedv(Classifications/Zichtjaar/HasMutations ? Classifications/Zichtjaar/name : ''
			, 'Af/' +  Classifications/Zichtjaar/name  + '&& Studiegebied/selected_per_m100'
			, Geography/rdc_grids/m100
			, bool
			
		);

	container Bij_studiegebied :=
		for_each_nedv(Classifications/Zichtjaar/HasMutations ? Classifications/Zichtjaar/name : ''
			, 'Bij/' +  Classifications/Zichtjaar/name  + '&& Studiegebied/selected_per_m100'
			, Geography/rdc_grids/m100
			, bool
			
		);

	container verdeling
	{
		container NL
		{
			attribute<units/m2>    Oppervlakte  (Classifications/GLTB) := sum(GlasTuinBouw/bestaand/Opp_M2 , GlasTuinBouw/bestaand/GLTB_Code);
			attribute<units/ratio> GLTB         (Classifications/GLTB) := scalesum(Oppervlakte, 1.0);
		}
	}
}