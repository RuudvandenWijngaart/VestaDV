//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door brondata.dms                              //
//                                                                                      //
//       In deze configuratiefile wordt een shapshot uit de BAG geconfigureerd          //
//       De snapshot kan gemaakt worden met de OV Bag Tools                             //
//       die gebruikt wordt voor de bebouwingscomponenten                               //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container BAG: url="%sourceDataProjDir%/BAG/20190114_actualisatie_BAG_FvdM.txt"
{
	parameter<string>   SnapShotDate := '20240101';
	parameter<units/yr> SnapShotYear := left(SnapShotDate,4)[units/yr];
	parameter<string>   SourceDir    := '%SourceDataProjDir%/BAG/' + SnapShotDate;
	parameter<int32>    MaxDatum     := 20500101i, Descr = "voor het forceren van de datum ipv 0";
	parameter<uint16>   verdiepinghoogte_cm := 280w;
			
	container typeringen
	{
		unit<uint8> gebruiksdoel: nrofrows = 11
		{
			attribute<string> label:
			[
				'bijeenkomstfunctie','celfunctie','gezondheidszorgfunctie','industriefunctie','kantoorfunctie','logiesfunctie'
				,'onderwijsfunctie','overige gebruiksfunctie','sportfunctie','winkelfunctie','woonfunctie'
			];
			attribute<uint32> BrushColor:
			[
				 rgb(230,204,255), rgb(100,100,100), rgb(150,  0,  0), rgb(100,100,100),rgb(179, 51,255),rgb(200,200,  0)
				,rgb(  0,  0,200), rgb(200,200,200), rgb(  0,255,  0), rgb(  0,128,  0),rgb(255,100,100)
			]
			,	DialogType = "BrushColor";
			attribute<uint32> PenColor := BrushColor, DialogType = "PenColor";
			attribute<string> name     := replace(label,' ','_', 'functie','');
			container impl: IsHidden = "True"
			{
				attribute<uint16> BitValue (gebruiksdoel) := ='union_data(..,'+AsItemList('1w'+repeat(' * 2w',uint32(id(gebruiksdoel))))+')';
			}
			container V:= for_each_nedv( name , 'value('+string(id( . ))+', ..)', void, . );
		}
		unit<uint8> WoningtypeSnapshot : nrofrows = 6 // zie ook: HouseTypeCombi, dat oook in RSL gebruikt wordt.
		{
			attribute<string> name : ['geen_woonpand','vrijstaand','twee_onder_1_kap','hoekwoning','tussenwoning','meergezinspand'];
			attribute<string> label := name, DialogType = "labelText";
	
			attribute<uint32> BrushColor: DialogType = "BrushColor",
			[
				 rgb(255,255,255)
				,rgb( 26,152, 80)
				,rgb(166,217,106)
				,rgb(100,100,255)
				,rgb(255,255,  0)
				,rgb(255, 50, 50)
			];
			attribute<uint32> PenColor := rgb((bitand(BrushColor / 16384, 255) / 2), (bitand(BrushColor / 256, 255) / 2), (bitand(BrushColor, 255) / 2))
			,	DialogType = "PenColor";

			container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
		}
	}

	container import : using = "classifications;typeringen"
	{
		unit<uint32> vbo_src
		:	StorageName     = "= SourceDir + '/vbo.fss'"
		,	StorageReadOnly = "True"
		{
			attribute<geography/rdc_mm>    geometry_mm;
			attribute<geography/rdc_meter> geometry                     := geometry_mm[geography/rdc_meter];
			attribute<string>              identificatie;
			attribute<string>              nummeraanduiding_id;
			attribute<int32>               oppervlakte;
			attribute<m2>                  oppervlakte_corr             := impl/IsRareOppervlakte ? null_d : oppervlakte[m2];
			// attribute<string>              pand_id_geom;
			attribute<string>              pand_id;

			attribute<bool>                vbo_selection_condition      := impl/IsBinnenNL && impl/IsVoorraad && impl/IsActief;
			
			container meta := meta_src_template(., vbo_status);
			container gebruiksdoelen :=
				for_each_ndv(
					typeringen/gebruiksdoel/name
					, vbo_src
					, bool
				);
				
			unit<uint32> impl := .
			{
					attribute<float64>    x                           := PointCol(geometry);
					attribute<float64>    y                            := PointRow(geometry);
					
					attribute<bool>       IsBinnenNL                   := x < 300000d && x > 0d && y < 620000d && y > 300000d;
					attribute<bool>       IsVoorraad                   := vbo_status/isVoorraad[meta/status_rel];
					attribute<bool>       IsActief                     := meta/begindatum_edit < int32(string(SnapShotYear)+'0101') && meta/einddatum_edit >= int32(string(SnapShotYear)+'0101');
					attribute<bool>       IsRareOppervlakte            := Oppervlakte = 1i || Oppervlakte = 999999i || Oppervlakte = 99999i || Oppervlakte = 9999i;
			}
		}
		
		unit<uint32> vbo := select_with_attr_by_cond(vbo_src, vbo_src/vbo_selection_condition)
		, Descr = "selectie van objecten die voldoen aan de CBS-voorraad definitie"
		{
			attribute<uint64>             vbo_bag_nr               := uint64(identificatie);
			attribute<uint64>             pand_bag_nr              := uint64(pand_id);
			// attribute<uint64>             pand_bag_nr_geom         := uint64(pand_id_geom);
			attribute<pand>               pand_rel                 := rlookup(pand_bag_nr, pand/pand_bag_nr);
			// attribute<pand>               pand_rel_geom            := point_in_polygon(geometry, pand/geometry);
			// attribute<pand>               pand_rel_augm            := IsNull(pand_rel) ? pand_rel_geom : pand_rel; //dit scheelt 300 van de 37000 nulls.
			attribute<float32>            pand_bouwlagen_fictief   := pand/bouwlagen_fictief[pand_rel];
			attribute<yr>                 bouwjaar                 := pand_rel -> bouwjaar_corr;
			attribute<WoningtypeSnapshot> woonpand_type            := pand_rel -> woonpand_type;
			container                     gebruiksdoelen           := collect_attr_by_cond(vbo_src/gebruiksdoelen, vbo, vbo_src/vbo_selection_condition);
			attribute<uint16>             gebruiksdoelcombinatie   := =AsList(string(typeringen/gebruiksdoel/impl/BitValue)+ 'w * uint16(gebruiksdoelen/' + typeringen/gebruiksdoel/name + ')',' + '), IsHidden = "True";
			attribute<string>             label                    := rjoin(nummeraanduiding_id, nummeraanduiding/identificatie, nummeraanduiding/adres_key);
			attribute<string>             postcode                 := rjoin(nummeraanduiding_id, nummeraanduiding/identificatie, nummeraanduiding/postcode);
			
			container oppervlak_filters
			{
				attribute<m2> wonen         (..) :=  Oppervlakte_corr > 10000[m2] && pand_bouwlagen_fictief > 10f ? null_d : Oppervlakte_corr
				,	Descr = "Correctie van oppervlaktes deels op basis van document: http://www.cbs.nl/nl-NL/menu/themas/bouwen-wonen/methoden/dataverzameling/korte-onderzoeksbeschrijvingen/2015-kenmerken-woningen-kob.htm"
							"Aanvulling van OV op oppervlakte in relatie tot pand footprint (verdacht groot) en op 99999";
				attribute<m2> utiliteit     (..) := Oppervlakte_corr > 160000[m2] && pand_bouwlagen_fictief > 45f ? null_d : Oppervlakte_corr
				,	Descr    = "Grootste kantoorgebouw van Nederland bedraagt 160.000m2 Rotterdam Kop van Zuid, aantal bouwlagen in Nederland maximaal 44";
			}
			
			// relatie naar buurt voor o.a. ruimtelijke selectie
			attribute<RegioIndelingen/buurt>    buurt_rel    := point_in_polygon(geometry, RegioIndelingen/buurt/geometry);
			attribute<RegioIndelingen/gemeente> gemeente_rel := RegioIndelingen/buurt/gemeente_rel[buurt_rel];
		}

		unit<uint32> pand_src
		:	StorageName     = "= SourceDir + '/pand.fss'"
		,	StorageReadOnly = "True"
		{
			attribute<rdc_mm>              geometry_mm (poly);
			attribute<rdc_meter>           geometry    (poly) := geometry_mm[rdc_meter];
			attribute<string>              identificatie;
			attribute<uint16>              AHN_hoogte;
			attribute<uint16>              bouwjaar;
			attribute<bool>                pand_selection_condition     := impl/IsVoorraad && impl/IsActief && impl/IsNietTeGroot && impl/IsBinnenNL;
			
			container meta := meta_src_template(., pand_status);
		
			unit<uint32> impl := .
			{
				attribute<rdc_meter>  NW                           := lower_bound(geometry);
				attribute<rdc_meter>  SE                           := upper_bound(geometry);
				attribute<float64>    X_ext                        := sub_or_null(PointCol(SE),PointCol(NW));
				attribute<float64>    Y_ext                        := sub_or_null(PointRow(SE),PointRow(NW));
				
				attribute<float64>    lower_x                      := pointCol(NW);
				attribute<float64>    upper_x                      := pointCol(SE);
				attribute<float64>    lower_y                      := pointRow(NW);
				attribute<float64>    upper_y                      := pointRow(SE);
				
				attribute<bool>       IsVoorraad                   := pand_status/isVoorraad[meta/status_rel];
				attribute<bool>       IsActief                     := meta/begindatum_edit < int32(string(SnapShotYear)+'0101') && meta/einddatum_edit >= int32(string(SnapShotYear)+'0101');
				attribute<bool>       IsNietTeGroot                := X_ext < 1000d && Y_ext < 1000d; //panden met een grotere x/y range dan 1km eruit
				attribute<bool>       IsBinnenNL                   := lower_x < 300000d && lower_x > 0d
																				&& upper_x < 300000d && upper_x > 0d
																				&& lower_y < 620000d && lower_y > 300000d
																				&& upper_y < 620000d && upper_y > 300000d; 
			}
		}
		
		unit<uint32> pand := select_with_attr_by_cond(pand_src, pand_src/pand_selection_condition)
		, Descr = "selectie van panden die voldoen aan de CBS-voorraad definitie"
		{
			attribute<WoningtypeSnapshot>  woonpand_type       := AfleidingPandtype/Results/WP5_rel;
			attribute<uint64>              pand_bag_nr         := uint64(identificatie);
			parameter<uint32>              PenColor            := rgb(7,243,243), DialogType = "PenColor";
			parameter<int16>               PenWidth            := 1s            , DialogType = "PenWidth";
			parameter<uint32>              BrushColor          := rgb(7,7,243)  , DialogType = "BrushColor";
			attribute<float32>             verdiepingen_float  := float32(AHN_hoogte) / float32(verdiepinghoogte_cm);
			attribute<uint32>              vbo_count           := pcount(vbo/pand_rel);
			attribute<yr>                  bouwjaar_corr       := (bouwjaar <= 1005w || bouwjaar[yr] > SnapShotYear) ? null_d : bouwjaar[yr], Descr = "Correctie van bouwjaren op basis van document", url   = "http://www.cbs.nl/nl-NL/menu/themas/bouwen-wonen/methoden/dataverzameling/korte-onderzoeksbeschrijvingen/2015-kenmerken-woningen-kob.htm";
			attribute<uint8>               nr_verdiepingen     := uint8(round(verdiepingen_float));
			attribute<m2>                  footprint           := area(geometry, m2);
			attribute<m2>                  vbo_oppervlak       := sum(vbo/Oppervlakte_corr, vbo/pand_rel)[m2];
			attribute<float32>             bouwlagen_fictief   := float32(vbo_oppervlak / footprint);
			attribute<uint32>              nr_woningen         := pcount(vbo_woonfunctie_studiegebied/pand_rel);
			
			#include<AfleidingPandtype.dms>
			
			attribute<bool> HeeftGreenvisAansluiting := sum_uint32(vbo/gebruiksdoelcombinatie != typeringen/gebruiksdoel/impl/BitValue[typeringen/gebruiksdoel/V/overige_gebruiks], vbo/pand_rel) > 0 
				|| area(geometry, float64) > 50.0;
				
			attribute<bool> HeeftVestaAansluiting := 
				sum_uint32(vbo/gebruiksdoelen/woon
						&& vbo/oppervlak_filters/wonen >=  10[Units/m2]
						&& vbo/oppervlak_filters/wonen < 1000[Units/m2]
				, vbo/pand_rel)
			+	sum_uint32(not(vbo/gebruiksdoelen/woon)
						&& IsDefined(import/vbo/oppervlak_filters/utiliteit)
						&& vbo/gebruiksdoelcombinatie <> 8w
				, vbo/pand_rel)
			> 0;
		}

		unit<uint32> nummeraanduiding
		:	StorageName     = "= SourceDir + '/nummeraanduiding.fss'"
		,	StorageReadOnly = "True"
		{
			attribute<string> identificatie;
			attribute<string> openbareruimte_id;
			attribute<int32>  huisnummer;
			attribute<string> huisletter;
			attribute<string> toevoeging;
			attribute<string> postcode;

			attribute<string> toev      := MakeDefined(huisletter,'') + MakeDefined(toevoeging,'');
			attribute<string> adres_key :=
				//lowercase(
					postcode + '_' + string(huisnummer) + 
					(strlen(toev) > 0
						? '_' + toev
						: ''
					)
				//)
				;
		}
		
		template meta_src_template // Voor het inlezen van de generieke BAG attributen
		{
			unit<uint32> domain;
			unit<uint8>  statustype;
			// end case parameters

			attribute<string>     STATUS                 (domain);
			attribute<statustype> status_rel             (domain) := rlookup(lowercase(STATUS), lowercase(statustype/label));

			attribute<int32>      BEGINDATUM             (domain);
			attribute<int32>      EINDDATUM              (domain);
			attribute<uint32>     VOORKOMENIDENTIFICATIE (domain); //een identificatienummer kan meerdere keren voorkomen. Dit is een volgnummer dat de versie weergeeft.

			attribute<string>     TIJDSTIPREGISTRATIE    (domain);
			attribute<string>     EINDREGISTRATIE        (domain);

			attribute<int32>      DOCUMENTDATUM          (domain);
			attribute<string>     DOCUMENTNUMMER         (domain);

			attribute<bool>       IS_GECONSTATEERD       (domain);
			
			attribute<int32>      TIJDSTIPREGISTRATIE_edit        (domain) := int32(replace(substr(TIJDSTIPREGISTRATIE,0,10),'-',''));
			attribute<int32>      EINDREGISTRATIE_edit            (domain) := int32(replace(substr(EINDREGISTRATIE,0,10),'-',''));
			attribute<int32>      begindatum_maxdatum             (domain) := BEGINDATUM == 0i ? MaxDatum : BEGINDATUM;
			attribute<int32>      einddatum_maxdatum              (domain) := EINDDATUM == 0i ? MaxDatum : EINDDATUM;
			attribute<int32>      begindatum_edit                 (domain) := begindatum_maxdatum < 19600101i ? TIJDSTIPREGISTRATIE_edit : begindatum_maxdatum;
			attribute<int32>      einddatum_edit                  (domain) := einddatum_maxdatum < 19600101i ? EINDREGISTRATIE_edit : einddatum_maxdatum;
		}
	}

	unit<uint32> vbo_woonfunctie_studiegebied := select_with_org_rel( 
			import/vbo/gebruiksdoelen/woon
		&& Studiegebied/selected_per_gemeente[import/vbo/gemeente_rel]
		&& IsDefined(import/vbo/pand_rel)
		&& import/vbo/oppervlak_filters/wonen >=  10[Units/m2]
		&& import/vbo/oppervlak_filters/wonen < 1000[Units/m2]
		)
	{
		attribute<rdc_meter>                              geometry                   := org_rel -> geometry;
		attribute<import/pand>                            pand_rel                   := org_rel -> pand_rel;
		attribute<yr>                                     bouwjaar                   := org_rel -> bouwjaar;
		attribute<uint64>                                 vbo_bag_nr                 := org_rel -> vbo_bag_nr;
		attribute<m2>                                     oppervlakte_corr           := org_rel -> oppervlakte_corr;
		attribute<units/pand_asl>                         pand_aandeel               := 1.0 / value(import/pand/nr_woningen[pand_rel],units/asl_pand);
		attribute<Classifications/BouwjaarWoningBestaand> BouwjaarWoningBestaand_rel := isNull(bouwjaar) ? Classifications/BouwjaarWoningBestaand/onbekend : classify(bouwjaar,Classifications/BouwjaarWoningBestaand/ClassBreak);
		attribute<typeringen/WoningtypeSnapshot>          WoningtypeSnapshot_rel     := org_rel -> woonpand_type;
		attribute<classifications/WoningtypeBestaand>     WoningtypeBestaand_rel     := (WoningtypeSnapshot_rel == typeringen/WoningtypeSnapshot/V/meergezinspand && nr_verdiepingen > 4b)
																							? classifications/WoningtypeBestaand/V/meergezinspand_hoog
																							: WoningtypeSnapshot_rel[classifications/WoningtypeBestaand];
		attribute<uint8>                                  nr_verdiepingen             := pand_rel -> import/pand/nr_verdiepingen;
		attribute<typeringen/gebruiksdoel>                gebruiksdoel                := const(Invoer/RuimtelijkeData/BAG/typeringen/gebruiksdoel/V/woon,.);
		attribute<Studiegebied/buurt>                     buurt_rel                  := point_in_polygon(geometry, Studiegebied/buurt/geometry);
		
		container labelsprong_opgelegd := 
			for_each_nedv(
				classifications/ZichtJaar/name
				,'rjoin(identificatie, energielabel/opgelegd/identificatie, energielabel/opgelegd/'+classifications/ZichtJaar/name+')'
				, .
				, string
			);
	}

	container Tabel_Woningtype_Bouwjaar := 
		for_each_nedv(
			classifications/BouwjaarWoningBestaand/name
			,'sum(vbo_woonfunctie_studiegebied/BouwjaarWoningBestaand_rel == ' + string(id(classifications/BouwjaarWoningBestaand)) + '[Classifications/BouwjaarWoningBestaand] ? 1 : 0  ,vbo_woonfunctie_studiegebied/WoningtypeBestaand_rel)'
			,classifications/WoningtypeBestaand
			,uint32
		);

	unit<uint32> vbo_woonfunctie_studiegebied_export := select_with_attr_by_cond(import/vbo,
			import/vbo/gebruiksdoelen/woon
		&& Studiegebied/selected_per_gemeente[import/vbo/gemeente_rel]
		&& IsDefined(import/vbo/pand_rel)
		&& import/vbo/oppervlak_filters/wonen >= 10[Units/m2]
		&& import/vbo/oppervlak_filters/wonen < 1000[Units/m2]
		)
	,	DialogType = "Map"
	,	DialogData = "locatie"
	{
		attribute<rdc_meter> locatie       := geometry;
	}

	parameter<string> vbo_utiliteit_industrie_sub_expr := 
		  SpecifiekeInstellingen/BAG/IndustrieDoetMee == 0b ? '!import/vbo/gebruiksdoelen/Industrie'
		: SpecifiekeInstellingen/BAG/IndustrieDoetMee == 1b ? 'import/vbo/gebruiksdoelcombinatie <> 8w'
		: SpecifiekeInstellingen/BAG/IndustrieDoetMee == 2b ? 'true' 
		: 'unknown_IndustrieDoetMee_code';

	unit<uint32> vbo_utiliteit_studiegebied := ='select_with_org_rel('
			'!import/vbo/gebruiksdoelen/woon '
		' && Studiegebied/selected_per_gemeente[import/vbo/gemeente_rel]'
		' && IsDefined(import/vbo/pand_rel)'
		' && IsDefined(import/vbo/oppervlak_filters/utiliteit)'
		' && '+ vbo_utiliteit_industrie_sub_expr
		+')'
	,	DialogType = "Map"
	,	DialogData = "locatie"
	{
		attribute<uint64>                               vbo_bag_nr            := import/vbo/vbo_bag_nr[org_rel];
		attribute<string>                               label                 := import/vbo/label[org_rel];
		attribute<string>                               postcode              := import/vbo/postcode[org_rel];
		attribute<rdc_meter>                            geometry              := import/vbo/geometry[org_rel];
		attribute<import/pand>                          pand_rel              := org_rel -> pand_rel;
		attribute<rdc_meter>                            locatie               := geometry;
		attribute<Units/m2>                             oppervlakte           := import/vbo/oppervlak_filters/utiliteit[org_rel];
		attribute<units/yr>                             bouwjaar              := import/vbo/bouwjaar[org_rel];

		// attribute<string>                               pand_id                  := import/vbo/pand_id[nr_OrgEntity];
		// attribute<import/pand>                          pand_rel                 := import/vbo/pand_rel[nr_OrgEntity];
		attribute<Studiegebied/buurt>                   buurt_rel                := point_in_polygon(geometry, Studiegebied/buurt/geometry);

		attribute<Classifications/BouwjaarUtilBestaand> BouwjaarUtilBestaand_rel := isNull(bouwjaar) ? Classifications/BouwjaarUtilBestaand/V/onbekend :classify(bouwjaar,Classifications/BouwjaarUtilBestaand/ClassBreak);

		container gebruiksdoelen :=
			for_each_nedv(
				Classifications/UtilTypeBestaand/name
				,'import/vbo/gebruiksdoelen/' + Classifications/UtilTypeBestaand/name + '[org_rel]'
				, vbo_utiliteit_studiegebied
				, bool
			);

		container gebruiksdoel_impl: IsHidden = "True" // op basis van de gemiddeld grootste utiliteit
		{
			container Oppervlaktes_gemiddeld_per_vbo :=
				for_each_nedv(
					Classifications/UtilTypeBestaand/name
					,'MakeDefined(sum(gebruiksdoelen/' + Classifications/UtilTypeBestaand/name + ' ? oppervlakte[m2] : 0 [m2]) / sum(float64(gebruiksdoelen/' + Classifications/UtilTypeBestaand/name + ')), 0[m2])' 
					, void
					, m2
				);

			attribute<m2>    oppervlakte_gemiddeld_per_vbo (Classifications/UtilTypeBestaand) := ='union_data(Classifications/UtilTypeBestaand, '+  AsItemList('Oppervlaktes_gemiddeld_per_vbo/' + Classifications/UtilTypeBestaand/name) + ')';
			attribute<uint8> gebruiksdoel_index_calc       (Classifications/UtilTypeBestaand) := index(-oppervlakte_gemiddeld_per_vbo) // 0: index van hoogste oppervlakte, 1: index van een na hoogste oppervlakte, etc.
			{
				attribute<string> name (Classifications/UtilTypeBestaand) := Classifications/UtilTypeBestaand/name[gebruiksdoel_index];
			}
			attribute<uint8> gebruiksdoel_index            (Classifications/UtilTypeBestaand) : [9,4,5,7,2,0,6,1,3,8];// 0: index van hoogste oppervlakte, 1: index van een na hoogste oppervlakte, etc. Berekende index is losgekoppeld
			attribute<string> name                         (Classifications/UtilTypeBestaand) := Classifications/UtilTypeBestaand/name[gebruiksdoel_index];
		}

		attribute<Classifications/UtilTypeBestaand> gebruiksdoel (vbo_utiliteit_studiegebied) := ='gebruiksdoel_impl/gebruiksdoel_index[argmax('+ AsItemList('gebruiksdoelen/' + gebruiksdoel_impl/name) + ')[Classifications/UtilTypeBestaand]]';
	}

	container Tabel_Gebruiksdoel_BouwjaarUtil := for_each_nedv(
			classifications/BouwjaarUtilBestaand/name
		,	'sum(vbo_utiliteit_studiegebied/BouwjaarUtilBestaand_rel == ' + string(id(classifications/BouwjaarUtilBestaand)) + '[Classifications/BouwjaarUtilBestaand] ? vbo_utiliteit_studiegebied/oppervlakte : 0[Units/m2]  ,vbo_utiliteit_studiegebied/gebruiksdoel)'
		,	classifications/UtilTypeBestaand 
		,	Units/m2
	);

	unit<uint32> vbo_utiliteit_studiegebied_export:= select_with_org_rel(
			!import/vbo/gebruiksdoelen/woon 
		&&	Studiegebied/selected_per_gemeente[import/vbo/gemeente_rel]
		&&	IsDefined(import/vbo/pand_rel)
		&&	IsDefined(import/vbo/oppervlak_filters/utiliteit)
	//	&&	!import/vbo/gebruiksdoelen/Industrie    // Deze regel aanzetten als in Utiliteit alle industrie niet meedoet (dus ook alle vbo die industrie in combinatie met een andere gebruikdoel hebben)
		&&	import/vbo/gebruiksdoelcombinatie <> 8w // Deze regel aanzetten als in Utiliteit vbo's niet meedoen die alleen een industriefunctie hebben.
		)
	,	DialogType = "Map"
	,	DialogData = "locatie"
	{
		attribute<string>                                identificatie           := import/vbo/identificatie[org_rel];
		attribute<string>                                id                      := import/vbo/identificatie[org_rel];
		attribute<rdc_meter>                             locatie                 := import/vbo/geometry[org_rel];
	}

	unit<uint32> gebruiksdoelcombinatie := unique(import/vbo/gebruiksdoelcombinatie), IsHidden = "True"
	{
		attribute<string>   label       := =AsList('(bitand(Values, uint16('+string(typeringen/gebruiksdoel/impl/BitValue)+')) != 0w ? '''+typeringen/gebruiksdoel/name+'''+''_'' : '''')','+');
		attribute<uint32>   count       := pcount(rlookup(import/vbo/gebruiksdoelcombinatie, values));
		attribute<units/m2> oppervlakte := sum(import/vbo/oppervlak_filters/wonen ,rlookup(import/vbo/gebruiksdoelcombinatie, values));
	}
}
