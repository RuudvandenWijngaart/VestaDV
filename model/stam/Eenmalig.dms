//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door stam.dms                                  //
//       In deze configuratiefile worden items voor de boekhouding geconfigureerd       //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container eenmalig: Using = "Units;Geography", KeepData = "True", isHidden = "True"
{
	unit<uint32> mutatieSchema: nrofrows = 20
	{
		parameter<uint32> nrAttr := 5;
		unit<uint32> Elements := Range(uint32, 0, nrAttr * #.)
		{
			// Factors
			parameter<string> PRR   := 'Bebouwing/@BC@/PlanRegio_rel';
			parameter<string> VAT_F := '(Invoer/Kengetallen/BTW/BTW[Zichtjaar_rel] / 100[percent])';

			attribute<string> Text := union_data(.
				// name //leverancier //afnemer  
					// item 
					// factor 
					//left-entity-rel // right-entity-rel

		// Investeringen in vervangen gasleidingen, betaling van [GasNet] aan [Netwerk]
			,	'Ki_vervangen', 'Netwerk', 'GasNet',
					'Netwerken/Aardgas/Ki_vervangen', ''
					
		// Investeringen in verwijderen gasleidingen, betaling van [GasNet] aan [Netwerk]
			,	'Ki_verwijderen', 'Netwerk', 'GasNet',
					'Netwerken/Aardgas/Ki_verwijderen', ''
					
		// Investeringen in verzwaren elektriciteitsnet, betaling van [ElektriciteitsNet] aan [Netwerk]
			,	'Ki_verzwaren', 'Netwerk', 'ElektriciteitsNet',
					'Netwerken/Elektriciteit/Ki_verzwaren', ''		

		// Investering aansluitingskosten nieuwbouw op elektriciteitsnet, betaling van [gebouweigenaar] aan [elektriciteitsnet]
			,	'Ki_Asl_Enet', 'ElektriciteitsNet', '@BC@/Eigenaar',
					'Allocatie/@BC@/BO/eenmalig/Ki_Asl_Enet', PRR
					
		// Investering aansluitingskosten nieuwbouw op elektriciteitsnet, betaling van [ElektriciteitsNet] aan [Netwerk]
			,	'Aansluitbijdrage_E', 'Netwerk', 'ElektriciteitsNet',
					'Netwerken/Elektriciteit/Oi_AansluitBijdrage', ''

		// Investering aansluitingskosten nieuwbouw op gasnet, betaling van [gebouweigenaar] aan [gasnet]
			,	'Ki_Asl_Gnet', 'GasNet', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki_Asl_Gnet', PRR
		
		// Investering aansluitingskosten nieuwbouw op gasnet, betaling van [GasNet] aan [Netwerk]
			,	'Aansluitbijdrage_G', 'Netwerk', 'GasNet', 
					'Netwerken/Aardgas/Oi_AansluitBijdrage', ''
					
		// Investering lokale opwekkingsinstallaties met looptijd van 30 jaar, betaling van [gebouweigenaar] aan [lokaleopwekking]
			,	'Ki30_LO', 'LokaleOpwekking', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki30_LO', PRR

		// Investering lokale opwekkingsinstallaties met looptijd van 20 jaar, betaling van [gebouweigenaar] aan [lokaleopwekking]
			,	'Ki20_LO', 'LokaleOpwekking', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki20_LO', PRR

		// Investering lokale opwekkingsinstallaties met looptijd van 15 jaar, betaling van [gebouweigenaar] aan [lokaleopwekking]
			,	'Ki15_LO', 'LokaleOpwekking', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki15_LO', PRR

		// Investering in afgiftesystemen in gebouwen, betaling van [gebouweigenaar] aan [lokaleopwekking]
			,	'Ki_LTAS', 'LokaleOpwekking', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki_LTAS', PRR

		// BTW over investeringen in lokale opwekkingsinstallaties, betaling van [gebouweigenaar] aan [BTW]
			,	'LokaleOpwekking_BTW', 'BTW', '@BC@/Eigenaar', 
					'(Allocatie/@BC@/BO/eenmalig/Ki30_LO
					+ Allocatie/@BC@/BO/eenmalig/Ki20_LO
					+ Allocatie/@BC@/BO/eenmalig/Ki15_LO
					+ Allocatie/@BC@/BO/eenmalig/Ki_LTAS)
					* '+VAT_F, 'Bebouwing/@BC@/PlanRegio_rel'

		// Investering in schilverbetering bij gebouwen, betaling van [gebouweigenaar] aan [Gebouw]
			,	'Gebouwverbetering', 'Gebouw', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki_GV', PRR

		// BTW over investeringen in lokale schilverbetering bij gebouwen, betaling van [gebouweigenaar] aan [BTW]
			,	'Gebouwverbetering_BTW', 'BTW', '@BC@/Eigenaar', 
					'Allocatie/@BC@/BO/eenmalig/Ki_GV * (Invoer/Kengetallen/BTW/BTW_gv[Zichtjaar_rel] / 100[percent])', 'Bebouwing/@BC@/PlanRegio_rel'

		// Subsidie voor investeringen in lokale opwekkingsinstallaties met looptijd van 30 jaar, betaling van [Subsidie] aan [gebouweigenaar]
			,	'Oi_s_LO_30', '@BC@/Eigenaar', 'Subsidie',
					'Allocatie/@BC@/BO/eenmalig/Oi30_LO', PRR
			
		// Subsidie voor investeringen in lokale opwekkingsinstallaties met looptijd van 20 jaar, betaling van [Subsidie] aan [gebouweigenaar]
			,	'Oi_s_LO_20', '@BC@/Eigenaar', 'Subsidie',
					'Allocatie/@BC@/BO/eenmalig/Oi20_LO', PRR

		// Subsidie voor investeringen in lokale opwekkingsinstallaties met looptijd van 15 jaar, betaling van [Subsidie] aan [gebouweigenaar]
			,	'Oi_s_LO_15', '@BC@/Eigenaar', 'Subsidie',
					'Allocatie/@BC@/BO/eenmalig/Oi15_LO', PRR
		
		// Subsidie voor investeringen in gebouwverbetering, betaling van [Subsidie] aan [gebouweigenaar]
			,	'Oi_s_GV', '@BC@/Eigenaar', 'Subsidie',
					'Allocatie/@BC@/BO/eenmalig/Oi_GV', PRR

		// Aansluitbijdrage aan Leverancier WijkDistributie, betaling van [gebouweigenaar] aan [Leverancier]
			,	'Oi_lv_Aansl_@RO@', 'Leverancier', '@BC@/Eigenaar',
					'Allocatie/@BC@/BO/eenmalig/Ki_Asl_@RO@', PRR
					
		// BTW over Aansluitbijdrage aan Leverancier WijkDistributie, betaling van [gebouweigenaar] aan [BTW]
			,	'Oi_lv_Aansl_@RO@_BTW', 'BTW', '@BC@/Eigenaar',
					'Allocatie/@BC@/BO/eenmalig/Ki_Asl_@RO@ * '+VAT_F, 'Bebouwing/@BC@/PlanRegio_rel'
			);
		}
		
			// NB: de boekingen uit Classifications/GebiedsOptie_eenmalig worden geboekt als xxx obv StateNaAllocatie/Cumulaties/@RO@/eenmalig/xxx

		attribute<string> name         := Elements/Text[value(id(.)* nrAttr + 0, Elements)];
		attribute<string> Leverancier  := Elements/Text[value(id(.)* nrAttr + 1, Elements)];
		attribute<string> Afnemer      := Elements/Text[value(id(.)* nrAttr + 2, Elements)];
		attribute<string> Expr         := Elements/Text[value(id(.)* nrAttr + 3, Elements)];
		attribute<string> PlanRegioRel := Elements/Text[value(id(.)* nrAttr + 4, Elements)];
		attribute<string> FullDescr    := 
				name + ': ' + Leverancier + ' -> ' + Afnemer +
				' = ' + Expr 
				+ (PlanRegioRel == '' ? '' : ' per '+PlanRegioRel);
	}

	// TODO_OV: Checken dat amounts wel opgesplitst zijn per BTW component
	container applyBTW := CalculationSchemes/Payments/substitute(mutatieSchema, '@BTW@', Classifications/BtwComponent);

	unit<uint32> mutatie := applyBTW/result;
}
