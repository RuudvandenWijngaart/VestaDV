//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template Flow: using = "Classifications"
{
	// begin case parameters
	unit<uint32>               RapportageRegio;
	parameter<string>          ReportRegioNaam; //: Expr = "'NL'";
	parameter<RapportageRegio> RapportageRegio_sel;
	// end case parameters

	parameter<string> ReportFolder := FolderInfo/OutputFolder+'/'+ReportRegioNaam, IsHidden = "True";

	attribute<RapportageRegio> PlanRegioRapportageRegio_rel (Invoer/SpecifiekeInstellingen/PlanRegio) := ='PlanRegio/'+RapportageRegio/RegioNaam + '_rel', IsHidden = "True"; //id(Invoer/SpecifiekeInstellingen/PlanRegio);  //
	attribute<float64>         PlanRegioInRapportageFactor  (Invoer/SpecifiekeInstellingen/PlanRegio) := float64(PlanRegioRapportageRegio_rel == RapportageRegio_sel), IsHidden = "True";

	container Ongewogen := Components('All', PlanRegioInRapportageFactor);

	container NaarInkomensGroep
	{
		container Inkomen := Invoer/RuimtelijkeData/StudieGebied/buurt/Inkomen;
		container laag    := Components('InkLaag',   PlanRegioInRapportageFactor * Inkomen/p_hh_li / Inkomen/p_checksum);
		container midden  := Components('InkMidden', PlanRegioInRapportageFactor * Inkomen/p_hh_mi / Inkomen/p_checksum);
		container hoog    := Components('InkHoog',   PlanRegioInRapportageFactor * Inkomen/p_hh_hi / Inkomen/p_checksum);

		parameter<string> GenerateAll := 'Ready', ExplicitSuppliers = "laag/GenerateAll;midden/GenerateAll;hoog/GenerateAll";
	}

	container NaarEigendom
	{
		container Eigendom := Invoer/RuimtelijkeData/StudieGebied/buurt/Eigendom;
		container koop     := Components('HK_kw',   PlanRegioInRapportageFactor * Eigendom/p_koopw  / Eigendom/p_checksum);
		container wcorpw   := Components('HK_kw',   PlanRegioInRapportageFactor * Eigendom/p_wcorpw / Eigendom/p_checksum);
		container ov_hw    := Components('HK_hw',   PlanRegioInRapportageFactor * Eigendom/p_ov_hw  / Eigendom/p_checksum);

		parameter<string> GenerateAll := 'Ready', ExplicitSuppliers = "koop/GenerateAll;wcorpw/GenerateAll;ov_hw/GenerateAll";
	}

	parameter<string> GenerateAll := 'Ready', ExplicitSuppliers = "Ongewogen/GenerateAll;NaarInkomensGroep/GenerateAll;NaarEigendom/GenerateAll";
}
