//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Netwerken: isHidden = "true"
	{
		attribute<bool> heeft_gas (PlanRegio) := TussenResultaten/AllocatieResultaten/heeft_gas;

		attribute<nrWoningen> nrWoonUnits     (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<nrWoningen> nrAllElectric   (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i * Allocatie/@BC@/Aandelen/AllElectric, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<float64>    R_all_electric  (Planregio) := makedefined(nrAllElectric / nrWoonUnits, 0.0);
		attribute<bool >      all_electric    (Planregio) := R_all_electric > 0.999, // tolereer afronding fouten en kleine afwijkingen
			IntegrityCheck = "!(heeft_gas && all_electric)";

		container aardgas
		{
			attribute<bool>		had_gas					(Planregio)	:=	/TussenResultaten/StartJaar/AllocatieResultaten/heeft_gas, Descr = "true indien deze planregio in de startsituatie een aardgasnet had";
			attribute<bool>		verwijderd				(Planregio)	:=	had_gas && !heeft_gas, Descr = "true indien deze buurt in de startsituatie een gasnet had dat nu is verwijderd";

			attribute<m>		L_infra_nu				(PlanRegio)	:=	float64(heeft_gas ) * StateNaAllocatie/KostenBaten/KostenD/L_infra_g;
			attribute<m>		L_infra_org				(PlanRegio)	:=	StartJaar/netwerken/aardgas/L_infra_nu;
			attribute<m>		L_infra					(PlanRegio)	:=	max_elem(L_infra_nu, L_infra_org);
			
			attribute<Eur_yr>	Oj_netwerkbijdrage		(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Aardgas/netwerk'  )+')';
			attribute<Eur_yr>	Oj_vastrecht			(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Vastrecht')+')';
			attribute<Eur_yr>	Oji_aansluitbijdrage	(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Aansl_m'  )+')';
			attribute<Eur_yr>	Oj_totaal				(Planregio)	:=	Oj_netwerkbijdrage + Oj_vastrecht + Oji_aansluitbijdrage;

			attribute<Eur_yr>	Kji_infra				(Planregio)	:=	L_infra * Kengetallen/Infra/Kj_g_m;
			attribute<Eur_yr>	Kj_oh					(Planregio)	:=	L_infra_nu * Kengetallen/Infra/Ko_g_m;
			attribute<Eur>		Ki_vervangen			(Planregio)	:=	kosten/cumulatief/Ki_verv_LD_net;
			attribute<Eur>		Ki_verwijderen			(Planregio)	:=	kosten/cumulatief/Ki_verw_g_aansl + kosten/cumulatief/Ki_verw_LD_net;
			attribute<Eur_yr>	Kji_vervangen			(Planregio)	:=	Ki_vervangen * NCW/mr50/AnnualisationFactor;
			attribute<Eur_yr>	Kji_verwijderen			(Planregio)	:=	Ki_verwijderen * NCW/mr50/AnnualisationFactor;
			attribute<Eur_yr>	Kj_totaal				(Planregio)	:=	Kji_vervangen + Kji_verwijderen + Kj_oh;
			attribute<Eur_yr>	Kj_netto				(Planregio)	:=	Kj_totaal - Oj_totaal;
		}
		container elektriciteit
		{
			attribute<m>		L_infra					(PlanRegio)	:=	StateNaAllocatie/KostenBaten/KostenD/L_infra_e;
			
			attribute<Eur_yr>	Oj_netwerkbijdrage		(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Elektriciteit/netwerk'  )+')';
			attribute<Eur_yr>	Oj_vastrecht			(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Vastrecht')+')';
			attribute<Eur_yr>	Oji_aansluitbijdrage	(Planregio)	:=	='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Aansl_m'  )+')';
			attribute<Eur_yr>	Oj_totaal				(Planregio)	:=	Oj_netwerkbijdrage + Oj_vastrecht + Oji_aansluitbijdrage;

			attribute<Eur_yr>	Kji_infra				(Planregio)	:=	L_infra * Kengetallen/Infra/Kj_e_m;
			attribute<Eur_yr>	Kj_oh					(Planregio)	:=	L_infra * Kengetallen/Infra/Ko_e_m;
			attribute<Eur>		Ki_verzwaren			(Planregio)	:=	kosten/cumulatief/Ki_verzw_e_aansl + kosten/cumulatief/Ki_verzw_MSR + kosten/cumulatief/Ki_verzw_LS;
			attribute<Eur_yr>	Kji_verzwaren			(Planregio)	:=	Ki_verzwaren * NCW/mr50/AnnualisationFactor;
			attribute<Eur_yr>	Kj_totaal				(Planregio)	:=	Kji_infra + Kji_verzwaren + Kj_oh;
			attribute<Eur_yr>	Kj_netto				(Planregio)	:=	Kj_totaal - Oj_totaal;
		}
		
		container kosten
		{
			attribute<Eur>		Ki_verv_LD_net			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verv_LD_net;
			attribute<Eur>		Ki_verw_g_aansl_laagb	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verw_g_aansl_laagb;
			attribute<Eur>		Ki_verw_g_aansl_hoogb	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verw_g_aansl_hoogb;
			attribute<Eur>		Ki_verw_g_aansl_util	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verw_g_aansl_util;
			attribute<Eur>		Ki_verw_g_aansl			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verw_g_aansl;
			attribute<Eur>		Ki_verw_LD_net			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verw_LD_net;
			attribute<Eur>		Ki_verzw_e_aansl		(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verzw_e_aansl;
			attribute<Eur>		Ki_verzw_MSR			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verzw_MSR;
			attribute<Eur>		Ki_verzw_LS				(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/kosten_verzw_LS;
				
			container cumulatief
			{
				attribute<Eur>	Ki_verv_LD_net			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verv_LD_net;
				attribute<Eur>	Ki_verw_g_aansl_laagb	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_laagb;
				attribute<Eur>	Ki_verw_g_aansl_hoogb	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_hoogb;
				attribute<Eur>	Ki_verw_g_aansl_util	(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_util;
				attribute<Eur>	Ki_verw_g_aansl			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl;
				attribute<Eur>	Ki_verw_LD_net			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_LD_net;
				attribute<Eur>	Ki_verzw_e_aansl		(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_e_aansl;
				attribute<Eur>	Ki_verzw_MSR			(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_MSR;
				attribute<Eur>	Ki_verzw_LS				(PlanRegio)	:=	StateNaAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_LS;
			}
		}
	}