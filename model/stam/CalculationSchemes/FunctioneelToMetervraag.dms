//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

Template FunctioneelToMetervraag
{
	unit<uint32> VraagBO;
	parameter<bool> terAfweging;
	
	container SPF := for_each_nedv(Classifications/Product/name
			, replace('Classifications/Performance/SPF_@S[PerformancePerProduct/@P]', '@P', Classifications/Product/name, '@S', Classifications/Product/ServiceName)
			, VraagBO
			, float64
		), ishidden = "True"
	{	
		attribute<Classifications/SchillabelEx> SchillabelEx_rel (VraagBO) := Classifications/Schillabel/SchillabelEx_rel[VraagBO/Schillabel_rel];

		 container Performance_key1 := for_each_nedv(Classifications/Product/name
			, 'combine_data(classifications/ISP, VraagBO/InstallatiePerProduct/'+Classifications/Product/name+
				', combine_data(classifications/SP, Classifications/Schillabel/SchillabelEx_rel[VraagBO/Schillabel_rel], Classifications/ProductType/ProductTypeEx_rel[Classifications/ProductType/V/'+Classifications/Product/TypeName+']))'
			, VraagBO
			, classifications/ISP
			)
		 , isHidden = "True";	

		 container Performance_key2 := for_each_nedv(Classifications/Product/name
			, 'combine_data(classifications/ISP, VraagBO/InstallatiePerProduct/'+Classifications/Product/name+
				', combine_data(classifications/SP, Classifications/SchilLabelEx/V/x, Classifications/ProductType/ProductTypeEx_rel[Classifications/ProductType/V/'+Classifications/Product/TypeName+']))'
			, VraagBO
			, classifications/ISP
			)
		 , isHidden = "True";
		 
		container PerformancePerProduct := for_each_nedv(Classifications/Product/name
			, replace(
				'MakeDefined('
					'Performance_key1/@P->Performance_rel, '
					'Performance_key2/@P->Performance_rel, '
					'Classifications/ISP/V/geen_x_@T->Performance_rel'
				')'
			  , '@P', Classifications/Product/name
			  , '@T', Classifications/Product/typename)
			, VraagBO
			, Classifications/Performance
			);
	}
	
	container ZonB : ishidden = "True"
	{
		attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_rel (VraagBO) := =not(terAfweging) ? 'VraagBO/InstallatiePerProduct/DK' : 'OverigeInstallaties/DK/Installatietype_def';
			
		attribute<GJ_yr> Oj_TW     (VraagBO) := 
		   Installatietype_rel == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/zonb
		|| Installatietype_rel == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/optimaal_zonb ? 
		   0.5 * VraagBO/Functioneel/TW : 0.0[GJ_yr];

		attribute<m2>    area      (VraagBO) := makedefined(Oj_TW / Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Opbr_opp_tw[Installatietype_rel], 0.0[m2]);
	}
	
	container Functioneel_per_product
	{
		attribute<float64> RVb (VraagBO) := ='VraagBO/Functioneel/RV *        Classifications/Performance/P_Vol[SPF/PerformancePerProduct/RVb] ';
		attribute<float64> RVp (VraagBO) := ='VraagBO/Functioneel/RV * (1.0 - Classifications/Performance/P_Vol[SPF/PerformancePerProduct/RVb])';
		attribute<float64> TWb (VraagBO) := ='(VraagBO/Functioneel/TW - ZonB/Oj_TW) *        Classifications/Performance/P_Vol[SPF/PerformancePerProduct/TWb] ';
		attribute<float64> TWp (VraagBO) := ='(VraagBO/Functioneel/TW - ZonB/Oj_TW) * (1.0 - Classifications/Performance/P_Vol[SPF/PerformancePerProduct/TWb])';
		attribute<float64> KDb (VraagBO) := ='VraagBO/Functioneel/KD *        Classifications/Performance/P_Vol[SPF/PerformancePerProduct/KDb] ';
		attribute<float64> KDp (VraagBO) := ='VraagBO/Functioneel/KD * (1.0 - Classifications/Performance/P_Vol[SPF/PerformancePerProduct/KDb])';
	}
	
	container metervraag_per_product := for_each_nedv(Classifications/Product/name
		, replace('Functioneel_per_product/@P / SPF/@P', '@P', Classifications/Product/name)
		, VraagBO
		, float64
	);

	container BemeterdeGebouwInput_rel := for_each_nedv(Classifications/Product/name
		, 'rlookup(Classifications/Performance/Input_rel, Classifications/BemeterdeGebouwInput/org_rel)[SPF/PerformancePerProduct/'+Classifications/Product/name+']'
		, VraagBO
		, Classifications/BemeterdeGebouwInput
	);
	
	container Expr_impl : ishidden = "True" 
	{
		attribute<string> MeterVraagExpr_asVector(Classifications/Product) := 'metervraag_base/' + Classifications/Product/name;

		attribute<string> aggr_OudLabel(Classifications/BemeterdeGebouwInput) := 
			replace('add('++AsItemList(replace('BemeterdeGebouwInput_rel/@P == Classifications/BemeterdeGebouwInput/V/@I ? metervraag_per_product/@P: 0[GJ_yr]', '@P', Classifications/Product/name))+')'
			, '@I', Classifications/BemeterdeGebouwInput/name
			);
	}

	container metervraag_per_input := for_each_nedv(Classifications/BemeterdeGebouwInput/name
		, Expr_impl/aggr_OudLabel
		, VraagBO
		, GJ_yr);
	
	container Koken
	{
		attribute<Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes> Installatietype_rel (VraagBO) := VraagBO/InstallatiePerProduct/KK;
			
		attribute<Classifications/BemeterdeGebouwInput> BemeterdeGebouwInput_rel (VraagBO) := Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes/BemeterdeGebouwInput_rel [Installatietype_rel], isHidden = "True";
		attribute<ratio> SPF     (VraagBO) := Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes/SPF[Installatietype_rel];
		attribute<GJ_yr> Vj_elek (VraagBO) := float64(BemeterdeGebouwInput_rel == Classifications/BemeterdeGebouwInput/V/e)  * (VraagBO/functioneel/KK / SPF);
		attribute<GJ_yr> Vj_gas  (VraagBO) := float64(BemeterdeGebouwInput_rel ==Classifications/BemeterdeGebouwInput/V/Gas) * (VraagBO/functioneel/KK / SPF);
	}

	container Omgeving := for_each_nedv(
		  Classifications/Product/name
		, replace('max_elem( 0.0[GJ_yr], Functioneel_per_product/@P - metervraag_per_product/@P)','@P',Classifications/Product/name)
		, VraagBO
		, GJ_yr		 )
	{
		attribute<GJ_yr> Totaal (VraagBO) := ='add('+AsItemList(Classifications/Product/name)+')';
	}

	container eEffect
	{
		container volume := for_each_nedv(Classifications/Product/name
			, replace(
				'Classifications/Performance/eEffect_vol[SPF/PerformancePerProduct/@P] * metervraag_per_product/@P'
				, '@P'
				, Classifications/Product/name
			  )
			, VraagBO
			, GJ_yr
		)
		{
			attribute<GJ_yr> Vj_elek (VraagBO) := ='add('+AsItemList(Classifications/Product/name)+')';
		}
		container CapaciteitPerProduct 
		{				
			attribute<kW> RVb (VraagBO) := VraagBO/AansluitCapaciteit/RV * Classifications/Performance/P_cap[SPF/PerformancePerProduct/RVb];
			attribute<kW> TWb (VraagBO) := VraagBO/AansluitCapaciteit/TW * Classifications/Performance/P_cap[SPF/PerformancePerProduct/TWb];
			attribute<kW> KDb (VraagBO) := VraagBO/AansluitCapaciteit/KD * Classifications/Performance/P_cap[SPF/PerformancePerProduct/KDb];
			attribute<kW> RVp (VraagBO) := VraagBO/AansluitCapaciteit/RV - RVb;
			attribute<kW> TWp (VraagBO) := VraagBO/AansluitCapaciteit/TW - TWb;
			attribute<kW> KDp (VraagBO) := VraagBO/AansluitCapaciteit/KD - KDb;
		}
		container CapaciteitPerInstallatie := for_each_nedv(Classifications/Installatie/name
		, replace('max_elem('
				'add(CapaciteitPerProduct/RVb * float64(VraagBO/InstallatiePerProduct/RVb == @VI), CapaciteitPerProduct/RVp * float64(VraagBO/InstallatiePerProduct/RVp == @VI)), '
				'add(CapaciteitPerProduct/TWb * float64(VraagBO/InstallatiePerProduct/TWb == @VI), CapaciteitPerProduct/TWp * float64(VraagBO/InstallatiePerProduct/TWp == @VI)), '
				'add(CapaciteitPerProduct/KDb * float64(VraagBO/InstallatiePerProduct/KDb == @VI), CapaciteitPerProduct/Kdp * float64(VraagBO/InstallatiePerProduct/Kdp == @VI)) '
			')[kW]'
			, '@VI', 'Classifications/Installatie/V/' + Classifications/Installatie/name)
		, VraagBO
		, kW
		);
		container capaciteit := for_each_nedv(Classifications/Installatie/name
		, replace(
			'add('+
				AsItemList(
					replace('Classifications/Performance/eEffect_cap[SPF/PerformancePerProduct/@P] * float64(VraagBO/InstallatiePerProduct/@P == Classifications/Installatie/V/@I)'
					,	'@P', Classifications/Product/name
					)
				)+
			') * CapaciteitPerInstallatie/@I'
			, '@I', Classifications/Installatie/name)
			, VraagBO
			, GJ_yr
		)
		{
			attribute<GJ_yr> Vj_elek(VraagBO) := ='add('+AsItemList(Classifications/Installatie/name)+')';
		}
		
		container ventilatie
		{
			attribute<GJ_yr> Vj_elek (VraagBO) := VraagBO/functioneel/VT;
		}
		
		container ZonPV
		{
			attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_rel (VraagBO) := VraagBO/InstallatiePerProduct/DK;
			
			attribute<m2> Area_DP (VraagBO) := 0.69 * VraagBO/Bouwdelen/DP * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/DakaandeelZonPV[Installatietype_rel];
			attribute<m2> Area_DS (VraagBO) := 0.69 * VraagBO/Bouwdelen/DS * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/DakaandeelZonPV[Installatietype_rel];
			attribute<m2> Area    (VraagBO) := Area_DP + Area_DS;
			attribute<kW> P_DP    (VraagBO) := Area_DP * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Pdak_opp  [Installatietype_rel];
			attribute<kW> P_DS    (VraagBO) := Area_DS * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Pdak_opp  [Installatietype_rel];

			attribute<GJ_yr> Oj_e_DP (VraagBO) := P_DP * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Opbr_cap_e_DP[Installatietype_rel];
			attribute<GJ_yr> Oj_e_DS (VraagBO) := P_DS * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Opbr_cap_e_DP[Installatietype_rel];
			attribute<GJ_yr> Oj_elek (VraagBO) := Oj_e_DP + Oj_e_DS;
		}
			
		attribute<GJ_yr> Vj_elek (VraagBO) := volume/Vj_elek + capaciteit/Vj_elek + ventilatie/Vj_elek - ZonPV/Oj_elek;
	}
	
	container result := metervraag_per_input
	{
		attribute<GJ_yr> e     (VraagBO) := metervraag_per_input/e + VraagBO/Functioneel/EA + eEffect/Vj_elek + koken/Vj_elek;
		attribute<GJ_yr> gas   (VraagBO) := metervraag_per_input/gas + Koken/Vj_gas    ;
		attribute<GJ_yr> Total (VraagBO) := ='add('+AsItemList(Classifications/BemeterdeGebouwInput/name)+')';

		attribute<GJ_yr> wv_e_koude (VraagBO) := add(BemeterdeGebouwInput_rel/KDb == Classifications/BemeterdeGebouwInput/V/e ? metervraag_per_product/KDb : 0[ GJ_yr ], BemeterdeGebouwInput_rel/KDp == Classifications/BemeterdeGebouwInput/V/e ? metervraag_per_product/KDp : 0[ GJ_yr ]);
	}
}