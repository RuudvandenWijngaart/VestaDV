//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template KostenWoningen
{
	// begin case parameter
	container       BCdata;
	unit<uint32>    PlanRegio;
	parameter<bool> IsNieuwbouw;
	// end case parameters

	unit<uint32>			WoonObject						:=	BCdata/BebouwingsObject;
	attribute<float64>		pand_aandeel	(WoonObject)	:=	WoonObject/Pand_aandeel;
	attribute<bool>			IsMeergezins	(WoonObject)	:=	BCdata/BebouwingsTypeDomein/IsMeergezins[BCdata/BebouwingsType];
	
	// kosten aansluitleiding
	attribute<m_aansl>		L_aansl			(WoonObject)	:=	/Invoer/RuimtelijkeData/buislengte/per_buurt/L_aansl_pand[BCdata/PlanRegio_rel];
	container				LeidingKosten					:=	BuisKostenBC(WoonObject, P_aansl * 0.001[ MW / kW]);
	attribute<EUR_m>		K_aansl_m		(WoonObject)	:=	Leidingkosten/K_m;	
	attribute<Eur_Aansl>	Ki_wd_w			(WoonObject)	:=	L_aansl * K_aansl_m * pand_aandeel;

	// vermeden gasketel
	parameter<Eur_Aansl> Oi_vermeden_gasketel := Kostenkentallen/Ki_HR_ketel_W * NCW/bw15/AnnualisationFactor * NCW/bw30/DurationFactor;

	// eenmalige kosten, zie FO tabellen 5..7
	attribute<Eur_Aansl> Ki_id_fa (WoonObject) := IsMeergezins ? Schuiven/VerketelingMin * Kostenkentallen/Ki_id_metBlok + Schuiven/VerketelingMax * Kostenkentallen/Ki_id_geenBlok :  0[Eur_Aansl]; // inpandige leidingen gestapelde woningen
	parameter<Eur_Aansl> Ki_id_va              := 0[Eur_Aansl] * float64(!IsNieuwbouw); // extra inpandige leidingen nieuwbouw

	// kosten voor projectmanagement en door huurder ervaren en door eigenaar vergoedde ongerief
	attribute<Eur_Aansl> Ki_ge_ov_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_Aansl], WoonObject)' : 'IsMeergezins ? Schuiven/VerketelingMin *                    0[Eur_Aansl] + Schuiven/VerketelingMax * Kostenkentallen/K_Ov             : Kostenkentallen/K_Ov';             // woon-ongerief
	attribute<Eur_Aansl> Ki_ge_pm_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_Aansl], WoonObject)' : 'IsMeergezins ? Schuiven/VerketelingMin * Kostenkentallen/K_Pm_min + Schuiven/VerketelingMax * Kostenkentallen/K_Pm_max : Kostenkentallen/K_Pm_max'; // project management
	attribute<Eur>       Ki_ge_ov_o   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur], WoonObject)'       : 'NrDeelnemers * Ki_ge_ov_w';
	attribute<Eur>       Ki_ge_pm_o   (WoonObject) :=  NrDeelnemers * Ki_ge_pm_w;

	// huurverlaging, alleen gebruikt in Payments en als kostenfactor
	attribute<Eur_yr_Aansl> Kj_hv_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_yr_Aansl], WoonObject)' : '(IsMeergezins ? Schuiven/VerketelingMax : 1.0) * 160[Eur_yr_Aansl] * Schuiven/HuurVerlagingBijGebiedsOptie';// ?????
	attribute<Eur_yr>       Kj_hv_o   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_yr      ], WoonObject)' : 'Kj_hv_w* NrDeelnemers';

	attribute<nrAansl>	NrDeelnemers	(WoonObject)	:=	BCdata/WarmteVraag/deelnemers/nrAansluitingen    * float64(!Classifications/WarmteOptie/isGebiedsOptie[BCdata/WarmteAllocatie]);
	attribute<kW>		P_aansl			(WoonObject)	:=	BCdata/WarmteVraag/deelnemers/AansluitCapaciteit * float64(!Classifications/WarmteOptie/isGebiedsOptie[BCdata/WarmteAllocatie]);
	attribute<kw>		P_sec			(Planregio)		:=	sum(P_aansl / Kengetallen/Aansluitwaarde/Pfactor, BCdata/PlanRegio_rel);

	attribute<Eur>     Ki_ge_bruto    (WoonObject) := Ki_ge_ov_o + Ki_ge_pm_o - Oi_vermeden_gasketel * NrDeelnemers;
	attribute<Eur>     Oi_ge_subsidie (WoonObject) := Ki_ge_ov_o * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_OngeriefsVergoeding + Ki_ge_pm_o * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_ProjectManagement;
	attribute<Eur>     Ki_ge_netto    (WoonObject) := Ki_ge_bruto - Oi_ge_subsidie;
	attribute<Eur_yr>  Kj_ge_base     (WoonObject) := Ki_ge_netto * NCW/bw28/AnnualisationFactor + Kj_hv_o;
	attribute<Eur_yr>  Oe_ge_subsidie (WoonObject) := Kj_ge_base * SpecifiekeInstellingen/Beleid/EEA/ge;
	attribute<Eur_yr>  Oj_ge_subsidie (WoonObject) := Oi_ge_subsidie *NCW/bw28/AnnualisationFactor + Oe_ge_subsidie;
	attribute<Eur_yr>  Kj_ge_netto    (WoonObject) := Kj_ge_base - Oe_ge_subsidie;

	container Onderstation
	{
		attribute<kW>	P_vraag_blokverwarming (PlanRegio)	:=	P_sec * Schuiven/VerketelingMin;
		attribute<Eur>	Ki_wd_OS               (PlanRegio)	:=	P_sec * Kostenkentallen/K_OS;
		attribute<Eur>	Ki_wd_Vergoeding       (PlanRegio)	:=	P_vraag_blokverwarming * Kostenkentallen/KetelVergoeding;
	}

	attribute<Eur> Ki_wd_sec     (PlanRegio) := sum(Ki_wd_w*NrDeelnemers, BCdata/PlanRegio_rel);
	attribute<Eur> Ki_wd         (PlanRegio) := Ki_wd_sec + Onderstation/Ki_wd_OS + Onderstation/Ki_wd_Vergoeding;
	attribute<Eur> Ki_id         (PlanRegio) := sum((Ki_id_fa + Ki_id_va) * NrDeelnemers, BCdata/PlanRegio_rel);
	attribute<Eur> Ki_ge_ov      (PlanRegio) := sum(Ki_ge_ov_w * NrDeelnemers, BCdata/PlanRegio_rel) * float64(!IsNieuwbouw);
	attribute<Eur> Ki_ge_pm      (PlanRegio) := sum(Ki_ge_pm_w * NrDeelnemers, BCdata/PlanRegio_rel) * float64(!IsNieuwbouw);

	attribute<Eur_yr> Kj_hv      (PlanRegio) :=  =IsNieuwbouw?'const(0[Eur_yr      ], PlanRegio)':'sum(Kj_hv_o, BCdata/PlanRegio_rel)'; // huurverlaging, alleen gebruikt in flow en als kostenfactor
}