//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Rapportage
{
	#include <RegionaleRapportageTempl.dms>
	#include <FlowTempl.dms>
	#include <Components.dms>

	template EindgebruikerComponent
	{
		// begin case parameters
		container AllocatieResultatenComponent;
		container NCW15;
		container NCW30;
		container NCW50;
		attribute<Eur_yr> Oj_VastRecht_g (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur>    Oi_Aansluit_g  (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur_yr> Oj_VastRecht_e (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur>    Oi_Aansluit_e  (Invoer/SpecifiekeInstellingen/PlanRegio);
		container PlanRegioKaartComponent;
		// end case parameters

		container BCdata                    := AllocatieResultatenComponent/BCdata;
		container RuimtelijkeVraagComponent := AllocatieResultatenComponent/RuimtelijkeVraagComponent;
		unit<uint32> BebouwingsObject       := BCdata/BebouwingsObject;

		parameter<Ratio> BTW_Factor         := BCdata/BTW_Factor;
		parameter<Ratio> BTW_Factor_gv      := BCdata/BTW_Factor_gv;

		unit<uint32> PlanRegio              := SpecifiekeInstellingen/PlanRegio;

		container gebouwen
		{
			container Elektriciteit
			{
				attribute<Eur_yr> Verbruik       (PlanRegio) := sum(AllocatieResultatenComponent/Elektriciteit/eindgebruik_excl, BCdata/PlanRegio_rel);
				attribute<Eur_yr> netwerk        (PlanRegio) := sum(AllocatieResultatenComponent/Elektriciteit/netwerk,          BCdata/PlanRegio_rel);
				attribute<Eur_yr> EnergieHeffing (PlanRegio) := sum(AllocatieResultatenComponent/Elektriciteit/EnergieHeffing,   BCdata/PlanRegio_rel);
				attribute<Eur_yr> CO2Heffing     (PlanRegio) := sum(AllocatieResultatenComponent/Elektriciteit/CO2Heffing,       BCdata/PlanRegio_rel);

				attribute<Eur_yr> VastRecht      (PlanRegio) := Oj_VastRecht_e;
				attribute<Eur_yr> Aansl_j        (PlanRegio) := Oi_Aansluit_e * Ncw50/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m        (PlanRegio) := Oi_Aansluit_e * Ncw/mr50/AnnualisationFactor;

				attribute<Eur_yr> Vraag_excl     (PlanRegio) := Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m        (PlanRegio) := Verbruik + Vastrecht + Aansl_m - EnergieHeffing;
			}

			container AardGas
			{
				attribute<Eur_yr> Verbruik       (PlanRegio) := sum(AllocatieResultatenComponent/AardGas/eindgebruik_excl, BCdata/PlanRegio_rel);
				attribute<Eur_yr> netwerk        (PlanRegio) := sum(AllocatieResultatenComponent/AardGas/netwerk,          BCdata/PlanRegio_rel);
				attribute<Eur_yr> EnergieHeffing (PlanRegio) := sum(AllocatieResultatenComponent/AardGas/EnergieHeffing,   BCdata/PlanRegio_rel);
				attribute<Eur_yr> CO2Heffing     (PlanRegio) := sum(AllocatieResultatenComponent/AardGas/CO2Heffing,       BCdata/PlanRegio_rel);

				attribute<Eur_yr> VastRecht      (PlanRegio) := Oj_VastRecht_g;
				attribute<Eur_yr> Aansl_j        (PlanRegio) := Oi_Aansluit_g  * Ncw50/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m        (PlanRegio) := Oi_Aansluit_g  * Ncw/mr50/AnnualisationFactor;

				attribute<Eur_yr> Vraag_excl     (PlanRegio) := Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m        (PlanRegio) := Verbruik + VastRecht + Aansl_m - EnergieHeffing;
			}
			
			container RestWarmte := GebiedsOptie(Classifications/GebiedsOptie/V/RestWarmte, BebouwingsObject, PlanRegio, BCData, AllocatieResultatenComponent/Aandelen/RestWarmte);
			container GeoThermie := GebiedsOptie(Classifications/GebiedsOptie/V/GeoThermie, BebouwingsObject, PlanRegio, BCData, AllocatieResultatenComponent/Aandelen/GeoThermie);
			container WijkWKK    := GebiedsOptie(Classifications/GebiedsOptie/V/WijkWKK   , BebouwingsObject, PlanRegio, BCData, AllocatieResultatenComponent/Aandelen/WijkWKK);
			container BioWKK     := GebiedsOptie(Classifications/GebiedsOptie/V/BioWKK    , BebouwingsObject, PlanRegio, BCData, AllocatieResultatenComponent/Aandelen/BioWKK);

			
			template GebiedsOptie
			{
				// begin case parameters
				parameter<Classifications/GebiedsOptie> Optie;
				unit<uint32> BebouwingsObject;
				unit<uint32> PlanRegio;
				container BCdata;
				attribute<Ratio> Aandeel (BebouwingsObject);
				// end case parameters
		
				attribute<Invoer/SpecifiekeInstellingen/PlanRegio> PlanRegio_rel (BebouwingsObject) := BCdata/PlanRegio_rel;
		
				attribute<Eur_yr> Verbruik   (PlanRegio) := sum(PlanRegioKaartComponent/WarmteVraag/deelnemers_obj * Aandeel, PlanRegio_rel);
				attribute<Eur_yr> VastRecht  (PlanRegio) := sum(BCdata/Opbrengsten/VastRechtBijdrageD                     * Aandeel, PlanRegio_rel);
		
				attribute<Eur>    Oi_AanslD  (PlanRegio) := sum(BCdata/Opbrengsten/AansluitBijdrageD                      * Aandeel, PlanRegio_rel);
				attribute<Eur_yr> Aansl_j    (PlanRegio) := Oi_AanslD * BCdata/NcwRefs/_28/StartDiscountFactor * BCdata/NcwRefs/_28/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m    (PlanRegio) := Oi_AanslD * NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor;
		
				attribute<Eur_yr> Vraag_excl (PlanRegio) : = Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m    (PlanRegio) : = Verbruik + VastRecht + Aansl_m;
				
				
			}
			// TODO ook container voor andere LT opties
			container WKO
			{
				parameter<bool> Active : = rjoin(BCdata/BCname,  Classifications/BebouwingsComponent/Name, Classifications/BebouwingsComponent/KanWKO);

				attribute<Ratio> Aandeel (BebouwingsObject) := AllocatieResultatenComponent/Aandelen/WKO;

				// *** Vastrecht en aansluitbijdrage voor Deelnemers wordt nu ook meegenomen itt FO C6

				attribute<Eur_yr> Warmte     (PlanRegio) := =Active ? 'sum(PlanRegioKaartComponent/WarmteVraag/deelnemers_obj * Aandeel, BCdata/PlanRegio_rel)' : 'const(0[Eur_yr], PlanRegio)';
				attribute<Eur_yr> Koude      (PlanRegio) := =Active ? 'sum(AllocatieResultatenComponent/KoudeAllocatie                  * Aandeel, BCdata/PlanRegio_rel)' : 'const(0[Eur_yr], PlanRegio)';
				attribute<Eur_yr> Om_Comfort (PlanRegio) := =Active ? 'sum(AllocatieResultatenComponent/Om_Comfort                      * Aandeel, BCdata/PlanRegio_rel)' : 'const(0[Eur_yr], PlanRegio)';

				attribute<Eur_yr> Verbruik   (PlanRegio) := =Active ? 'Warmte + Koude' : 'const(0[Eur_yr], PlanRegio)';
				attribute<Eur_yr> VastRecht  (PlanRegio) := =Active ? 'sum(BCdata/Opbrengsten/VastRechtBijdrageD              * Aandeel, BCdata/PlanRegio_rel)' : 'const(0[Eur_yr], PlanRegio)';

				attribute<Eur>    Aansl_i    (PlanRegio) := =Active ? 'StateNaAllocatie/WKO/Oi_Aansl/'+ BCdata/BCname : 'const(0[Eur], PlanRegio)';
				attribute<Eur_yr> Aansl_j    (PlanRegio) := =Active ? 'Aansl_i * BCdata/NcwRefs/_28/StartDiscountFactor * BCdata/NcwRefs/_28/AnnualisationFactor' : 'const(0[Eur_yr], PlanRegio)';
				attribute<Eur_yr> Aansl_m    (PlanRegio) := =Active ? 'Aansl_i * NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor' : 'const(0[Eur_yr], PlanRegio)';

				attribute<Eur_yr> Vraag_excl (PlanRegio) := =Active ? 'Verbruik + VastRecht + Aansl_j' : 'const(0[Eur_yr], PlanRegio)';
				attribute<Eur_yr> Vraag_m    (PlanRegio) := =Active ? 'Verbruik + VastRecht + Aansl_m' : 'const(0[Eur_yr], PlanRegio)';
			}

			attribute<Eur>    Ki_GebouwVerbetering_excl (PlanRegio):= sum(BCdata/Kosten/Ki_GebouwVerbetering, BCdata/PlanRegio_rel);
			attribute<Eur_yr> Kw_GebouwVerbetering_excl (PlanRegio):= Ki_GebouwVerbetering_excl * NCW30/AnnualisationFactor;

			attribute<Eur_yr> Kw_Totaal_excl            (PlanRegio):= Elektriciteit/Vraag_excl + AardGas/Vraag_excl + RestWarmte/Vraag_excl + GeoThermie/Vraag_excl + WKO/Vraag_excl + WijkWKK/Vraag_excl + BioWKK/Vraag_excl + Kw_GebouwVerbetering_excl;
			attribute<Eur_yr> Kj_Onderh_excl            (PlanRegio):= Ki_GebouwVerbetering_excl * Kengetallen/Onderhoud/R_GV_onderhoud;
			attribute<Eur_yr> Kj_Admin_excl             (PlanRegio):= Ki_GebouwVerbetering_excl * Kengetallen/Onderhoud/R_GV_admin;
		}
		container LokaleEnergieProductie
		{
			attribute<Eur>		Ki_ZonneBoiler (PlanRegio)	:=	sum(BCdata/kosten/Ki_ZonneBoiler,       BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_Zon_PV      (PlanRegio)	:=	sum(BCdata/kosten/Ki_Zon_PV,            BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_mWKK        (PlanRegio)	:=	sum(BCdata/Kosten/Ki_mWKK,              BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_eWP         (PlanRegio)	:=	sum(BCdata/Kosten/Ki_eWP,               BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_hWP         (PlanRegio)	:=	sum(BCdata/Kosten/Ki_hWP,               BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_EWV         (PlanRegio)	:=	sum(BCdata/Kosten/Ki_EWV,               BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_Totaal30    (PlanRegio)	:=	sum(BCdata/Kosten/Ki_LokaleOpwekking30, BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_Totaal15    (PlanRegio)	:=	sum(BCdata/Kosten/Ki_LokaleOpwekking15, BCdata/PlanRegio_rel);
			attribute<Eur_yr>	Kw_Totaal_excl (PlanRegio)	:=	Ki_Totaal30 * Ncw30/AnnualisationFactor + Ki_Totaal15 * Ncw15/AnnualisationFactor;
			attribute<Eur_yr>	Kj_Onderh_excl (PlanRegio)	:=	(Ki_Totaal30 + Ki_Totaal15 - Ki_eWP) * Kengetallen/Onderhoud/R_LO_onderhoud + Ki_eWP * Kengetallen/Onderhoud/R_LO_EWP_onderhoud;
			attribute<Eur_yr>	Kj_Admin_excl  (PlanRegio)	:=	(Ki_Totaal30 + Ki_Totaal15 - Ki_eWP) * Kengetallen/Onderhoud/R_LO_admin     + Ki_eWP * Kengetallen/Onderhoud/R_LO_EWP_admin;
		}

		attribute<Eur_yr> Om_Comfort     (PlanRegio) := gebouwen/WKO/Om_Comfort;
		attribute<Eur_yr> Kw_Totaal_excl (PlanRegio) := gebouwen/Kw_Totaal_excl + LokaleEnergieProductie/Kw_Totaal_excl;
		attribute<Eur_yr> Kj_Onderh_excl (PlanRegio) := gebouwen/Kj_Onderh_excl + LokaleEnergieProductie/Kj_onderh_excl;
		attribute<Eur_yr> Kj_Admin_excl  (PlanRegio) := gebouwen/Kj_Admin_excl  + LokaleEnergieProductie/Kj_Admin_excl;
		
		attribute<Eur_yr> Oj_Subsidie (PlanRegio) := 
			sum(
				  BCdata/sprongen/subsidie/EIA30 * NCW30/AnnualisationFactor
				+ BCdata/sprongen/subsidie/EIA15 * NCW15/AnnualisationFactor
				+ BCdata/sprongen/subsidie/EEA
			,	BCdata/PlanRegio_rel
			);

		attribute<Eur_yr> Om_Subsidie (PlanRegio) := 
			sum(
				  BCdata/sprongen/subsidie/EIA30 * NCW/mr30/AnnualisationFactor
				+ BCdata/sprongen/subsidie/EIA15 * NCW/mr15/AnnualisationFactor
				+ BCdata/sprongen/subsidie/EEA
			,	BCdata/PlanRegio_rel
			);

		attribute<Eur_yr> Kw_Totaal_incl (PlanRegio): = 
			BTW_Factor * (Kw_Totaal_excl + Kj_Onderh_excl + Kj_Admin_excl) +
			(BTW_Factor_gv -BTW_Factor) * gebouwen/Kw_GebouwVerbetering_excl
			- Oj_Subsidie;
	}

	template WriteTableWithMetricToCSV
	{
		// begin case parameters
		container context;
		unit<uint8> RapGroep;
		parameter<string> fieldlist;
		parameter<string> filename;
		// end case parameters

		container impl: isHidden = "True"
		{
			unit<uint32> Field := range(uint32,0,strcount(fieldlist,';')+1)
			{
				attribute<string> FieldDescr  := ReadArray(fieldlist,.,string,0);
				attribute<UInt32> SepPos      := strpos(FieldDescr, ':');
				attribute<string> Name        := IsDefined(SepPos) ? substr(FieldDescr, 0, SepPos): FieldDescr;
				attribute<string> NameInTable := IsDefined(SepPos) ? substr(FieldDescr, SepPos+1, strlen(FieldDescr)) : FieldDescr;
			}
			parameter<string> NewLine := '\n';
		}

		parameter<string> Header := AsList(impl/Field/Name, ';');

		parameter<string> Metric := 
			=asList(
				Quote('[')+'+PropValue(ValuesUnit(context/' + impl/Field/NameInTable +'),' + quote('metric') + ')+'+Quote(']')
				, '+'+Quote(';')+'+'
			);

		attribute<string> Body (RapGroep) := 
			='union_data(RapGroep, '
				+AsList('string(context/'+impl/Field/NameInTable+')'
					,'+' + quote(';') + '+'
				)
			+')';

		parameter<string> Result := Header + impl/NewLine + Metric + impl/NewLine + AsList(Body+impl/NewLine,'')
		,	StorageName = "=filename"
		,	StorageType = "str";
	}

	template WriteTableToCSV
	{
		// begin case parameters
		unit<uint32> table;
		unit<uint32> variabele;
		parameter<string> filename;
		// end   case parameters

		parameter<string> result_expr        := AsList('MakeDefined(string(table/' + variabele/name + '),'''')',' +'';''+ ');
		attribute<string> result_att (table) := =result_expr;
		parameter<string> result             := AsList(variabele/name, ';')+'\n'+AsList(result_att, '\n')
		,	Storagename = "=filename"
		,	StorageType = "str";
	}
}