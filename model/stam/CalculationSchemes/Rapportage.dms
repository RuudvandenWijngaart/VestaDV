//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Rapportage
{
	#include <RegionaleRapportageTempl.dms>
	#include <FlowTempl.dms>
	#include <Components.dms>

	template EindgebruikerComponent
	{
		// begin case parameters
		unit<uint32> BO;
		container AllocatieResultaatComponent;
		container NCW15;
		container NCW20;
		container NCW30;
		container NCW50;
		attribute<Eur_yr> Oj_VastRecht_g (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur>    Oi_Aansluit_g  (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur_yr> Oj_VastRecht_e (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur>    Oi_Aansluit_e  (Invoer/SpecifiekeInstellingen/PlanRegio);
		attribute<Eur_yr> Oj_Verbruik (BO);
		// end case parameters

		parameter<ratio> BTW_Factor         := BO/BebouwingsComponent_UpRef/BTW_Factor;
		parameter<ratio> BTW_Factor_gv      := BO/BebouwingsComponent_UpRef/BTW_Factor_gv;
		parameter<ratio> BTW_Factor_energie := BO/BebouwingsComponent_UpRef/BTW_Factor_energie;

		unit<uint32> PlanRegio         := SpecifiekeInstellingen/PlanRegio;

		container gebouwen := for_each_ne(Classifications/GebiedsOptie/name
			,	replace('GebiedsOptie(Classifications/GebiedsOptie/V/@GO, BO, PlanRegio, AllocatieResultaatComponent/Aandelen/@GO)'
				, '@GO',Classifications/GebiedsOptie/name
				)
			)
		{
			container Elektriciteit
			{
				attribute<Eur_yr> Verbruik       (PlanRegio) := sum(AllocatieResultaatComponent/Elektriciteit/eindgebruik_excl, BO/PlanRegio_rel);
				attribute<Eur_yr> netwerk        (PlanRegio) := sum(AllocatieResultaatComponent/Elektriciteit/netwerk         , BO/Planregio_rel);
				attribute<Eur_yr> EnergieHeffing (PlanRegio) := sum(AllocatieResultaatComponent/Elektriciteit/EnergieHeffing  , BO/Planregio_rel);
				attribute<Eur_yr> CO2Heffing     (PlanRegio) := sum(AllocatieResultaatComponent/Elektriciteit/CO2Heffing      , BO/Planregio_rel);

				attribute<Eur_yr> VastRecht      (PlanRegio) := Oj_VastRecht_e;
				attribute<Eur_yr> Aansl_j        (PlanRegio) := Oi_Aansluit_e * Ncw50/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m        (PlanRegio) := Oi_Aansluit_e * Ncw/mr50/AnnualisationFactor;

				attribute<Eur_yr> Vraag_excl     (PlanRegio) := Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m        (PlanRegio) := Verbruik + Vastrecht + Aansl_m - EnergieHeffing;
			}

			container AardGas
			{
				attribute<Eur_yr> Verbruik       (PlanRegio) := sum(AllocatieResultaatComponent/AardGas/eindgebruik_excl, BO/Planregio_rel);
				attribute<Eur_yr> netwerk        (PlanRegio) := sum(AllocatieResultaatComponent/AardGas/netwerk         , BO/Planregio_rel);
				attribute<Eur_yr> EnergieHeffing (PlanRegio) := sum(AllocatieResultaatComponent/AardGas/EnergieHeffing  , BO/Planregio_rel);
				attribute<Eur_yr> CO2Heffing     (PlanRegio) := sum(AllocatieResultaatComponent/AardGas/CO2Heffing      , BO/Planregio_rel);

				attribute<Eur_yr> VastRecht      (PlanRegio) := Oj_VastRecht_g;
				attribute<Eur_yr> Aansl_j        (PlanRegio) := Oi_Aansluit_g * Ncw50/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m        (PlanRegio) := Oi_Aansluit_g * Ncw/mr50/AnnualisationFactor;

				attribute<Eur_yr> Vraag_excl     (PlanRegio) := Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m        (PlanRegio) := Verbruik + VastRecht + Aansl_m - EnergieHeffing;
			}

			template GebiedsOptie
			{
				// begin case parameters
				parameter<Classifications/GebiedsOptie> Optie;
				unit<uint32> BO;
				unit<uint32> PlanRegio;
				attribute<bool> Aandeel (BO);
				// end case parameters
		
				attribute<Eur_yr> Verbruik   (PlanRegio) := sum(Aandeel ? Oj_verbruik : 0[Eur_yr], BO/PlanRegio_rel);
				attribute<Eur_yr> VastRecht  (PlanRegio) := sum(Aandeel ? BO/Opbrengsten/VastRechtMT_T : 0[Eur_yr], BO/PlanRegio_rel);

				attribute<Eur>    Oi_AanslD  (PlanRegio) := sum(Aandeel ? BO/Opbrengsten/AansluitBijdrageT : 0[Eur], BO/PlanRegio_rel);
				attribute<Eur_yr> Aansl_j    (PlanRegio) := Oi_AanslD * BO/BebouwingsComponent_UpRef/NcwRefs/_28/StartDiscountFactor * BO/BebouwingsComponent_UpRef/NcwRefs/_28/AnnualisationFactor;
				attribute<Eur_yr> Aansl_m    (PlanRegio) := Oi_AanslD * NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor;

				attribute<Eur_yr> Vraag_excl (PlanRegio) : = Verbruik + VastRecht + Aansl_j;
				attribute<Eur_yr> Vraag_m    (PlanRegio) : = Verbruik + VastRecht + Aansl_m;

				attribute<Eur_yr> Om_Comfort (PlanRegio) := sum(AllocatieResultaatComponent/Elektriciteit/Om_Comfort * float64(Aandeel), BO/PlanRegio_rel);
			}

			attribute<Eur>    Ki_GebouwVerbetering_excl (PlanRegio) := sum(BO/eenmalig/Ki_gv, BO/Planregio_rel);
			attribute<Eur_yr> Kw_GebouwVerbetering_excl (PlanRegio) := Ki_GebouwVerbetering_excl * NCW30/AnnualisationFactor;
			attribute<Eur_yr> Kw_Totaal_excl            (PlanRegio) := Elektriciteit/Vraag_excl + AardGas/Vraag_excl + RestWarmte/Vraag_excl + GeoThermie/Vraag_excl + WKO/Vraag_excl + WijkWKK/Vraag_excl + BioWKK/Vraag_excl + Kw_GebouwVerbetering_excl;
		}

		container LokaleEnergieProductie
		{
			attribute<Eur>    Ki_Totaal30    (PlanRegio) := sum(BO/eenmalig/Ki30_LO, BO/Planregio_rel);
			attribute<Eur>    Ki_Totaal20    (PlanRegio) := sum(BO/eenmalig/Ki20_LO, BO/Planregio_rel);
			attribute<Eur>    Ki_Totaal15    (PlanRegio) := sum(BO/eenmalig/Ki15_LO, BO/Planregio_rel);
			attribute<Eur>    Ki_TotaalLTAS  (PlanRegio) := sum(BO/eenmalig/Ki_LTAS, BO/Planregio_rel);
			attribute<Eur_yr> Kw_Totaal_excl (PlanRegio) := Ki_Totaal30 * Ncw30/AnnualisationFactor + Ki_Totaal20 * Ncw20/AnnualisationFactor + Ki_Totaal15 * Ncw15/AnnualisationFactor;

			attribute<Eur_yr> Kj_Onderh_excl (PlanRegio) := sum(BO/jaarlijks/Kj_OH_LO , BO/Planregio_rel);
			attribute<Eur_yr> Kj_Admin_excl  (PlanRegio) := sum(BO/jaarlijks/Kj_Adm_LO, BO/Planregio_rel);
		}

		attribute<Eur_yr> Om_Comfort     (PlanRegio) := gebouwen/WKO/Om_Comfort;
		attribute<Eur_yr> Kw_Totaal_excl (PlanRegio) := LokaleEnergieProductie/Kw_Totaal_excl + gebouwen/Kw_Totaal_excl;
		attribute<Eur_yr> Kj_Onderh_excl (PlanRegio) := LokaleEnergieProductie/Kj_onderh_excl; // REMOVE, always zero + gebouwen/Kj_Onderh_excl
		attribute<Eur_yr> Kj_Admin_excl  (PlanRegio) := LokaleEnergieProductie/Kj_Admin_excl ; // REMOVE, always zerogebouwen/Kj_Admin_excl 

		attribute<Eur_yr> Oj_Subsidie (PlanRegio) := 
			sum(
				  BO/eenmalig/Oi30_LO * NCW30/AnnualisationFactor
				+ BO/eenmalig/Oi20_LO * NCW20/AnnualisationFactor
				+ BO/eenmalig/Oi15_LO * NCW15/AnnualisationFactor
				+ BO/eenmalig/Oi_GV   * NCW30/AnnualisationFactor
				, BO/Planregio_rel
			);

		attribute<Eur_yr> Om_Subsidie (PlanRegio) := 
			sum(
				  BO/eenmalig/Oi30_LO * NCW/mr30/AnnualisationFactor
				+ BO/eenmalig/Oi20_LO * NCW/mr20/AnnualisationFactor
				+ BO/eenmalig/Oi15_LO * NCW/mr15/AnnualisationFactor
				+ BO/eenmalig/Oi_GV   * NCW/mr30/AnnualisationFactor
				, BO/Planregio_rel
			);

		attribute<Eur_yr> Kw_Totaal_incl (PlanRegio): = 
			   BTW_Factor * (Kw_Totaal_excl + Kj_Onderh_excl + Kj_Admin_excl) 
			+ (BTW_Factor_gv -BTW_Factor) * gebouwen/Kw_GebouwVerbetering_excl
			- Oj_Subsidie;
	}

	template WriteTable8ToCSV
	{
		// begin case parameters
		unit<uint8> table;
		parameter<string> fieldlist;
		parameter<string> filename;
		// end case parameters

		container impl: isHidden = "True"
		{
			unit<uint32> Field := range(uint32,0,strcount(fieldlist,';')+1)
			{
				attribute<string> FieldDescr  := ReadArray(fieldlist,.,string,0);
				attribute<uint32> SepPos      := strpos(FieldDescr, ':');
				attribute<string> name        := IsDefined(SepPos) ? substr(FieldDescr, 0, SepPos): FieldDescr;
				attribute<string> NameInTable := IsDefined(SepPos) ? substr(FieldDescr, SepPos+1, strlen(FieldDescr)) : FieldDescr;
				attribute<string> Metric := 
					='union_data(.,'+
						AsList('+PropValue(ValuesUnit(table/' + impl/Field/NameInTable +'),' + quote('metric') + ')', ',')+
					')';
			}
		}

		parameter<string> Header := AsList(Quote(impl/Field/name +(impl/Field/Metric=='' ? '' : ' [' + impl/Field/Metric +']')), ';');

		parameter<string> Body_expr := AsList('AsExprList(table/' + impl/Field/NameInTable + ', id(table))',' +'';''+ ');
		attribute<string> Body (table) := =Body_expr;

		parameter<string> Result := Header + '\n' + AsList(Body+'\n', '')
		, StorageName = "=filename"
		, StorageType = "str";
	}

	template WriteTable32ToCSV
	{
		// begin case parameters
		unit<uint32> table;
		parameter<string> fieldlist;
		parameter<string> filename;
		// end   case parameters

		container impl: isHidden = "True"
		{
			unit<uint32> Field := range(uint32,0,strcount(fieldlist,';')+1)
			{
				attribute<string> FieldDescr  := ReadArray(fieldlist,.,string,0);
				attribute<uint32> SepPos      := strpos(FieldDescr, ':');
				attribute<string> name        := IsDefined(SepPos) ? substr(FieldDescr, 0, SepPos): FieldDescr;
				attribute<string> NameInTable := IsDefined(SepPos) ? substr(FieldDescr, SepPos+1, strlen(FieldDescr)) : FieldDescr;
				attribute<string> Metric := 
					='union_data(.,'+
						AsList('+PropValue(ValuesUnit(table/' +  impl/Field/NameInTable +'),' + quote('metric') + ')', ',')+
					')';
			}
		}
		parameter<string> Header := AsList(Quote(impl/Field/name +(impl/Field/Metric=='' ? '' : ' [' + impl/Field/Metric +']')), ';'), isHidden = "true";

		parameter<string> Body_expr := AsList('AsExprList(table/' + impl/Field/NameInTable + ', id(table))',' +'';''+ '), isHidden = "true";
		attribute<string> Body (table) := =Body_expr, isHidden = "true";
		parameter<string> Export := Header + '\n' + AsList(Body+'\n', '')
		, Storagename = "=filename"
		, StorageType = "str";
	}
}