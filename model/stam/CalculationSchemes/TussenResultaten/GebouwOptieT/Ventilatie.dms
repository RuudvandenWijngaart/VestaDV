//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2022 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container ventilatie
{
	unit<uint32> BBN := ZichtjaarBeleid/BestaandeBouwNorm, ishidden = "True";
	container Normen := for_each_ne(BBN/name, 'ImplSubs(BBN/V/'+BBN/name+')')
	{
		parameter<bool>      GeenNormen := sum(BBN/naleving) <= 0.0[ratio];

		attribute<m2>        TotaalSchil (GeschiktObject) := ='add('+AsItemList('BO/Bouwdelen/'+classifications/bouwdeel/name+'[GeschiktObject/Bo_rel]')+') - BO/Bouwdelen/KR[GeschiktObject/Bo_rel]';
		attribute<ratio>     Schilfactor (GeschiktObject) := Totaalschil / BO/oppervlakte[GeschiktObject/BO_rel];
		attribute<yr_uint16> Bouwjaar    (GeschiktObject) := BO/bouwjaar[GeschiktObject/BO_rel];

		attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_norm_i (GeschiktObject) := 
			 = GeenNormen ? 'const( (0/0)[Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes], GeschiktObject)' : 'max_elem('+AsItemList('Normen/'+BBN/name+'/Geldig ? BBN/Ventilatienorm/Ventilatietype_norm[Normen/'+BBN/name+'/BBN_rel] : (0/0)[Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes]')+')';

		//===== CHECK: is genormeerd installatietype per object wel beschikbaar in het huidige zichtjaar?
		attribute<bool> Type_norm_beschikbaar (GeschiktObject) := 
				MakeDefined(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/startjaar[Installatietype_norm_i], 0000w) <= Zichtjaar_jaar 
			&&	MakeDefined(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/eindjaar [Installatietype_norm_i], 9999w) >= Zichtjaar_jaar;
		attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_norm (GeschiktObject) := Type_norm_beschikbaar ? Installatietype_norm_i : (0/0)[Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes];
	}
	template ImplSubs : ishidden = "True"
	{
		//case parameters
		parameter<BBN> BBN_rel;
		//end case parameters

		attribute<bool>  Naleving (GeschiktObject) := first(rnd_uniform(JaarSeed * 106u, GeschiktObject, range(float64, 0d, 1d)), GeschiktObject/BO_rel)[GeschiktObject/BO_rel] <= BBN/Naleving[BBN_rel];
		

		container voorwaarden
		{
			parameter<ratio>     MinSchilfactor  := BBN/voorwaarde/MinSchilfactor[BBN_rel];
			parameter<ratio>     MaxSchilfactor  := BBN/voorwaarde/MaxSchilfactor[BBN_rel];
			parameter<yr_uint16> MinBouwjaar     := BBN/voorwaarde/MinBouwjaar[BBN_rel];
			parameter<yr_uint16> MaxBouwjaar     := BBN/voorwaarde/MaxBouwjaar[BBN_rel];

			attribute<bool> InDoelgroepED (GeschiktObject) := ='OR('+AsItemList(replace('BBN/doelgroep/@ED[BBN_rel] && BO/Eigendom_rel[geschiktobject/BO_rel] == Classifications/Eigendom/V/@ED','@ED',Classifications/Eigendom/label))+')', ishidden = "True";
			attribute<bool> InDoelgroepWT (GeschiktObject) := BBN/doelgroep/EGW[BBN_rel] && BO/IsMeergezins[GeschiktObject/BO_rel] || BBN/doelgroep/MGW[BBN_rel] && not(BO/IsMeergezins[GeschiktObject/BO_rel]);
			
			attribute<bool> SchilfactorVoldaan  (GeschiktObject) := Schilfactor >= MakeDefined(MinSchilfactor, 0.0) && Schilfactor < MakeDefined(MaxSchilfactor, 9999.0);
			attribute<bool> BouwjaarVoldaan     (GeschiktObject) := Bouwjaar >= MakeDefined(MinBouwjaar, 0w) && Bouwjaar < MakeDefined(MaxBouwjaar, 9999w);

			attribute<bool> VoorwaardenVoldaan  (GeschiktObject) := AND(SchilfactorVoldaan, BouwjaarVoldaan, InDoelgroepWT, InDoelgroepED);
		}

		attribute<bool>  Geldig   (GeschiktObject) := voorwaarden/VoorwaardenVoldaan && Naleving;
	}

	container Afweging
	{
		//====== Huidige ventilatiesysteem
		attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/VT[GeschiktObject/BO_rel];
		attribute<bool>                                                       VentilatieActief    (GeschiktObject) := BO/Activatie/producten/VT[GeschiktObject/BO_rel];

		//====== Selectie van ventilatiesystemen die in huidige zichtjaar toegepast kunnen worden
		unit<uint32> Ventilatieopties := select_with_org_rel(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/startjaar <= Zichtjaar_jaar && Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/eindjaar >= Zichtjaar_jaar)
		{
			attribute<string>    name       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/name[org_rel];
			attribute<GJ_yr_asl> V_RV_asl   := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_RV_asl[org_rel];
			attribute<GJ_yr_asl> V_E_asl    := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_E_asl[org_rel];
			attribute<yr_uint16> Levensduur := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Levensduur[org_rel];
			attribute<float64>   P50P       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/P50P[org_rel];
			attribute<float64>   BETA       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/BETA[org_rel];

//					container Ki_asl := for_each_nedv(Classifications/Uitvoering/Name, 'leerCurves/Ventilatie/curve * Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Ki_asl/'+Classifications/Uitvoering/Name+'[org_rel]',Ventilatieopties ,eur_asl);
			container V      := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}

		parameter<float64> LOG_MAX_FLOAT64 := 700.0; // slightly less than ln( 2^1024 / pcount(xInvesteringsOptie/ActieveWoning_rel) )

		//====== Berekeningen per ventilatiesysteem t.b.v. afweging
		container PerOptie := for_each_ne(Ventilatieopties/Name, 'OptieBerekening(Ventilatieopties/V/'+Ventilatieopties/Name+')');
		template OptieBerekening : ishidden = "true"
		{
			//case parameter
			parameter<Ventilatieopties> Ventilatieoptie_rel;
			//end
			parameter<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype := rlookup(Ventilatieopties/name[Ventilatieoptie_rel], Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/name);

			unit<uint32> VentilatieMaatregel := select_with_org_rel(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Installatie_na == Installatietype)
			{
				attribute<string> name := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/name[org_rel];
				attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_voor := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Installatie_voor[org_rel];

				container Ki_asl := for_each_nedv(Classifications/Uitvoering/Name, 'leerCurves/Ventilatie/curve * Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Ki_asl/'+Classifications/Uitvoering/Name+'[org_rel]',VentilatieMaatregel ,eur_asl);
			}
			attribute<VentilatieMaatregel> VentilatieMaatregel_rel (GeschiktObject) := rlookup(Installatietype_org, VentilatieMaatregel/Installatietype_voor);


			attribute<GJ_yr>   V_RV       (GeschiktObject) :=
				  BO/nrAansluitingen[GeschiktObject/BO_rel]
				* Ventilatieopties/V_RV_asl[Ventilatieoptie_rel]
				* BO/Functioneel_base/Gedragfactor/VT
				* BO/Functioneel_base/Klimaateffect[GeschiktObject/BO_rel]
				* BO/Functioneel_base/Fitfactor_VT[GeschiktObject/BO_rel];

			attribute<GJ_yr>   V_E        (GeschiktObject) := BO/nrAansluitingen[GeschiktObject/BO_rel]     * Ventilatieopties/V_E_asl[Ventilatieoptie_rel]  * BO/Functioneel_base/Gedragfactor/VT * BO/Functioneel_base/Fitfactor_VT[GeschiktObject/BO_rel];
			attribute<Eur_yr>  Kj_energie (GeschiktObject) := first(Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl) * V_E + first(Prijzen/AardGas/Staffel/KGJ_eindgebruik_excl) * V_RV; //TODO: koppelen aan huidige installaties

			attribute<Eur>     Ki_VT      (GeschiktObject) := ='BO/nrAansluitingen[GeschiktObject/BO_rel] * merge(uint8(BO/Activatie/Uitvoering[GeschiktObject/BO_rel]), eur_asl, '+AsItemList('VentilatieMaatregel/Ki_asl/'+Classifications/Uitvoering/Name+'[VentilatieMaatregel_rel]')+')';

			attribute<Eur_yr>  Kji_VT     (GeschiktObject) := ='Ki_VT * NcwRefs/AF_'+string(Ventilatieopties/Levensduur[Ventilatieoptie_rel])+'[GeschiktObject/BO_rel]';
			attribute<Eur_yr>  Kmi_VT     (GeschiktObject) := ='Ki_VT * NCW/mr'+string(Ventilatieopties/Levensduur[Ventilatieoptie_rel])+'/AnnualisationFactor';

			attribute<Eur_yr>  kosten_tbv_afweging    (GeschiktObject) := (Kj_energie * SplitIncentiveVerbruik * BTW_Factor_Energie + Kji_VT * BTW_Factor) * Ventilatieopties/P50P[Ventilatieoptie_rel];

			attribute<float64> Geschiktheid (GeschiktObject) := Choice/Kj_duursteoptie - kosten_tbv_afweging;
			attribute<float64> Odds         (GeschiktObject) := IsDefined(Geschiktheid) ? exp(min_elem(Geschiktheid * Ventilatieopties/beta[Ventilatieoptie_rel], LOG_MAX_FLOAT64)) : 0.0;
			attribute<float64> Probability  (GeschiktObject) := Odds / Choice/TotalOdds;
			attribute<float64> Cumulprob    (GeschiktObject) := ='add('+AsItemList('float64(PerOptie/'+Ventilatieopties/Name+'/Ventilatieoptie_rel <= Ventilatieoptie_rel) * PerOptie/'+Ventilatieopties/Name+'/Probability')+')';
			attribute<bool>    IsSelected   (GeschiktObject) := CumulProb - Probability <= Choice/rnd && Choice/rnd < CumulProb;

			attribute<Ventilatieopties> Ventilatieoptie_choice (GeschiktObject) := IsSelected ? Ventilatieoptie_rel : (0/0)[Ventilatieopties];
			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Type_choice (GeschiktObject) := IsSelected ? Installatietype : (0/0)[Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes];
		}
		
		//====== Afweging tussen verschillende ventilatiesystemen
		container Choice
		{
			attribute<Eur_yr>  Kj_duursteoptie (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Kosten_tbv_afweging')+')';
			attribute<float64> TotalOdds       (GeschiktObject) := ='add('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Odds')+')';
			attribute<float64> rnd             (GeschiktObject) := rnd_uniform(JaarSeed * 19u, GeschiktObject, range(float64, 0d, 1d));

			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_def (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Type_choice')+')';
			attribute<Ventilatieopties>                                           Ventilatieoptie_def (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Ventilatieoptie_choice')+')';

			//====== Definitievie waarden gekozen systeem
			attribute<Eur>     Ki_VT           (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), Eur   ,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Ki_VT')+')';
			attribute<Eur_yr>  Kji_VT          (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), Eur_yr,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Kji_VT')+')';
			attribute<Eur_yr>  Kmi_VT          (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), Eur_yr,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Kmi_VT')+')';
			attribute<GJ_yr>   V_RV            (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), GJ_yr ,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/V_RV')+')';
			attribute<GJ_yr>   V_E             (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), GJ_yr ,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/V_E')+')';
		}

		attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_def (GeschiktObject) := =not(Afweging_VT) ? 'Installatietype_org' : 'VentilatieActief ? makedefined(Normen/Installatietype_norm, Choice/Installatietype_def) : Installatietype_org';
		
		//====== waarden gekozen ventilatiesysteem
		attribute<eur>    Ki_vent (GeschiktObject) := =not(Afweging_VT) ? 'const(0.0[eur],GeschiktObject)' : 'not(VentilatieActief) ? 0.0[eur] : Choice/Ki_VT';
		attribute<eur_yr> Kji_VT  (GeschiktObject) := =not(Afweging_VT) ? 'const(0.0[eur_yr],GeschiktObject)' : 'not(VentilatieActief) ? 0.0[eur_yr] : Choice/Kji_VT';
		attribute<eur_yr> Kmi_VT  (GeschiktObject) := =not(Afweging_VT) ? 'const(0.0[eur_yr],GeschiktObject)' : 'not(VentilatieActief) ? 0.0[eur_yr] : Choice/Kmi_VT';
		attribute<GJ_yr>  V_RV    (GeschiktObject) := =not(Afweging_VT) ? 'BO/Functioneel/Vrv_VT[GeschiktObject/BO_rel]' : 'not(VentilatieActief) ? BO/Functioneel/Vrv_VT[GeschiktObject/BO_rel] : Choice/V_RV';
		attribute<GJ_yr>  V_E     (GeschiktObject) := =not(Afweging_VT) ? 'BO/MetervraagBerekening/eEffect/Ventilatie/Vj_elek[GeschiktObject/BO_rel]' : 'not(VentilatieActief) ? BO/MetervraagBerekening/eEffect/Ventilatie/Vj_elek[GeschiktObject/BO_rel] : Choice/V_E';

	}
	//====== Gekozen ventilatiesysteem
	attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_rel (GeschiktObject) := Afweging/Installatietype_def;
}