//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2022 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////


container DK
{
	unit<uint32> inst := Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes;

	//===== Oorspronkelijke dakinstallatie
	attribute<inst> Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/DK[GeschiktObject/BO_rel];
	attribute<bool> EOL                 (GeschiktObject) := MakeDefined(inst/Levensduur[Installatietype_org], 9999w) <= MakeDefined((Zichtjaar_jaar - BO/InstallatiePerProduct/LastVervanging/DK)[GeschiktObject/BO_rel], 0w);
	
	parameter<float64> LOG_MAX_FLOAT64 := 700.0; // slightly less than ln( 2^1024 / pcount(xInvesteringsOptie/ActieveWoning_rel) )

	container PerOptie := for_each_ne(inst/Name, 'OptieBerekening(inst/V/'+inst/Name+')');
	template OptieBerekening : ishidden = "true"
	{
		//case parameter
		parameter<inst> inst_rel;
		//end

		container Energie
		{
			attribute<m2>     A_DK          (GeschiktObject) := add(GeschiktObject/Bouwdelen/DP, GeschiktObject/Bouwdelen/DS);
			attribute<kW>     kWp           (GeschiktObject) := inst/pdak_opp[inst_rel] * A_DK * inst/NuttigDak * inst/dakaandeelZonPV[inst_rel] * leerCurves/ZonPVcap/Curve;
			attribute<GJ_yr>  Oj_elek_ZonPV (GeschiktObject) := makedefined(kWp * ( (GeschiktObject/Bouwdelen/DP / A_DK) * inst/Opbr_cap_e_DP[inst_rel] + (GeschiktObject/Bouwdelen/DS / A_DK) * inst/Opbr_cap_e_DS[inst_rel] ), 0[GJ_yr]);
			attribute<GJ_yr>  Oj_tw_ZonB    (GeschiktObject) := float64(inst/Opbr_opp_TW[inst_rel] > 0.0[GJ_yr_m2]) * GeschiktObject/Functioneel/TW * inst/AandeelTW;
			attribute<GJ_yr>  Vj_elek_ZonB  (GeschiktObject) := const(inst/Hulpenergie[inst_rel],GeschiktObject);
		}
		container Kosten
		{					
			attribute<eur>    Ki_zonB       (GeschiktObject) := leerCurves/ZonB/Curve  * (
				  inst/Ki_ZonB_asl[inst_rel] * BO/nrAansluitingen[GeschiktObject/BO_rel]
				+ inst/Ki_ZonB_opp[inst_rel] * makedefined(((GeschiktObject/Functioneel/TW * inst/AandeelTW) / inst/Opbr_opp_TW[inst_rel]),0.0[m2]) );
			attribute<eur>    Ki_ZonPV      (GeschiktObject) := leerCurves/ZonPV/Curve * (
				  (Schuiven/KostenMin * inst/Ki_PV_asl_min[inst_rel] + Schuiven/KostenMax * inst/Ki_PV_asl_max[inst_rel]) * BO/nrAansluitingen[GeschiktObject/BO_rel]
				+ (Schuiven/KostenMin * inst/Ki_PV_opp_min[inst_rel] + Schuiven/KostenMax * inst/Ki_PV_opp_max[inst_rel]) * Energie/A_DK * inst/NuttigDak * inst/dakaandeelZonPV[inst_rel] );
			attribute<Eur_yr> Kj_OH         (GeschiktObject) := const(inst/Kj_OH[inst_rel],GeschiktObject);

			attribute<Eur_yr> Kji_ZonB      (GeschiktObject) := = MakeDefined(inst/Levensduur[inst_rel], 0[yr_uint16]) > 15[yr_uint16] ? 'Ki_zonB  * NcwRefs/AF_20[GeschiktObject/BO_rel]' : 'Ki_zonB  * NcwRefs/AF_15[GeschiktObject/BO_rel]';
			attribute<Eur_yr> Kji_ZonPV     (GeschiktObject) := = MakeDefined(inst/Levensduur[inst_rel], 0[yr_uint16]) > 15[yr_uint16] ? 'Ki_ZonPV * NcwRefs/AF_20[GeschiktObject/BO_rel]' : 'Ki_ZonPV * NcwRefs/AF_15[GeschiktObject/BO_rel]';

			attribute<Eur_yr> Kmi_ZonB      (GeschiktObject) := = MakeDefined(inst/Levensduur[inst_rel], 0[yr_uint16]) > 15[yr_uint16] ? 'Ki_zonB  * NCW/mr20/AnnualisationFactor' : 'Ki_zonB  * NCW/mr15/AnnualisationFactor';
			attribute<Eur_yr> Kmi_ZonPV     (GeschiktObject) := = MakeDefined(inst/Levensduur[inst_rel], 0[yr_uint16]) > 15[yr_uint16] ? 'Ki_ZonPV * NCW/mr20/AnnualisationFactor' : 'Ki_ZonPV * NCW/mr15/AnnualisationFactor';

		//	='add(Ki_zonB, Ki_ZonPV) * NcwRefs/_'+string(inst/Levensduur[inst_rel])+'/AnnualisationFactor';

			attribute<Eur_yr> Kji_DK        (GeschiktObject) := add(Kji_zonB, Kji_ZonPV);
			attribute<Eur_yr> Kmi_DK        (GeschiktObject) := add(Kmi_zonB, Kmi_ZonPV);
		}	
		container Opbrengsten
		{
			attribute<Eur_yr> Oj_productie  (GeschiktObject) := (Energie/Oj_elek_ZonPV - Energie/Vj_elek_ZonB) * first(prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl) + Energie/Oj_tw_ZonB * 
				(  Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/e        ? first(prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl)
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/biomassa ? meterprijzen/Pj_biomassa
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/olie     ? meterprijzen/Pj_olie
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/pellets  ? meterprijzen/Pj_pellets
				 : first(prijzen/AardGas/Staffel/KGJ_eindgebruik_excl)	);

			attribute<Eur_yr> Om_productie  (GeschiktObject) := (Energie/Oj_elek_ZonPV - Energie/Vj_elek_ZonB) * first(prijzen/Elektriciteit/Staffel/KGJ_Maatschappelijk) + Energie/Oj_tw_ZonB * 
				(  Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/e        ? first(prijzen/Elektriciteit/Staffel/KGJ_Maatschappelijk)
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/biomassa ? meterprijzen/Pm_biomassa
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/olie     ? meterprijzen/Pm_olie
				 : Func2Meter/BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/pellets  ? meterprijzen/Pm_pellets
				 : first(prijzen/AardGas/Staffel/KGJ_Maatschappelijk)	);
		}

		attribute<Eur_yr> Kji_afweging (GeschiktObject) := (float64(not(inst_rel == Installatietype_org) || EOL) * Kosten/Kji_DK - Opbrengsten/Oj_productie + Kosten/Kj_OH) * BTW_Factor * inst/P50P[inst_rel];
		attribute<Eur_yr> Kmi_afweging (GeschiktObject) := (float64(not(inst_rel == Installatietype_org) || EOL) * Kosten/Kmi_DK - Opbrengsten/Om_productie + Kosten/Kj_OH) * inst/P50P[inst_rel];

		attribute<Eur_yr> Kosten_tbv_afweging (GeschiktObject) := =( LocatieSpecifiekeOpties/GebouwOpties/KiesMaatschappelijk ) ? 'Kmi_afweging' : 'Kji_afweging' ;

		attribute<float64> Geschiktheid (GeschiktObject) := Choice/Kj_duursteoptie - kosten_tbv_afweging;
		attribute<float64> Odds         (GeschiktObject) := IsDefined(Geschiktheid) ? exp(min_elem(Geschiktheid * inst/beta[inst_rel], LOG_MAX_FLOAT64)) : 0.0;
		attribute<float64> Probability  (GeschiktObject) := Odds / Choice/TotalOdds;
		attribute<float64> Cumulprob    (GeschiktObject) := ='add('+AsItemList('float64(PerOptie/'+inst/Name+'/inst_rel <= inst_rel) * PerOptie/'+inst/Name+'/Probability')+')';
		attribute<bool>    IsSelected   (GeschiktObject) := CumulProb - Probability <= Choice/rnd && Choice/rnd < CumulProb;
		attribute<inst>    Type_choice  (GeschiktObject) := IsSelected ? inst_rel : (0/0)[inst];
	}

	//====== Afweging tussen verschillende systemen
	container Choice
	{
		attribute<Eur_yr>  Kj_duursteoptie     (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+inst/Name+'/Kosten_tbv_afweging')+')';
		attribute<float64> TotalOdds           (GeschiktObject) := ='add('+AsItemList('PerOptie/'+inst/Name+'/Odds')+')';
		attribute<float64> rnd                 (GeschiktObject) := rnd_uniform(JaarSeed * 41u, GeschiktObject, range(float64, 0d, 1d));
		attribute<inst>    Installatietype_def (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+inst/Name+'/Type_choice')+')';
	}
		
	//===== Gekozen (nieuwe) dakinstallatie
	attribute<inst> Installatietype_def (GeschiktObject) := =not(Afweging_DK) ? 'Installatietype_org' : 'Choice/Installatietype_def';

	//===== Is dit een verandering?
	attribute<bool> IsChangedNow (GeschiktObject) := Installatietype_org != Installatietype_def;

	container Result
	{
		//====== Definitievie waarden gekozen systeem
		attribute<GJ_yr>  Oj_elek_ZonPV (GeschiktObject) := ='merge(uint8(Installatietype_def), GJ_yr,' +AsItemList('PerOptie/'+inst/Name+'/Energie/Oj_elek_ZonPV')+')';
		attribute<GJ_yr>  Oj_tw_ZonB    (GeschiktObject) := ='merge(uint8(Installatietype_def), GJ_yr,' +AsItemList('PerOptie/'+inst/Name+'/Energie/Oj_tw_ZonB')+')';
		attribute<GJ_yr>  Vj_elek_ZonB  (GeschiktObject) := ='merge(uint8(Installatietype_def), GJ_yr,' +AsItemList('PerOptie/'+inst/Name+'/Energie/Vj_elek_ZonB')+')';
		attribute<Eur>    Ki_zonB       (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur,'   +AsItemList('PerOptie/'+inst/Name+'/Kosten/Ki_zonB')+')';
		attribute<Eur>    Ki_ZonPV      (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur,'   +AsItemList('PerOptie/'+inst/Name+'/Kosten/Ki_ZonPV')+')';
		attribute<Eur_yr> Kj_OH         (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur_yr,'+AsItemList('PerOptie/'+inst/Name+'/Kosten/Kj_OH')+')';

		attribute<Eur_yr> Kji_zonB      (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur_yr,'+AsItemList('PerOptie/'+inst/Name+'/Kosten/Kji_zonB')+')';
		attribute<Eur_yr> Kji_ZonPV     (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur_yr,'+AsItemList('PerOptie/'+inst/Name+'/Kosten/Kji_ZonPV')+')';
		attribute<Eur_yr> Kmi_zonB      (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur_yr,'+AsItemList('PerOptie/'+inst/Name+'/Kosten/Kmi_zonB')+')';
		attribute<Eur_yr> Kmi_ZonPV     (GeschiktObject) := ='merge(uint8(Installatietype_def), Eur_yr,'+AsItemList('PerOptie/'+inst/Name+'/Kosten/Kmi_ZonPV')+')';
	}
	
}



