//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container StateNaAllocatie := GebiedsAllocatie/ResultState
{
	parameter<bool> IsBeginSituatie := false;

	container KostenBaten := CalculationSchemes/KostenBaten(GebiedsAllocatie/ResultState/Bebouwing, invoer/Energieprijzen);
	
	attribute<bool>    GebiedsOptie  (PlanRegio) := IsDefined(PlanRegioWarmteAllocatie_REMOVE);
	attribute<bool>    NewGebruik    (PlanRegio) := GebiedsOptie && not(IsDefined(StateVoorAllocatie/PlanRegioWarmteAllocatie_REMOVE));
	
	container Cumulaties := for_each_ne(Classifications/GebiedsOptie/name, 
		replace(IsDefined(invert(Voorkeuren/ActieveGebiedsOptie/GebiedsOptie_rel))
			? 'CalculationSchemes/AanbodOpties/GebiedsAllocatieResultaat_cumuleren(StateVoorAllocatie/Cumulaties/@GO@, ../@GO@)'
			: 'StateVoorAllocatie/Cumulaties/@GO@'
		, '@GO@', Classifications/GebiedsOptie/name)
	);
	container Totaal := = #Voorkeuren/ActieveGebiedsOptie > 0
		? 'CalculationSchemes/AanbodOpties/GebiedsAllocatieResultaat_verzamelen(Cumulaties, Voorkeuren/ActieveGebiedsOptie)'
		: 'BeginSituatie/LegeGebiedsOptie'
		;
	
	container nrAansluitinging := for_each_nedv(Classifications/WarmteOptie/Name, 
		replace(
			'add('+
				AsItemList(
					replace('sum(Bebouwing/@BC@/RuimtelijkeVraagComponent/nrAansluitingen * Float64(Bebouwing/@BC@/WarmteAllocatie == Classifications/WarmteOptie/V/@WO@), Bebouwing/@BC@/PlanRegio_rel)'
					, '@BC@', Classifications/BebouwingsComponent/name)
				)
			+')'
		,	'@WO@', Classifications/WarmteOptie/Name
		)
	,	Planregio, nrAansl
	);
	
	container Infra_per_buurt : using = "Invoer/RuimtelijkeData/infra/kentallen"
	{
		attribute<nrAansl>           aant_g_aansl                      (PlanRegio)    := AllocatieResultaten/aant_g_aansl_per_planregio;
		attribute<nrAansl>           aant_g_aansl_woning               (PlanRegio)    := AllocatieResultaten/aant_g_aansl_woning_per_planregio;
		attribute<nrAansl>           aant_g_aansl_woning_hoogb         (PlanRegio)    := AllocatieResultaten/aant_g_aansl_woning_hoogb_per_planregio;
		attribute<nrAansl>           aant_g_aansl_woning_laagb         (PlanRegio)    := AllocatieResultaten/aant_g_aansl_woning_laagb_per_planregio;
		attribute<nrAansl>           aant_g_aansl_util                 (PlanRegio)    := AllocatieResultaten/aant_g_aansl_util_per_planregio;
		
		attribute<nrAansl>           aant_g_aansl_woning_gasloos       (PlanRegio)    := aant_g_aansl_woning - StateVoorAllocatie/Infra_per_buurt/aant_g_aansl_woning;
		attribute<nrAansl>           aant_g_aansl_woning_hoogb_gasloos (PlanRegio)    := aant_g_aansl_woning_hoogb - StateVoorAllocatie/Infra_per_buurt/aant_g_aansl_woning_hoogb;
		attribute<nrAansl>           aant_g_aansl_woning_laagb_gasloos (PlanRegio)    := aant_g_aansl_woning_laagb - StateVoorAllocatie/Infra_per_buurt/aant_g_aansl_woning_laagb;
		attribute<nrAansl>           aant_g_aansl_util_gasloos         (PlanRegio)    := aant_g_aansl_util - StateVoorAllocatie/Infra_per_buurt/aant_g_aansl_util;
		attribute<Ratio>             fractie_laagb_gasloos             (PlanRegio)    := makedefined(aant_g_aansl_woning_laagb_gasloos / aant_g_aansl_woning_gasloos, 0d);
		attribute<Ratio>             fractie_hoogb_gasloos             (PlanRegio)    := makedefined(aant_g_aansl_woning_hoogb_gasloos / aant_g_aansl_woning_gasloos, 0d);
		
		attribute<m>                 lengte_grogel_LD_net              (PlanRegio)    := AllocatieResultaten/GrondroeringInPlanRegio ? 0[m] : StateVoorAllocatie/Infra_per_buurt/lengte_grogel_LD_net;
		attribute<m>                 lengte_LD_net                     (PlanRegio)    := aant_g_aansl == 0[nrAansl] ? 0[m] : StateVoorAllocatie/Infra_per_buurt/lengte_LD_net;
		attribute<m>                 lengte_LS_net                     (PlanRegio)    := StateVoorAllocatie/Infra_per_buurt/lengte_LS_net;
		
		attribute<nrAansl>           aant_won_elec                     (PlanRegio)    := AllocatieResultaten/BestaandeWoning/AllElec_Aansl_inPlanRegio + AllocatieResultaten/NieuwbouwWoning/AllElec_Aansl_inPlanRegio;
		attribute<nrAansl>           aant_util_elec                    (PlanRegio)    := AllocatieResultaten/BestaandeUtil/AllElec_Aansl_inPlanRegio + AllocatieResultaten/NieuwbouwUtil/AllElec_Aansl_inPlanRegio;
		attribute<Ratio>             fractie_3x25_aansl                (PlanRegio)    := StateVoorAllocatie/Infra_per_buurt/fractie_3x25_aansl;
		attribute<kW>                vermogensvraag_woningen           (PlanRegio)    := (ASW_w_zonderWP * AllocatieResultaten/BestaandeWoning/ModelUnits_zonder_WP_inPlanRegio * GTF_w_zonderWP) 
																							+ (ASW_w_metWP * AllocatieResultaten/NieuwbouwWoning/ModelUnits_met_WP_inPlanRegio * GTF_w_metWP);
		attribute<kW>                vermogensvraag_util               (PlanRegio)    := (ASW_u_zonderWP * AllocatieResultaten/BestaandeUtil/ModelUnits_zonder_WP_inPlanRegio  * GTF_u_zonderWP) 
																							+ (ASW_u_metWP * AllocatieResultaten/NieuwbouwUtil/ModelUnits_met_WP_inPlanRegio * GTF_u_metWP);
		attribute<kW>                vermogensvraag_gltb               (PlanRegio)    := (ASW_gltb_zonderWP * AllocatieResultaten/BestaandeGlTb/ModelUnits_zonder_WP_inPlanRegio * GTF_gltb_zonderWP) 
																							+ (ASW_gltb_metWP * AllocatieResultaten/NieuwbouwGlTb/ModelUnits_met_WP_inPlanRegio * GTF_gltb_metWP);
		attribute<MSR>               aant_nieuwe_MSR                   (PlanRegio)    := (max_elem((vermogensvraag_woningen + vermogensvraag_util - StateVoorAllocatie/Infra_per_buurt/capaciteit_buurt), 0[kW]) / P_MSR) ;
		attribute<kW>                capaciteit_buurt                  (PlanRegio)    := StateVoorAllocatie/Infra_per_buurt/capaciteit_buurt + (aant_nieuwe_MSR * P_MSR);
		
		attribute<m_kW>              relatieve_belasting_LS            (PlanRegio)    := lengte_LS_net / (vermogensvraag_woningen + vermogensvraag_util + vermogensvraag_gltb);
		
		container Kosten : using = "Invoer/RuimtelijkeData/infra/kentallen"
		{
			attribute<Eur>                 kosten_verv_LD_net        (PlanRegio)  := Ki_verv_LD_len * (AllocatieResultaten/GrondroeringInPlanRegio ? StateVoorAllocatie/Infra_per_buurt/lengte_grogel_LD_net : 0[m]);
			attribute<Eur>                 kosten_verw_g_aansl_laagb (PlanRegio)  := Ki_verw_g_laagb_asl * (aant_g_aansl - aant_g_aansl_util) * fractie_laagb_gasloos;
			attribute<Eur>                 kosten_verw_g_aansl_hoogb (PlanRegio)  := Ki_verw_g_hoogb_asl * (aant_g_aansl - aant_g_aansl_util) * fractie_hoogb_gasloos;
			attribute<Eur>                 kosten_verw_g_aansl_util  (PlanRegio)  := Ki_verw_g_util_asl  * aant_g_aansl_util_gasloos;
			attribute<Eur>                 kosten_verw_g_aansl       (PlanRegio)  := kosten_verw_g_aansl_laagb + kosten_verw_g_aansl_hoogb + kosten_verw_g_aansl_util;
			attribute<Eur>                 kosten_verw_LD_net        (PlanRegio)  := Ki_verw_LD_len * (StateVoorAllocatie/Infra_per_buurt/lengte_LD_net - lengte_LD_net);
			attribute<Eur>                 kosten_verzw_e_aansl      (PlanRegio)  := Ki_verzw_e_asl * aant_won_elec * (1d - fractie_3x25_aansl);
			attribute<Eur>                 kosten_verzw_MSR          (PlanRegio)  := Ki_MSR * aant_nieuwe_MSR;
			attribute<Eur>                 kosten_verzw_LS           (PlanRegio)  := Ki_verzw_LS_len * relatieve_belasting_LS * max_elem((vermogensvraag_woningen - StateVoorAllocatie/Infra_per_buurt/capaciteit_buurt), 0[kW]);
			
			container cumulatief
			{
				attribute<Eur>                 kosten_verv_LD_net        (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/kosten/cumulatief/kosten_verv_LD_net + kosten/kosten_verv_LD_net;
				attribute<Eur>                 kosten_verw_g_aansl_laagb (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_laagb + kosten/kosten_verw_g_aansl_laagb;
				attribute<Eur>                 kosten_verw_g_aansl_hoogb (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_hoogb + kosten/kosten_verw_g_aansl_hoogb;
				attribute<Eur>                 kosten_verw_g_aansl_util  (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_util + kosten/kosten_verw_g_aansl_util;
				attribute<Eur>                 kosten_verw_g_aansl       (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl + kosten/kosten_verw_g_aansl;
				attribute<Eur>                 kosten_verw_LD_net        (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verw_LD_net + kosten/kosten_verw_LD_net;
				attribute<Eur>                 kosten_verzw_e_aansl      (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_e_aansl + kosten/kosten_verzw_e_aansl;
				attribute<Eur>                 kosten_verzw_MSR          (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_MSR + kosten/kosten_verzw_MSR;
				attribute<Eur>                 kosten_verzw_LS           (PlanRegio)  := StateVoorAllocatie/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_LS + kosten/kosten_verzw_LS;
			}
		}
	}
}