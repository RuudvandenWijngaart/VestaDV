//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template RekenstapT
{
	// begin case parameters
	parameter<string> RekenstapName;
	container PrevState;
	// end case parameters

	parameter<bool>                      isStap0         := RekenstapName == 'StartJaar';
	parameter<Classifications/Rekenstap> Rekenstap_rel   := rlookup(RekenstapName, Classifications/Rekenstap/name);
	parameter<yr_uint16>                 Rekenstap_jaar  := Classifications/Rekenstap/jaar[Rekenstap_rel];
	parameter<string>                    RekenJaarName   := Classifications/RekenJaar/name[Classifications/Rekenstap/RekenJaar_rel          [Rekenstap_rel]];
	parameter<string>                    BouwjaarName    := Classifications/ZichtJaar/name[Classifications/Rekenstap/ZichtJaar_rel_bebouwing[Rekenstap_rel]];
	parameter<string>                    KlimaatjaarName := Classifications/ZichtJaar/name[Classifications/Rekenstap/ZichtJaar_rel_klimaat  [Rekenstap_rel]];

	parameter<Classifications/ZichtJaar> ZichtJaar_rel   := rlookup(BouwjaarName, Classifications/ZichtJaar/name);
	parameter<yr_uint16>                 ZichtJaar_jaar  := Classifications/ZichtJaar/jaar[ZichtJaar_rel];
	parameter<Classifications/RekenJaar> RekenJaar_rel   := Classifications/Rekenstap/RekenJaar_rel[Rekenstap_rel];
	parameter<yr_uint16>                 RekenJaar_jaar  := Classifications/Rekenstap/Jaar         [Rekenstap_rel];

	unit<uint32> PlanRegio           := Invoer/SpecifiekeInstellingen/PlanRegio;

	container SpecifiekeInstellingen := ='Invoer/SpecifiekeInstellingen/' + RekenstapName;
	container VraagKentallen         := SpecifiekeInstellingen/VraagKentallen;
	container AanbodKentallen        := SpecifiekeInstellingen/AanbodKentallen;

	container Bebouwing              := ='RuimtelijkeData/Bebouwing/' + BouwjaarName;

	unit<uint32> WarmteBronnen       := RuimtelijkeData/WarmteBronnen/RestWarmte/data;

	attribute<ratio> KlimaatFactorMapPartial (Geography/rdc_grids/m100) := = 'Invoer/RuimtelijkeData/klimaat/'+ KlimaatjaarName +'[ratio]';
	attribute<ratio> KlimaatFactorMap        (Geography/rdc_grids/m100) := MakeDefined(KlimaatFactorMapPartial, mean(KlimaatFactorMapPartial));

	container Voorkeuren := SpecifiekeInstellingen/Voorkeuren
	{
		container afweging := ='Invoer/SpecifiekeInstellingen/' + RekenstapName + '/voorkeuren/afweging'
		{
		}
		unit<uint8> ActieveGebiedsOptie_Org := range(uint8, 0b, uint8(GebiedsOpties/aantal)) // te kiezen keten van: { GeoTermie, RestWarmte, ... } LET OP: laatste cijfer moet gelijk zijn aan aantal regels onder 'union_data('
		{
			attribute<string> name := SchemaName;

			attribute<Classifications/GebiedsOptie> GebiedsOptie_rel := = GebiedsOpties/Aantal > 0 
				? 'union_data(.'+AsList(',GebiedsOpties/Optie'+string(id(.) + 1b), '')+')' 
				: 'const((0b / 0b)[Classifications/GebiedsOptie], .)';
			attribute<string> SchemaName := Classifications/GebiedsOptie/name[GebiedsOptie_rel];
			attribute<string> code       := Classifications/GebiedsOptie/code[GebiedsOptie_rel];
		}
		unit<uint8> ActieveGebiedsOptie_Ext: = union_unit_uint8(range(uint8,0b, 1b), ActieveGebiedsOptie_Org) // {  RestWarmte, ... } 
		{
			attribute<string> name: = SchemaName;

			attribute<Classifications/GebiedsOptie> GebiedsOptie_rel := union_data(., Classifications/GebiedsOptie/V/RestWarmte, ActieveGebiedsOptie_Org/GebiedsOptie_rel);
			attribute<string>                       SchemaName       := union_data(., 'RestWarmte', ActieveGebiedsOptie_Org/SchemaName);
			attribute<string>                       code             := union_data(., '0', ActieveGebiedsOptie_Org/code);
		}

		parameter<bool> NewRestWarmte := IsDefined(rlookup(Classifications/GebiedsOptie/V/RestWarmte, ActieveGebiedsOptie_Org/GebiedsOptie_rel)); // is nieuwe allocatie toegestaan

		parameter<bool> OldRestWarmte := not(NewRestWarmte);

		unit<uint8> ActieveGebiedsOptie := =NewRestWarmte ? 'ActieveGebiedsOptie_Org' : 'ActieveGebiedsOptie_Ext';
		unit<uint8> PlanRegioOptie      := subset(lookup(ActieveGebiedsOptie/GebiedsOptie_rel, Classifications/GebiedsOptie/IsPlanRegioOptie))
		{
			attribute<string> name := ActieveGebiedsOptie/name[nr_OrgEntity];
			attribute<string> code := ActieveGebiedsOptie/code[nr_OrgEntity];
		}
		unit<uint8> ClusterOptie := subset(not(lookup(ActieveGebiedsOptie/GebiedsOptie_rel, Classifications/GebiedsOptie/IsPlanRegioOptie)))
		{
			attribute<string> name := ActieveGebiedsOptie/name[nr_OrgEntity];
			attribute<string> code := ActieveGebiedsOptie/code[nr_OrgEntity];
		}
	}
	container Schuiven
	{
		parameter<ratio> CurveMax       := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Kosten/LeercurveMinMaxSchuif;
		parameter<ratio> CurveMin       := 1.0 - CurveMax;

		parameter<ratio> LerenAan       := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Kosten/LeercurveGebruikSchuif;
		parameter<ratio> LerenUit       := 1.0 - LerenAan;

		parameter<ratio> KostenMax      : = SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Kosten/VerbeterMinMaxSchuif;
		parameter<ratio> KostenMin      := 1.0 - KostenMax;

		parameter<ratio> OpbrMax        := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Opbrengsten/MinMaxSchuif;
		parameter<ratio> OpbrMin        := 1.0 - OpbrMax;

		parameter<ratio> VerketelingMax := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Kosten/IndividueleVerwarmingSchuif;
		parameter<ratio> VerketelingMin := 1.0 - VerketelingMax;

		parameter<ratio> HuurVerlagingBijGebiedsOptie := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Opbrengsten/HuurVerlagingBijGebiedsOptieSchuif;

		parameter<ratio> EfficiencySchuif := SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/Generiek/Kosten/EfficiencySchuif; // 1.0 = maximale efficiency, 0.0 = minimale efficiency
	}
	container Vermogens := Invoer/Kengetallen/Vermogens(Schuiven/EfficiencySchuif);

	container LeerCurves :=
		for_each_ne(Classifications/leercurves/name,
			'MaakCurve('
				'Kengetallen/Leercurves/Optimistisch/'+Classifications/leercurves/name+', Kengetallen/Leercurves/Pessimistisch/' + Classifications/leercurves/name +
			')'
		);

	container Efficiencies := Invoer/Kengetallen/Efficiency/EfficiencyKentallen(Schuiven/EfficiencySchuif);
	#include<Prijzen.dms>

	attribute<bool> HeeftResterendFactoren (Classifications/BebouwingsComponent) :=
		='union_data(
			Classifications/BebouwingsComponent
			,' + AsList(Classifications/BebouwingsComponent/IsNieuwbouw 
				? 'false' 
				: 'Bebouwing/Has' + Classifications/BebouwingsComponent/name + 'ResterendFactors',
			',')
		+')';

	// TODO_OV: onderzoek of het aantal parameters hier niet wat naar beneden kan; er is veel samengevoegd en geabstraheerd; maak daar gebruik van.
	container BebouwingsComponenten :=
		for_each_ne(Classifications/BebouwingsComponent/name,
			'BebouwingsComponentT('
				'Bebouwing/'+ Classifications/BebouwingsComponent/name +','
				+ quote(Classifications/BebouwingsComponent/name)+','
				+ string(Classifications/BebouwingsComponent/IsNieuwbouw)+','
				+ string(HeeftResterendFactoren)+','
				+(HeeftResterendFactoren ? 'Bebouwing/'+ Classifications/BebouwingsComponent/name +'ResterendFactors' : 'Bebouwing')+','
				'Invoer/Kengetallen/Bebouwing/'+ Classifications/BebouwingsComponent/name +'/results,'
				'Classifications/SchilSprong/'+Classifications/BebouwingsComponent/SchilSprong_sel+','
				'SpecifiekeInstellingen/RuimtelijkeVraag/Lokaal/'+ Classifications/BebouwingsComponent/name +','
				'VraagKentallen/'+ Classifications/BebouwingsComponent/name +','
				'PrevState/Bebouwing/'+ Classifications/BebouwingsComponent/name +','
				'Prijzen/AardGas/'+(Classifications/BebouwingsComponent/IsGlastuinbouw ? 'Glastuinbouw' : 'Staffel')+','
				+ quote(Classifications/BebouwingsComponent/NcwRefBase)+','
				+'1.0 '+(Classifications/BebouwingsComponent/DraagtBTW ? '+ SpecifiekeInstellingen/VAT    / 100[percent]' : '')+','
				+'1.0 '+(Classifications/BebouwingsComponent/DraagtBTW ? '+ SpecifiekeInstellingen/VAT_gv / 100[percent]' : '')
			+')'
		);


	container NCW: isHidden = "True"
	{
		container DV := SpecifiekeInstellingen/DiscontoVoet;
		container IN := SpecifiekeInstellingen/investering;

		container ow13 := NettoContanteWaarde(DV/ow, IN/BeginOpbrengstJaar, 15[yr]);
		container id13 := NettoContanteWaarde(DV/id, IN/BeginOpbrengstJaar, 15[yr]);
		container wd13 := NettoContanteWaarde(DV/wd, IN/BeginOpbrengstJaar, 15[yr]);
		container pt13 := NettoContanteWaarde(DV/pt, IN/BeginOpbrengstJaar, 15[yr]);
		container mr13 := NettoContanteWaarde(DV/mr, IN/BeginOpbrengstJaar, 15[yr]);
		
		container ow28 := NettoContanteWaarde(DV/ow, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container id28 := NettoContanteWaarde(DV/id, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container wd28 := NettoContanteWaarde(DV/wd, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container pt28 := NettoContanteWaarde(DV/pt, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container mr28 := NettoContanteWaarde(DV/mr, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);

		container bw28 := NettoContanteWaarde(DV/E_bw, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container nw28 := NettoContanteWaarde(DV/E_nw, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container bu28 := NettoContanteWaarde(DV/E_bu, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container nu28 := NettoContanteWaarde(DV/E_nu, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container bt28 := NettoContanteWaarde(DV/E_bt, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);
		container nt28 := NettoContanteWaarde(DV/E_nt, IN/BeginOpbrengstJaar, IN/EindOpbrengstJaar);

		container mr40 := NettoContanteWaarde(DV/mr  , 0[yr], 40[yr]);
		
		container mr50 := NettoContanteWaarde(DV/mr  , 0[yr], 50[yr]);
		container bw50 := NettoContanteWaarde(DV/E_bw, 0[yr], 50[yr]);
		container nw50 := NettoContanteWaarde(DV/E_nw, 0[yr], 50[yr]);
		container bu50 := NettoContanteWaarde(DV/E_bu, 0[yr], 50[yr]);
		container nu50 := NettoContanteWaarde(DV/E_nu, 0[yr], 50[yr]);
		container bt50 := NettoContanteWaarde(DV/E_bt, 0[yr], 50[yr]);
		container nt50 := NettoContanteWaarde(DV/E_nt, 0[yr], 50[yr]);

		container mr30 := NettoContanteWaarde(DV/mr  , 0[yr], 30[yr]);
		container bw30 := NettoContanteWaarde(DV/E_bw, 0[yr], 30[yr]);
		container nw30 := NettoContanteWaarde(DV/E_nw, 0[yr], 30[yr]);
		container bu30 := NettoContanteWaarde(DV/E_bu, 0[yr], 30[yr]);
		container nu30 := NettoContanteWaarde(DV/E_nu, 0[yr], 30[yr]);
		container bt30 := NettoContanteWaarde(DV/E_bt, 0[yr], 30[yr]);
		container nt30 := NettoContanteWaarde(DV/E_nt, 0[yr], 30[yr]);

		container mr20 := NettoContanteWaarde(DV/mr  , 0[yr], 20[yr]);
		container bw20 := NettoContanteWaarde(DV/E_bw, 0[yr], 20[yr]);
		container nw20 := NettoContanteWaarde(DV/E_nw, 0[yr], 20[yr]);
		container bu20 := NettoContanteWaarde(DV/E_bu, 0[yr], 20[yr]);
		container nu20 := NettoContanteWaarde(DV/E_nu, 0[yr], 20[yr]);
		container bt20 := NettoContanteWaarde(DV/E_bt, 0[yr], 20[yr]);
		container nt20 := NettoContanteWaarde(DV/E_nt, 0[yr], 20[yr]);

		container mr15 := NettoContanteWaarde(DV/mr  , 0[yr], 15[yr]);
		container bw15 := NettoContanteWaarde(DV/E_bw, 0[yr], 15[yr]);
		container nw15 := NettoContanteWaarde(DV/E_nw, 0[yr], 15[yr]);
		container bu15 := NettoContanteWaarde(DV/E_bu, 0[yr], 15[yr]);
		container nu15 := NettoContanteWaarde(DV/E_nu, 0[yr], 15[yr]);
		container bt15 := NettoContanteWaarde(DV/E_bt, 0[yr], 15[yr]);
		container nt15 := NettoContanteWaarde(DV/E_nt, 0[yr], 15[yr]);
	}

	container StateMetGebouwOpties := PrevState
	{
		container Bebouwing := for_each_nex(Classifications/BebouwingsComponent/name, 'BebouwingsComponenten/'+Classifications/BebouwingsComponent/name+'/results', uint32);
	}
	
	container GebiedsAllocatie :=
		for_each_ne(
			Voorkeuren/ActieveGebiedsOptie/name,
			'CalculationSchemes/AanbodOpties/'+Voorkeuren/ActieveGebiedsOptie/SchemaName+'(
				'+MakeDefined(Voorkeuren/ActieveGebiedsOptie/name[id(Voorkeuren/ActieveGebiedsOptie)-1b]+'/Results', 'StateMetGebouwOpties')+',
				AanbodKentallen/'+Voorkeuren/ActieveGebiedsOptie/SchemaName+', 
				EnergiePrijzen, RekenJaar_jaar
			)'
		)
	{
		parameter<string> ResultStateName := MakeDefined(last(Voorkeuren/ActieveGebiedsOptie/name)+'/Results', 'Initieel');
		container ResultState := =ResultStateName;
	}

	#include<StateNaAllocatie.dms>

	container AllocatieResultaten :=
		for_each_ne(Classifications/BebouwingsComponent/name,
			replace(
				'AllocatieResultatenComponent('
					'StateNaAllocatie/bebouwing/@BC,'
					'''@BC'''
				')'
			,'@BC', Classifications/BebouwingsComponent/name)
		), FreeData = "False"
	{
		unit<uint32> AfnameGebied             := StateNaAllocatie/AfnameGebied;
		unit<uint64> PlanRegio_x_AfnameGebied := combine_uint64(PlanRegio, AfnameGebied);
		unit<uint32> ObjectResults            := ='union_unit('+AsItemList(Classifications/BebouwingsComponent/name+'/BO')+')'
		{
			attribute<PlanRegio   >             PlanRegio_rel                := ='union_data(., '+AsItemList(Classifications/BebouwingsComponent/name+'/BO/PlanRegio_rel'   )+')';
			attribute<AfnameGebied>             AfnameGebied_rel             := ='union_data(., '+AsItemList(Classifications/BebouwingsComponent/name+'/BO/AfnameGebied_rel')+')';
			attribute<PlanRegio_x_AfnameGebied> PlanRegio_x_AfnameGebied_rel := combine_data(PlanRegio_x_AfnameGebied, PlanRegio_rel[PlanRegio], AfnameGebied_rel[AfnameGebied]);			
		}
		unit<uint32> PlanRegioAfnameGebied := unique(ObjectResults/PlanRegio_x_AfnameGebied_rel);

		attribute<string> AfnamegebiedenPerPlanregio(PlanRegio) := AsItemList(
			('{'+AfnameGebied/label+'}')[value(PlanRegioAfnameGebied/values % uint64(#AfnameGebied), AfnameGebied)], value(PlanRegioAfnameGebied/values / uint64(#AfnameGebied), PlanRegio)
		);

		attribute<string> AfnamegebiedenPerPlanregioReport_org   (PlanRegio) := replace(AfnamegebiedenPerPlanregio,"{","","}","","[","","]","",",",";");
		attribute<string> AfnamegebiedenPerPlanregioReport       (PlanRegio) := replace(AfnamegebiedenPerPlanregioReport_org,";",",");

		attribute<bool>    heeft_gas                             (PlanRegio) :=
			( aant_g_aansl_woning_per_planregio
			+ aant_g_aansl_util_per_planregio
			+ StateNaAllocatie/nrAansluitinging/WaterstofHR 
			+ StateNaAllocatie/nrAansluitinging/WaterstofWP ) > 0[nrAsl];

		attribute<nrAsl> aant_g_aansl_per_planregio              (PlanRegio) :=
			= 
			'add('+AsItemList(replace('@C/Aansluitingen/gas + @C/Aansluitingen/H2net','@C', Classifications/BebouwingsComponent/name))+')';
		
		attribute<nrAsl> aant_g_aansl_woning_per_planregio       (PlanRegio) := 
			= 'add(const(0[nrAsl], PlanRegio)'+AsList(replace(', @C/Aansluitingen/gas + @C/Aansluitingen/H2net','@C', Classifications/WoningComponent/name),'')+')';

		attribute<nrAsl> aant_g_aansl_util_per_planregio         (PlanRegio) :=
			= 'add(const(0[nrAsl], PlanRegio)'+AsList(replace(', @C/Aansluitingen/gas + @C/Aansluitingen/H2net','@C', Classifications/UtilComponent/name),'')+')';

		attribute<nrAsl> aant_g_aansl_woning_hoogb_per_planregio (PlanRegio) :=
			= 'add(const(0[nrAsl], PlanRegio)'+AsList(replace(', @C/aant_g_aansl_hoogb_per_planregio','@C', Classifications/WoningComponent/name),'')+')';

		attribute<nrAsl> aant_g_aansl_woning_laagb_per_planregio (PlanRegio) :=
			= 'add(const(0[nrAsl], PlanRegio)'+AsList(replace(', @C/aant_g_aansl_laagb_per_planregio','@C', Classifications/WoningComponent/name),'')+')';

		attribute<bool> GrondroeringInPlanRegio                  (PlanRegio) := ='or('+AsItemList(replace('@BC@/GrondroeringInPlanRegio','@BC@', Classifications/BebouwingsComponent/name))+')';
	}
}