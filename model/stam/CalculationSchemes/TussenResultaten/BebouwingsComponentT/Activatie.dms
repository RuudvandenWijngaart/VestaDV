//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Activatie
{			
	//parameters voor activatie
	parameter<ratio> StartKoudeBasis   := SpecifiekeInstellingen/Probabilisering/StartKoudeBasis;
	parameter<ratio> StartOokKoudePiek := SpecifiekeInstellingen/Probabilisering/StartOokKoudePiek;
	parameter<ratio> SpreidingsFactor  := SpecifiekeInstellingen/Probabilisering/SpreidingsFactor;
	parameter<ratio> StaartFactor      := SpecifiekeInstellingen/Probabilisering/StaartFactor;
	parameter<uint8> RenovatieGrens    := SpecifiekeInstellingen/Probabilisering/RenovatieGrens;
	//end parameters

	container Beleid
	{
		unit<uint32> ZBA := ZichtjaarBeleid/Activatie , ishidden = "True";

		container ActivatieBepaling := for_each_ne(ZBA/name, 'ImplActs(ZBA/V/'+ZBA/name+')');

		template ImplActs : ishidden = "True"
		{
			//case parameters
			parameter<ZBA> ZBA_rel;
			//end case parameters

			parameter<ratio> R_Naleving       := makedefined(ZBA/Naleving[ZBA_rel],0.0[ratio]) , ishidden = "True";
			attribute<bool>  Naleving    (BO) := rnd_uniform(JaarSeed * 104u, BO, range(float32, 0d, 1d)) < R_Naleving;
			attribute<bool>  InDoelgroep (BO) := ='OR('+ AsItemList( replace('ZBA/doelgroep/@ED[ZBA_rel] && BO/Eigendom_rel == Classifications/Eigendom/V/@ED', '@ED', Classifications/Eigendom/name) ) +')';

			container LabelVoorwaarde := for_each_nedv(
				  Classifications/Schillabel/name
				, 'ZBA/Voorwaarde/HuidigLabel/'+Classifications/SchilLabel/name+'[ZBA_rel]'
				, void
				, bool)
			{
				attribute<bool> Voldaan (BO) := ='merge(uint8(Energielabel_rel), bool, '+AsItemList(Classifications/Schillabel/name)+')';
			}

			attribute<bool> BoSelectie(BO) := Naleving && LabelVoorwaarde/Voldaan && InDoelgroep;
			container Grondslag := for_each_nedv(
				  classifications/bouwdeel/name
				, 'ZBA/Grondslag/'+classifications/bouwdeel/name+'[ZBA_rel] && BoSelectie' 
				, BO
				, bool)
			{
				attribute<bool> RV (BO) := ZBA/Grondslag/RV[ZBA_rel] && BoSelectie;
				attribute<bool> TW (BO) := ZBA/Grondslag/TW[ZBA_rel] && BoSelectie;
				attribute<bool> KD (BO) := ZBA/Grondslag/KD[ZBA_rel] && BoSelectie;
				attribute<bool> VT (BO) := ZBA/Grondslag/VT[ZBA_rel] && BoSelectie;
			}
		}

		container BeleidsActivatie := for_each_nedv(
			 classifications/bouwdeel/name
			,count(ZBA/org_rel) == 0[uint32] ? 'const(false,BO)' : replace('OR('+AsItemList('ActivatieBepaling/'+ZBA/name+'/Grondslag/@BD@')+')','@BD@',Classifications/bouwdeel/name)
			,BO
			,bool)
		{
			attribute<bool> RV (BO) := =count(ZBA/org_rel) == 0[uint32] ? 'const(false,BO)' : 'OR('+AsItemList('ActivatieBepaling/'+ZBA/name+'/Grondslag/RV')+')';
			attribute<bool> TW (BO) := =count(ZBA/org_rel) == 0[uint32] ? 'const(false,BO)' : 'OR('+AsItemList('ActivatieBepaling/'+ZBA/name+'/Grondslag/TW')+')';
			attribute<bool> KD (BO) := =count(ZBA/org_rel) == 0[uint32] ? 'const(false,BO)' : 'OR('+AsItemList('ActivatieBepaling/'+ZBA/name+'/Grondslag/KD')+')';
			attribute<bool> VT (BO) := =count(ZBA/org_rel) == 0[uint32] ? 'const(false,BO)' : 'OR('+AsItemList('ActivatieBepaling/'+ZBA/name+'/Grondslag/VT')+')';
		}
	}

	attribute<bool>    Verhuismoment (..) := rnd_uniform(JaarSeed * 2u, .., range(float32, 0d, 1d)) < Classifications/WoningType/Verhuiskans[BO/WoningType_rel];
	
	attribute<float64> KoudeKans     (..) := rnd_uniform(JaarSeed * 3u, .., range(float32, 0d, 1d)), isHidden = "True";
	attribute<bool>    KoudemomentB  (..) := KoudeKans   > 1.0[ratio] - StartKoudeBasis  , Descr = "Jaarlijkse kans dat een gebouweigenaar besluit te starten met de basisvraag naar koude in te vullen" , isHidden = "True";
	attribute<bool>    KoudemomentP  (..) := KoudeKans   > 1.0[ratio] - StartOokKoudePiek, Descr = "Jaarlijkse kans dat een gebouweigenaar besluit te starten ook de piekvraag naar koude in te vullen, indien ook al basis ingevuld" , isHidden = "True";

	parameter<ratio> increment        := 1.0/(1.0-SpreidingsFactor)*StaartFactor, isHidden = "True";

	container Bouwdeel_odds := for_each_nedv(
		 classifications/bouwdeel/name
		,'BO/Bouwdelen/Isolatie/'+ classifications/bouwdeel/name +' == Classifications/IsolatieNiveau/V/N4 ? 0.0[ratio] : Verhuismoment ? 1.0[ratio] :
		 max_elem(min_elem(increment * 
		 ((float64(sub_or_null(Zichtjaar_jaar, BO/Bouwdelen/LastVervanging/' + classifications/bouwdeel/name + ')) / float64(Classifications/Bouwdeel/Levensduur[Classifications/Bouwdeel/V/'+ classifications/bouwdeel/name +'])) - SpreidingsFactor), 1.0[ratio]),0.0[ratio])'
		,(..)
		,float64), isHidden = "True";

	container Bouwdelen_base := for_each_nedv(
		classifications/bouwdeel/name,
		'BO/Bouwdelen/Isolatie/'+ classifications/bouwdeel/name +' != Classifications/IsolatieNiveau/V/N4 && (Verhuismoment || '
		'(MakeDefined(sub_or_null(Zichtjaar_jaar, BO/Bouwdelen/LastVervanging/' + classifications/bouwdeel/name + '), 0w)'
		'>= Classifications/Bouwdeel/Levensduur[Classifications/Bouwdeel/V/'+ classifications/bouwdeel/name +']))',
		(..),
		bool), isHidden = "True";

	container Product_odds := for_each_nedv(
		 classifications/product/name
		,'max_elem(min_elem(increment * ('
			'(float64(sub_or_null(Zichtjaar_jaar, InstallatiePerProduct/LastVervanging/' + classifications/product/name + ')) '
				'/ float64(Classifications/Installatie/Levensduur[InstallatiePerProduct/'+ classifications/product/name +']))'
			'- SpreidingsFactor), 1.0[ratio]),0.0[ratio]) '
			'> rnd_uniform(JaarSeed * (18u+ Classifications/Product/V/'+classifications/product/name+') , ..., range(float64, 0d, 1d))'
		,(..)
		,bool), isHidden = "True";

	container Producten_base := for_each_nedv(
		classifications/product/name,
		'Zichtjaar_jaar >= InstallatiePerProduct/LastVervanging/' + classifications/product/name +
		'+ Classifications/Installatie/Levensduur[InstallatiePerProduct/'+ classifications/product/name +']',
		(..),
		bool), isHidden = "True";

	container Producten_EOL := for_each_nedv(
		classifications/product/name,
		(SpreidingsFactor > 0.99 ? 'Producten_base/' : 'Product_odds/') + classifications/product/name, 
		(..),
		bool), isHidden = "True";

	container Producten
	{
		attribute<bool> RVb (...) := InstallatiePerProduct/RVb == Classifications/Installatie/V/gebiedsOptie ? false : Verhuismoment || Producten_EOL/RVb || Beleid/BeleidsActivatie/RV;
		attribute<bool> RVp (...) := (InstallatiePerProduct/RVp == InstallatiePerProduct/RVb) ? RVb : (Verhuismoment || Producten_EOL/RVp) || Beleid/BeleidsActivatie/RV;
		attribute<bool> TWb (...) := InstallatiePerProduct/TWb == Classifications/Installatie/V/gebiedsOptie ? false : (InstallatiePerProduct/TWb == InstallatiePerProduct/RVb) ? RVb : (Verhuismoment || Producten_EOL/TWb) || Beleid/BeleidsActivatie/TW;
		attribute<bool> TWp (...) := (InstallatiePerProduct/TWp == InstallatiePerProduct/TWb) ? TWb : (Verhuismoment || Producten_EOL/TWp) || Beleid/BeleidsActivatie/TW;

		attribute<bool> KDb (...) := InstallatiePerProduct/KDb == Classifications/Installatie/V/gebiedsOptie ? false : Beleid/BeleidsActivatie/KD || (InstallatiePerProduct/KDb == InstallatiePerProduct/RVb) ? RVb : (Verhuismoment || Producten_EOL/KDb || (InstallatiePerProduct/KDb == Classifications/Installatie/V/geen && KoudemomentB));
		attribute<bool> KDp (...) := Beleid/BeleidsActivatie/KD || ((InstallatiePerProduct/KDb == InstallatiePerProduct/KDp) && (InstallatiePerProduct/KDp != Classifications/Installatie/V/geen)) ? KDb : (Verhuismoment || Producten_EOL/KDb || (InstallatiePerProduct/KDp == Classifications/Installatie/V/geen && KoudemomentP && KDb));	

		attribute<bool> VT  (...) := Beleid/BeleidsActivatie/VT || Bouwdelen/RenovatieMoment || Zichtjaar_jaar - InstallatiePerProduct/LastVervanging/VT >= Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Levensduur [ InstallatiePerProduct/VT ] 	;
	//	attribute<bool> VT  (...) := Beleid/BeleidsActivatie/VT || Zichtjaar_jaar - InstallatiePerProduct/LastVervanging/VT >= Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Levensduur [ InstallatiePerProduct/VT ] 	;
	}

	container Bouwdelen := Bouwdelen_base
	{
		attribute<Bool> RenovatieMoment (...) := add(
			uint8( Bouwdelen_base/RB || Bouwdelen_base/RO || Bouwdelen_base/DR ),
			uint8( Bouwdelen_base/VL ),
			uint8( Bouwdelen_base/MG || Bouwdelen_base/MS || Bouwdelen_base/PL ),
			uint8( Bouwdelen_base/DP || Bouwdelen_base/DS )
				) > RenovatieGrens ;
		
		attribute<Bool> MG (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/MG ? true : Bouwdelen_base/MG' : 'RenovatieMoment || Beleid/BeleidsActivatie/MG ? true : Bouwdeel_odds/MG > rnd_uniform(JaarSeed * 8u , ..., range(float64, 0d, 1d))';
		attribute<Bool> DP (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/DP ? true : Bouwdelen_base/DP' : 'RenovatieMoment || Beleid/BeleidsActivatie/DP ? true : Bouwdeel_odds/DP > rnd_uniform(JaarSeed * 9u , ..., range(float64, 0d, 1d))';
		attribute<Bool> DS (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/DS ? true : Bouwdelen_base/DS' : 'RenovatieMoment || Beleid/BeleidsActivatie/DS ? true : Bouwdeel_odds/DS > rnd_uniform(JaarSeed * 10u, ..., range(float64, 0d, 1d))';
		attribute<Bool> RB (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/RB ? true : Bouwdelen_base/RB' : 'RenovatieMoment || Beleid/BeleidsActivatie/RB ? true : Bouwdeel_odds/RB > rnd_uniform(JaarSeed * 11u, ..., range(float64, 0d, 1d))';
		attribute<Bool> RO (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/RO ? true : Bouwdelen_base/RO' : 'RenovatieMoment || Beleid/BeleidsActivatie/RO ? true : Bouwdeel_odds/RO > rnd_uniform(JaarSeed * 12u, ..., range(float64, 0d, 1d))';
		attribute<Bool> KR (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/KR ? true : Bouwdelen_base/KR' : 'RenovatieMoment || Beleid/BeleidsActivatie/KR ? true : Bouwdeel_odds/KR > rnd_uniform(JaarSeed * 13u, ..., range(float64, 0d, 1d))';
		attribute<Bool> VL (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/VL ? true : Bouwdelen_base/VL' : 'RenovatieMoment || Beleid/BeleidsActivatie/VL ? true : Bouwdeel_odds/VL > rnd_uniform(JaarSeed * 14u, ..., range(float64, 0d, 1d))';
		attribute<Bool> DR (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/DR ? true : Bouwdelen_base/DR' : 'RenovatieMoment || Beleid/BeleidsActivatie/DR ? true : Bouwdeel_odds/DR > rnd_uniform(JaarSeed * 15u, ..., range(float64, 0d, 1d))';

		attribute<Bool> MS (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/MS || Bouwdelen_base/MG ? true : Bouwdelen_base/MS' : 'RenovatieMoment || Beleid/BeleidsActivatie/MS || Bouwdelen_base/MG ? true : Bouwdeel_odds/MS > rnd_uniform(JaarSeed * 16u, ..., range(float64, 0d, 1d))';
		attribute<Bool> PL (...) := =SpreidingsFactor > 0.99 ? 'RenovatieMoment || Beleid/BeleidsActivatie/PL || Bouwdelen_base/MG ? true : Bouwdelen_base/PL' : 'RenovatieMoment || Beleid/BeleidsActivatie/PL || Bouwdelen_base/MG ? true : Bouwdeel_odds/PL > rnd_uniform(JaarSeed * 17u, ..., range(float64, 0d, 1d))';
	}

	attribute<bool> BouwdeelVervanging (..) := ='add('+AsItemList('uint8(Bouwdelen/'+Classifications/bouwdeel/name+')')+') > 0b';
	attribute<bool> ProductVervanging  (..) := ='add('+AsItemList('uint8(Producten/'+Classifications/product/name+')')+') > 0b';

	attribute<bool> KanNietActiveren   (..) := =Invoer/DefaultInstellingen/Basis/RuimtelijkeOntwikkelingAan ? 
		'MakeDefined(sub_or_null(Sloopjaar, Zichtjaar_jaar), 9999w) < 10[yr_uint16] || MakeDefined(sub_or_null(Zichtjaar_jaar, Bouwjaar), 9999w) < 10[yr_uint16] || Afbouw || Aanbouw || not(nrAansluitingen > 0.0[nrAsl])' :
		'const(false,..)';
	attribute<bool> BouwdeelActief     (..) := =RekenStapName == 'StartJaar' ? 'const(false,..)' : 'BouwdeelVervanging && not(KanNietActiveren)';
	attribute<bool> ProductActief      (..) := =RekenStapName == 'StartJaar' ? 'const(false,..)' : 'ProductVervanging  && not(KanNietActiveren)';

	attribute<classifications/Uitvoering> Uitvoering (..) := switch(
		  case(AND(Bouwdelen/RenovatieMoment, IsMeergezins                                                     ), Classifications/Uitvoering/V/Nat_Mgw_Prj)
	//	, case(AND(Bouwdelen/RenovatieMoment, IsMeergezins                                                     ), Classifications/Uitvoering/V/Nat_Mgw_Ind) //Meergezinswoningen zijn altijd projectmatig (zet dit weer aan om aanname anders te maken)
		, case(AND(Bouwdelen/RenovatieMoment              , Eigendom_rel == Classifications/Eigendom/V/WoonCorp), Classifications/Uitvoering/V/Nat_Egw_Prj)
		, case(AND(Bouwdelen/RenovatieMoment                                                                   ), Classifications/Uitvoering/V/Nat_Egw_Ind)
		, case(AND(                           IsMeergezins                                                     ), Classifications/Uitvoering/V/Zst_Mgw_Prj)
	//	, case(AND(                           IsMeergezins                                                     ), Classifications/Uitvoering/V/Zst_Mgw_Ind) //Meergezinswoningen zijn altijd projectmatig (zet dit weer aan om aanname anders te maken)
		, case(AND(                                         Eigendom_rel == Classifications/Eigendom/V/WoonCorp), Classifications/Uitvoering/V/Zst_Egw_Prj)
		, 		                                                                                                  Classifications/Uitvoering/V/Zst_Egw_Ind);

}