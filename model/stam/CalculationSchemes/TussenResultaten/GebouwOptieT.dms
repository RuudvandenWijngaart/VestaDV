//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template SubsetGebouwOptieT
{
	// begin case parameters	
	unit<uint32> ActiefObject;
	// end case parameters
	
	// TODO: Wegsubstitueren
	attribute<Classifications/GebouwOptie>     GebouwOptie_rel    (ActiefObject) := ActiefObject/GebouwOptie_rel;
	attribute<bool>                            Criterium          (ActiefObject) := ActiefObject/Criterium;
	attribute<Classifications/IsolatieAmbitie> MinIsolatieAmbitie_rel(ActiefObject) := ActiefObject/MinIsolatieAmbitie_rel;
	attribute<Classifications/IsolatieAmbitie> MaxIsolatieAmbitie_rel(ActiefObject) := ActiefObject/MaxIsolatieAmbitie_rel;

	attribute<bool> KoudeGeschikt (ActiefObject) := (BO/Activatie/Producten/KDb[ActiefObject/BO_rel] || BO/Activatie/Producten/KDp[ActiefObject/BO_rel]) == Classifications/GebouwOptie/MetKoude[GebouwOptie_rel];
	
	attribute<bool> LabelGeschikt (ActiefObject) := 
	AND( ActiefObject/SchilLabel_rel <= Classifications/GebouwOptie/Min_schillabel_rel[GebouwOptie_rel]
	,	OR(BO/Aansluitingen/GNet[ActiefObject/BO_rel] > 0[nrAsl], not(Classifications/GebouwOptie/GasNodig[GebouwOptie_rel]))
	)
	, Descr = "Voldoet de woning aan de isolatienorm voor deze gebouwoptie en is benodigde aansluiting aanwezig?";
	
	// TODO: check op dat Input van Installatie wel Aangesloten kan worden; Beinvloedt dit de activatie of alleen de invulling ?
	parameter<string> IPP_active_expr  := 'Classifications/GebouwOptie/Installatie_rel/@PN[GeschiktObject/GebouwOptie_rel]', ishidden = "True";
	parameter<string> IPP_active_cond  := IPP_active_expr+' != Classifications/Installatie/V/geen && BO/Activatie/Producten/@PN[GeschiktObject/BO_rel]', ishidden = "True"; 
	parameter<string> IPP_default_expr := 'BO/InstallatiePerProduct/@PN[GeschiktObject/BO_rel]', ishidden = "True";
		
	parameter<string> IPP_invest_cond := IPP_active_cond + '&& (BO/Activatie/Producten_EOL/@PN[GeschiktObject/BO_rel] || GeschiktObject/InstallatiePerProduct/@PN != BO/InstallatiePerProduct/@PN[BO_rel])', ishidden = "True"; // EOL of Veranderd ? 
		
	//===== Selectie van woningen die voldoen aan de minimale isolatiegraad voor de gebouwoptie in kwestie =====
	unit<uint32> GeschiktObject := select_unit(LabelGeschikt && KoudeGeschikt && Criterium), ishidden = "True"
	{
		attribute<uint32>                      BO_rel           := select_data(GeschiktObject, ActiefObject/BO_rel);
		attribute<Classifications/SchilLabel>  SchilLabel_rel   := select_data(GeschiktObject, ActiefObject/SchilLabel_rel);
		attribute<Classifications/SchilLabel>  Energielabel_rel := ../Energielabel_rel;
		attribute<Classifications/GebouwOptie> GebouwOptie_rel  := select_data(GeschiktObject, ../GebouwOptie_rel);
		attribute<Classifications/IsolatieAmbitie> MinIsolatieAmbitie_rel := select_data(GeschiktObject, ../MinIsolatieAmbitie_rel);
		attribute<Classifications/IsolatieAmbitie> MaxIsolatieAmbitie_rel := select_data(GeschiktObject, ../MaxIsolatieAmbitie_rel);
		
		container AansluitCapaciteit
		{
			attribute<kW> RV (GeschiktObject) := select_data(GeschiktObject, ActiefObject/AansluitCapaciteit/RV);
			attribute<kW> TW (GeschiktObject) := select_data(GeschiktObject, ActiefObject/AansluitCapaciteit/TW);
			attribute<kW> KD (GeschiktObject) := select_data(GeschiktObject, ActiefObject/AansluitCapaciteit/KD);
		}

		container Bouwdelen := for_each_nedv(Classifications/Bouwdeel/name
			,	replace('BO/Bouwdelen/@BN[BO_rel]', '@BN', Classifications/Bouwdeel/name)
			,	GeschiktObject, m2
			)
		{
			container Isolatie := for_each_nedv(Classifications/Bouwdeel/name
			,	replace('select_data(GeschiktObject, ActiefObject/Bouwdelen/Isolatie/@BN)', '@BN', Classifications/Bouwdeel/name)
			,	GeschiktObject, Classifications/IsolatieNiveau
			);
		}
		container Functioneel := for_each_nedv(Classifications/FunctioneleVraag/name
			,	Classifications/FunctioneleVraag/name == 'VT' ? 'OverigeInstallaties/Ventilatie/Afweging/V_E' :
				Classifications/FunctioneleVraag/name == 'RV' ? 'select_data(GeschiktObject, ActiefObject/Functioneel/RV) - BO/Functioneel/Vrv_VT[BO_rel] + OverigeInstallaties/Ventilatie/Afweging/V_RV' :
				replace('select_data(GeschiktObject, ActiefObject/Functioneel/@BN)', '@BN', Classifications/FunctioneleVraag/name)
			,	GeschiktObject, GJ_yr
			);
		
		container InstallatiePerProduct := for_each_nedv(Classifications/Product/name
			, replace(IPP_active_cond+'?'+IPP_active_expr+':'+IPP_default_expr, '@PN', Classifications/Product/name)
			, GeschiktObject
			, Classifications/Installatie
		)
		{
			attribute<Classifications/AfgifteSysteem> AS (GeschiktObject) :=  max_elem(
				 BO/InstallatiePerProduct/AS[BO_rel]
				,Classifications/Performance/AfgifteSysteem_rel[Func2Meter/SPF/PerformancePerProduct/RVb]
				,Classifications/Performance/AfgifteSysteem_rel[Func2Meter/SPF/PerformancePerProduct/RVp]);
			

			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> VT (GeschiktObject) := OverigeInstallaties/ventilatie/Installatietype_rel;
			attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> DK (GeschiktObject) := OverigeInstallaties/zonPV/Installatietype_rel     ;
			attribute<Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes> KK (GeschiktObject) := OverigeInstallaties/koken/Installatietype_rel     ;
		}	
		
		// code copied form GebouwOptie maar nu per GeschiktObject vanwege rol specifieke activatie op voor welk een installatie vervanging is.
		unit<uint32> xProduct := combine(Classifications/Product, GeschiktObject)
		{
			attribute<Classifications/Product>      Product_rel     := nr_1;
			attribute<GeschiktObject> GeschiktObject_rel := nr_2;
			attribute<Classifications/GebouwOptie>  GebouwOptie_rel := GeschiktObject_rel->GebouwOptie_rel;
//			attribute<string>       label           := BO_rel->Label + ':'+Classifications/Product/name[Product_rel] + '.' + GebouwOptie/name[GebouwOptie_rel];
			attribute<Classifications/Installatie>  Installatie_rel := ='union_data(., '+AsItemList('GeschiktObject/GebouwOptie_rel->PerProduct/'+Classifications/Product/name)+')';
			attribute<bool>         IsActief        := ='union_data(.,'+AsItemList(replace(IPP_invest_cond, '@PN', Classifications/Product/name))+')'; 
				
			attribute<xInstallatie> xInstallatie_code := point(UInt32(xProduct/GeschiktObject_rel), UInt32(IsActief? xProduct/Installatie_rel : classifications/Installatie/V/geen), xInstallatie);
			attribute<uInstallatie> uInstallatie_rel := rlookup(xInstallatie_code, uInstallatie/Values);
			
			attribute<.> per_uInstallatie(uInstallatie) :=  uInstallatie/First_xProduct_rel;
			
			attribute<Eur_asl> Ki_asl := sum(VerbruiksOpties/Installatie/Ki_asl[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_m2 > Ki_opp := sum(VerbruiksOpties/Installatie/Ki_opp[uInstallatie/Installatie_rel], per_uInstallatie);

			attribute<Eur_yr_asl> Kj_Adm_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/R_Adm)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_yr_asl> Kj_OH_asl  := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/R_OH )[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_yr_m2 > Kj_Adm_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/R_Adm)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_yr_m2 > Kj_OH_opp  := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/R_OH )[uInstallatie/Installatie_rel], per_uInstallatie);

			attribute<Eur_asl> Ki30_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT30)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_asl> Ki20_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT20)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_asl> Ki15_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT15)[uInstallatie/Installatie_rel], per_uInstallatie);

			attribute<Eur_m2 > Ki30_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT30)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_m2 > Ki20_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT20)[uInstallatie/Installatie_rel], per_uInstallatie);
			attribute<Eur_m2 > Ki15_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT15)[uInstallatie/Installatie_rel], per_uInstallatie);
		}

		unit<upoint> xInstallatie;
		unit<uint32> uInstallatie := unique(xProduct/xInstallatie_code) 
		{
			attribute<GeschiktObject>              GeschiktObject_rel := value(pointrow(values), GeschiktObject);
			attribute<Classifications/Installatie> Installatie_rel    := value(pointcol(values), Classifications/Installatie);
			attribute<xProduct>                    First_xProduct_rel := rlookup(values, xProduct/xInstallatie_code);
			attribute<Classifications/Product>     FirstProduct_rel   := xProduct/Product_rel[First_xProduct_rel];
		}		
		
		attribute<Eur_asl> Ki_asl := sum(VerbruiksOpties/Installatie/Ki_asl[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_m2 > Ki_opp := sum(VerbruiksOpties/Installatie/Ki_opp[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);

		attribute<Eur_yr_asl> Kj_Adm_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/R_Adm)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_yr_asl> Kj_OH_asl  := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/R_OH )[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_yr_m2 > Kj_Adm_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/R_Adm)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_yr_m2 > Kj_OH_opp  := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/R_OH )[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);

		attribute<Eur_asl> Ki30_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT30)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_asl> Ki20_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT20)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_asl> Ki15_asl := sum((VerbruiksOpties/Installatie/Ki_asl * VerbruiksOpties/Installatie/AT15)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);

		attribute<Eur_m2 > Ki30_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT30)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_m2 > Ki20_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT20)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
		attribute<Eur_m2 > Ki15_opp := sum((VerbruiksOpties/Installatie/Ki_opp * VerbruiksOpties/Installatie/AT15)[uInstallatie/Installatie_rel], uInstallatie/GeschiktObject_rel);
	}
	
	//===== InstallatiePerProduct geeft per BebouwingsObject of per SchilLabel voor ieder Product de te kiezen of al aanwezige installatie =====
	
	container Func2Meter := CalculationSchemes/FunctioneelToMetervraag(GeschiktObject);
	
	// cap_prod geeft per BebouwingsObject of per SchilLabel voor ieder Product de benodigde capaciteit obv het vastgestelde profiel.
	
	unit<uint8>  VGO := VerbruiksOpties/GebouwOptie, ishidden = "True";
	unit<uint32> VGO_xProduct := VerbruiksOpties/GebouwOptie/xProduct, ishidden = "True";
/* REMOVE, zit nu in GeschiktObject	
	unit<uint32> xProduct := combine_unit(GeschiktObject, Classifications/Product)
	{
		attribute<GeschiktObject>              GeschiktObject_rel := value(ID(.) / #Classifications/Product, GeschiktObject);
//		attribute<ActiefObject>                ActiefObject_rel   := GeschiktObject_rel->ActiefObject_rel;
		attribute<Classifications/Product>     Product_rel        := value(ID(.) % #Classifications/Product, Classifications/Product);
		attribute<VGO_xProduct>                VGO_xProduct_rel   := combine_data(VerbruiksOpties/GebouwOptie/xProduct, GeschiktObject_rel->GebouwOptie_rel, Product_rel);
		attribute<Classifications/Installatie> Installatie_rel    := VGO_xProduct_rel->(uInstallatie_rel->Installatie_rel); // neemt alleen een installatie bij eerste product waar het voor komt.
	}
*/	
	parameter<string> KX_cap_Expr :=
			'merge(uint8(GeschiktObject/xProduct/Product_rel)'
				', kW'
				', '+AsItemList('Func2Meter/eEffect/CapaciteitPerProduct/'+Classifications/Product/name+'[GeschiktObject/xProduct/GeschiktObject_rel]')+
			') * '
			'VerbruiksOpties/Installatie/K@X_cap[GeschiktObject/xProduct/Installatie_rel]', ishidden = "True";
		
	attribute<Eur   > Ki_cap     (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'i');
	attribute<Eur   > Ki30_cap   (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'i30');
	attribute<Eur   > Ki20_cap   (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'i20');
	attribute<Eur   > Ki15_cap   (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'i15');
	attribute<Eur_yr> Kj_OH_cap  (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'j_OH');
	attribute<Eur_yr> Kj_Adm_cap (GeschiktObject/xProduct) := =replace(KX_cap_Expr, '@X', 'j_Adm');

	attribute<Eur   > Ki_LTAS (GeschiktObject) := 
		(GeschiktObject/InstallatiePerProduct/AS >= Classifications/AfgifteSysteem/V/LTAS) && (BO/InstallatiePerProduct/AS[GeschiktObject/BO_rel] < Classifications/AfgifteSysteem/V/LTAS)
			? (BO/IsMeergezins[GeschiktObject/BO_rel]
				? BO/BCkentallen/Ki_LTAS_h_asl 
				: BO/BCkentallen/Ki_LTAS_l_asl) * BO/nrAansluitingen[GeschiktObject/BO_rel] + BO/BCkentallen/Ki_LTAS_opp * BO/Oppervlakte[GeschiktObject/BO_rel]
			: const(0[Eur], GeschiktObject);
			
	// #TRELLO35 KiXX per product tbv per installatie varierende EIA
	// #TRELLO35 neem alleen vervanging mee; kapitaallasten vna installaties die er al staat (mits niet toe aan vervanging) weegt niet mee in afweging voor iets nieuws
	parameter<string> KX_expr :=
		'add('
			'GeschiktObject/xProduct/K@X_asl * BO/nrAansluitingen[GeschiktObject/xProduct/GeschiktObject_rel->BO_rel],  '
			'GeschiktObject/xProduct/K@X_opp * BO/Oppervlakte[GeschiktObject/xProduct/GeschiktObject_rel->BO_rel] , ' 
			'K@X_cap'
		+')';

	attribute<Eur   > Ki_LO       (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'i');
	attribute<Eur   > Ki30_LO     (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'i30');
	attribute<Eur   > Ki20_LO     (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'i20');
	attribute<Eur   > Ki15_LO     (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'i15');	
	
	container Jaarlijks := Kj_verbruik
	{	
		attribute<Eur_yr> KJ_OH_LO       (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'j_OH');
		attribute<Eur_yr> Kj_Adm_LO      (GeschiktObject/xProduct) := =replace(KX_expr, '@X', 'j_Adm');
		attribute<Eur_yr> Kj_Vastrecht_g (GeschiktObject) := VastRechtBijdrageG; // bijdrage en heffingskortingen voor E, MT en LT zijn voor alle GebouwOpties hetzelfde, dus niet relevant voor de afweging.
	}
	
	attribute<Eur>    Ki_gv       (GeschiktObject) := leerCurves/Isolatie/Curve * select_data(GeschiktObject, ActiefObject/Ki_GV );
	attribute<Eur_yr> Kji_gv      (GeschiktObject) := leerCurves/Isolatie/Curve * select_data(GeschiktObject, ActiefObject/Kji_GV);
	attribute<Eur_yr> Kmi_gv      (GeschiktObject) := leerCurves/Isolatie/Curve * select_data(GeschiktObject, ActiefObject/Kmi_GV);

	attribute<Eur>    Ki_totaal   (GeschiktObject) := sum(Ki_LO  , GeschiktObject/xProduct/GeschiktObject_rel)  + Ki_GV + Ki_LTAS;
	attribute<Eur>    Ki30_totaal (GeschiktObject) := sum(Ki30_LO, GeschiktObject/xProduct/GeschiktObject_rel) + Ki_GV + Ki_LTAS;
	attribute<Eur>    Ki20_totaal (GeschiktObject) := sum(Ki20_LO, GeschiktObject/xProduct/GeschiktObject_rel);
	attribute<Eur>    Ki15_totaal (GeschiktObject) := sum(Ki15_LO, GeschiktObject/xProduct/GeschiktObject_rel);
	
	container OverigeInstallaties
	{	
		//================ Berekeningen en afwining Ventilatie-installaties
		container ventilatie
		{
			container Afweging
			{
				//====== Huidige ventilatiesysteem
				attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/VT[GeschiktObject/BO_rel];
				attribute<bool>                                                       VentilatieActief    (GeschiktObject) := BO/Activatie/producten/VT[GeschiktObject/BO_rel];

				//====== Selectie van ventilatiesystemen die in huidige zichtjaar toegepast kunnen worden
				unit<uint32> Ventilatieopties := subset(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/startjaar <= Zichtjaar_jaar && Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/eindjaar >= Zichtjaar_jaar)
				{
					attribute<string>    name       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/name[nr_orgentity];
					attribute<GJ_yr_asl> V_RV_asl   := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_RV_asl[nr_orgentity];
					attribute<GJ_yr_asl> V_E_asl    := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_E_asl[nr_orgentity];
					attribute<yr_uint16> Levensduur := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Levensduur[nr_orgentity];
					attribute<float64>   P50P       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/P50P[nr_orgentity];
					attribute<float64>   BETA       := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/BETA[nr_orgentity];

//					container Ki_asl := for_each_nedv(Classifications/Uitvoering/Name, 'leerCurves/Ventilatie/curve * Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Ki_asl/'+Classifications/Uitvoering/Name+'[nr_orgentity]',Ventilatieopties ,eur_asl);
					container V      := for_each_nedv(name, string(id(.))+'[..]', void, .);
				}

				parameter<float64> LOG_MAX_FLOAT64 := 700.0; // slightly less than ln( 2^1024 / pcount(xInvesteringsOptie/ActieveWoning_rel) )

				//====== Berekeningen per ventilatiesysteem t.b.v. afweging
				container PerOptie := for_each_ne(Ventilatieopties/Name, 'OptieBerekening(Ventilatieopties/V/'+Ventilatieopties/Name+')');
				template OptieBerekening
				{
					//case parameter
					parameter<Ventilatieopties> Ventilatieoptie_rel;
					//end
					parameter<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype := rlookup(Ventilatieopties/name[Ventilatieoptie_rel], Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/name);

					unit<uint32> VentilatieMaatregel := subset(Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Installatie_na == Installatietype)
					{
						attribute<string> name := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/name[nr_orgentity];
						attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_voor := Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Installatie_voor[nr_orgentity];

						container Ki_asl := for_each_nedv(Classifications/Uitvoering/Name, 'leerCurves/Ventilatie/curve * Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/InstallatieMaatregel/Ki_asl/'+Classifications/Uitvoering/Name+'[nr_orgentity]',VentilatieMaatregel ,eur_asl);
					}
					attribute<VentilatieMaatregel> VentilatieMaatregel_rel (GeschiktObject) := rlookup(Installatietype_org, VentilatieMaatregel/Installatietype_voor);


					attribute<GJ_yr>   V_RV       (GeschiktObject) :=
						  BO/nrAansluitingen[GeschiktObject/BO_rel]
						* Ventilatieopties/V_RV_asl[Ventilatieoptie_rel]
						* BO/Functioneel_base/Gedragfactor/VT
						* BO/Functioneel_base/Klimaateffect[GeschiktObject/BO_rel]
						* BO/Functioneel_base/Fitfactor_VT[GeschiktObject/BO_rel];

					attribute<GJ_yr>   V_E        (GeschiktObject) := BO/nrAansluitingen[GeschiktObject/BO_rel]     * Ventilatieopties/V_E_asl[Ventilatieoptie_rel]  * BO/Functioneel_base/Gedragfactor/VT * BO/Functioneel_base/Fitfactor_VT[GeschiktObject/BO_rel];
					attribute<Eur_yr>  Kj_energie (GeschiktObject) := first(Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl) * V_E + first(Prijzen/AardGas/Staffel/KGJ_eindgebruik_excl) * V_RV; //TODO: koppelen aan huidige installaties

					attribute<Eur>     Ki_VT      (GeschiktObject) := ='BO/nrAansluitingen[GeschiktObject/BO_rel] * merge(uint8(BO/Activatie/Uitvoering[GeschiktObject/BO_rel]), eur_asl, '+AsItemList('VentilatieMaatregel/Ki_asl/'+Classifications/Uitvoering/Name+'[VentilatieMaatregel_rel]')+')';

					attribute<Eur_yr>  Kji_VT     (GeschiktObject) := ='Ki_VT * NcwRefs/_'+string(Ventilatieopties/Levensduur[Ventilatieoptie_rel])+'/AnnualisationFactor';

					attribute<Eur_yr>  kosten_tbv_afweging    (GeschiktObject) := (Kj_energie * SplitIncentiveVerbruik + Kji_VT) * BTW_Factor * Ventilatieopties/P50P[Ventilatieoptie_rel];

					attribute<float64> Geschiktheid (GeschiktObject) := Choice/Kj_duursteoptie - kosten_tbv_afweging;
					attribute<float64> Odds         (GeschiktObject) := IsDefined(Geschiktheid) ? exp(min_elem(Geschiktheid * Ventilatieopties/beta[Ventilatieoptie_rel], LOG_MAX_FLOAT64)) : 0.0;
					attribute<float64> Probability  (GeschiktObject) := Odds / Choice/TotalOdds;
					attribute<float64> Cumulprob    (GeschiktObject) := ='add('+AsItemList('float64(PerOptie/'+Ventilatieopties/Name+'/Ventilatieoptie_rel <= Ventilatieoptie_rel) * PerOptie/'+Ventilatieopties/Name+'/Probability')+')';
					attribute<bool>    IsSelected   (GeschiktObject) := CumulProb - Probability <= Choice/rnd && Choice/rnd < CumulProb;

					attribute<Ventilatieopties> Ventilatieoptie_choice (GeschiktObject) := IsSelected ? Ventilatieoptie_rel : (0/0)[Ventilatieopties];
					attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Type_choice (GeschiktObject) := IsSelected ? Installatietype : (0/0)[Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes];
				}
				
				//====== Afweging tussen verschillende ventilatiesystemen
				container Choice
				{
					attribute<Eur_yr>  Kj_duursteoptie (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Kosten_tbv_afweging')+')';
					attribute<float64> TotalOdds       (GeschiktObject) := ='add('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Odds')+')';
					attribute<float64> rnd             (GeschiktObject) := rnd_uniform(JaarSeed * 19u, GeschiktObject, range(float64, 0d, 1d));

					attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_def (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Type_choice')+')';
					attribute<Ventilatieopties>                                           Ventilatieoptie_def (GeschiktObject) := ='Max_elem('+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Ventilatieoptie_choice')+')';

					//====== Definitievie waarden gekozen systeem
					attribute<Eur>     Ki_VT           (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), Eur,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/Ki_VT')+')';
					attribute<GJ_yr>   V_RV            (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), GJ_yr,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/V_RV')+')';
					attribute<GJ_yr>   V_E             (GeschiktObject) := ='merge(uint8(Ventilatieoptie_def), GJ_yr,'+AsItemList('PerOptie/'+Ventilatieopties/Name+'/V_E')+')';
				}

				attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_def (GeschiktObject) := VentilatieActief ? Choice/Installatietype_def : Installatietype_org;
				
				//====== waarden gekozen ventilatiesysteem
				attribute<eur>   Ki_vent (GeschiktObject) := Choice/Ki_VT;
				attribute<GJ_yr> V_RV    (GeschiktObject) := Choice/V_RV;
				attribute<GJ_yr> V_E     (GeschiktObject) := Choice/V_E;

			}
			//====== Gekozen ventilatiesysteem
			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> Installatietype_rel (GeschiktObject) := Afweging/Installatietype_def;
		}
		//================

		container zonB
		{
			container afweging
			{
				attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/DK[GeschiktObject/BO_rel];
				
				//TODO: hier afweging invoegen voor keuze zon
				attribute<bool> KeuzeZonB (GeschiktObject) := (Installatietype_org == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonPV_gr) ? false : false; 
				
				attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_def (GeschiktObject) :=
					KeuzeZonB ? 
						Installatietype_org == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonPV_kl ? 
							  Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonB_PVkl
							: Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonPV_kl
					: Installatietype_org;
				
				attribute<bool> IsChangedNow (GeschiktObject) := Installatietype_org != Installatietype_def;
				attribute<kW>    P           (GeschiktObject) := Func2Meter/ZonB/P;
				attribute<eur>  Ki_zonB      (GeschiktObject) := float64(IsChangedNow) * (
					(BO/nrAansluitingen[GeschiktObject/BO_rel] * 
						( Schuiven/KostenMin * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_asl_min[Installatietype_def]
						+ Schuiven/KostenMax * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_asl_max[Installatietype_def])) +
					(P * 
						( Schuiven/KostenMin * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_cap_min[Installatietype_def]
						+ Schuiven/KostenMax * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_cap_max[Installatietype_def]))
						);
			}
		}
		
		container zonPV
		{
			container Afweging
			{
				attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/DK[GeschiktObject/BO_rel];
				
				attribute<bool> KeuzePVgroot (GeschiktObject) := Installatietype_org == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonB ? false : false; //TODO: hier afweging invoegen
				attribute<bool> KeuzePVklein (GeschiktObject) := const(false,GeschiktObject); //TODO: hier afweging invoegen
				
				attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_def (GeschiktObject) :=
					  KeuzePVgroot ? Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonPV_gr
					: KeuzePVklein ? 
						Installatietype_org == Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonB ? 
							  Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonB_PVkl
							: Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/V/ZonPV_kl
					: Installatietype_org;
				
				attribute<bool> IsChangedNow (GeschiktObject) := Installatietype_org != Installatietype_def;
				attribute<kW>    P           (GeschiktObject) := Func2Meter/eEffect/ZonPV/P;
				attribute<eur>  Ki_zonPV     (GeschiktObject) := float64(IsChangedNow) * (
					(BO/nrAansluitingen[GeschiktObject/BO_rel] * 
						( Schuiven/KostenMin * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_asl_min[Installatietype_def]
						+ Schuiven/KostenMax * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_asl_max[Installatietype_def])) +
					(P * 
						( Schuiven/KostenMin * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_cap_min[Installatietype_def]
						+ Schuiven/KostenMax * Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Ki_cap_max[Installatietype_def]))
						);
			}
			attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> Installatietype_rel (GeschiktObject) := Afweging/Installatietype_def;
		}
		
		container koken
		{
			unit<uint32> Inst := Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes;
			container Afweging
			{
				parameter<ratio> KookElekNatuurlijk  := SpecifiekeInstellingen/Probabilisering/KookElekNatuurlijk;
				parameter<ratio> KookElekZelfstandig := SpecifiekeInstellingen/Probabilisering/KookElekZelfstandig;

				attribute<bool>    KokenActief         (GeschiktObject) := Zichtjaar_jaar - BO/InstallatiePerProduct/LastVervanging/KK[GeschiktObject/BO_rel] >= Inst/Levensduur[Installatietype_org];
				attribute<float64> Probability_gasloos (GeschiktObject) := rnd_uniform(JaarSeed * 4u, GeschiktObject, range(float64, 0d, 1d));
				attribute<Inst>    Installatietype_org (GeschiktObject) := BO/InstallatiePerProduct/KK[GeschiktObject/BO_rel], Descr = "uitgangssituatie qua koken";
				attribute<bool>    Gasloos             (GeschiktObject) := Func2Meter/metervraag_per_input/Gas == 0.0[GJ_yr], Descr = "Gebruikt de woning nog gas, buiten koken om?";
				attribute<bool>    Overstap            (GeschiktObject) := not(KokenActief) ? Probability_gasloos < KookElekZelfstandig : Probability_gasloos < KookElekNatuurlijk;
				attribute<Inst>    Installatietype_def (GeschiktObject) := gasloos || overstap ? Inst/V/Elek : Installatietype_org, Descr = "nieuwe situatie qua koken";
				attribute<bool>    IsChangedNow        (GeschiktObject) := Installatietype_org != Installatietype_def;
				attribute<eur>     Ki_koken            (GeschiktObject) := float64(IsChangedNow) * BO/nrAansluitingen[GeschiktObject/BO_rel] * 
																			( Schuiven/KostenMin * Leercurves/Koken/curve * Inst/Ki_asl_min[Installatietype_def]
																			+ Schuiven/KostenMax * Leercurves/Koken/curve * Inst/Ki_asl_max[Installatietype_def]), Descr = "investeringskosten eventuele overstap";
			}
			attribute<Inst> Installatietype_rel (GeschiktObject) := Afweging/Installatietype_def;
		}
	}
	
	attribute<Eur> Ki_DK   (GeschiktObject) := OverigeInstallaties/ZonB/Afweging/Ki_zonB + OverigeInstallaties/ZonPV/Afweging/Ki_zonPV;
	attribute<Eur> Ki_KK   (GeschiktObject) := OverigeInstallaties/Koken/Afweging/Ki_koken;
	attribute<Eur> Ki_VT   (GeschiktObject) := OverigeInstallaties/Ventilatie/Afweging/Ki_vent;
	attribute<Eur> Ki_Gnet (GeschiktObject) :=  BO/nrAansluitingen[GeschiktObject/BO_rel] *
												float64(Func2Meter/metervraag_per_input/Gas == 0.0[GJ_yr] && BO/Aansluitingen/GeenGasafsluiting[GeschiktObject/BO_rel] && Zichtjaar_jaar < 2021[yr_uint16]) *
												(Bo/IsMeergezins[GeschiktObject/BO_rel] ? Invoer/Kengetallen/infra/Afsluitkosten_gas_mgw : Invoer/Kengetallen/infra/Afsluitkosten_gas_egw);
	
	container metervraag := Func2Meter/result;
	
	
	container meterprijzen
	{
		attribute<Eur_GJ> Pj_e  (GeschiktObject) := Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl [classify(metervraag/e  , Prijzen/Elektriciteit/Staffel/ClassBreak)];
		attribute<Eur_GJ> Pj_gas(GeschiktObject) := Prijzen/AardGas/Staffel/KGJ_eindgebruik_excl       [classify(metervraag/gas, Prijzen/AardGas/Staffel/ClassBreak)];
		attribute<Eur_GJ> Pm_e  (GeschiktObject) := Prijzen/Elektriciteit/Staffel/KGJ_Maatschappelijk  [classify(metervraag/e  , Prijzen/Elektriciteit/Staffel/ClassBreak)];
		attribute<Eur_GJ> Pm_gas(GeschiktObject) := Prijzen/AardGas/Staffel/KGJ_Maatschappelijk        [classify(metervraag/gas, Prijzen/AardGas/Staffel/ClassBreak)];

		parameter<Eur_GJ> Pj_biomassa := Prijzen/Overig/Pj_biomassa;
		parameter<Eur_GJ> Pj_olie     := Prijzen/Overig/Pj_olie;
		parameter<Eur_GJ> Pj_olie_EH  := Prijzen/Overig/Pj_olie_EH;
		parameter<Eur_GJ> Pj_pellets  := Prijzen/Overig/Pj_pellets;
		parameter<Eur_GJ> Pj_H2       := Prijzen/Overig/Pj_H2;

		parameter<Eur_GJ> Pm_biomassa := Pj_biomassa;
		parameter<Eur_GJ> Pm_olie     := Pj_olie;
		parameter<Eur_GJ> Pm_pellets  := Pj_pellets;
		parameter<Eur_GJ> Pm_H2       := Pj_H2;
	}

	container Kj_verbruik := for_each_nedv(
		'Kj_' + Classifications/BemeterdeGebouwInput/name,
		replace('meterprijzen/Pj_@E@ * metervraag/@E@', '@E@', Classifications/BemeterdeGebouwInput/name),
		GeschiktObject,
		Eur_yr)
		{
			attribute<Eur_yr> Totaal     (GeschiktObject) := ='add('+AsItemList('Kj_'+Classifications/BemeterdeGebouwInput/name)+', Kj_olie_eh)';
			attribute<Eur_yr> Kj_elek    (GeschiktObject) := Kj_e;
			attribute<Eur_yr> Kj_olie_eh (GeschiktObject) := meterprijzen/Pj_olie_eh * metervraag/olie;
		}
	container Km_verbruik := for_each_nedv(
		Classifications/BemeterdeGebouwInput/name,
		replace('meterprijzen/Pm_@E@ * metervraag/@E@', '@E@', Classifications/BemeterdeGebouwInput/name),
		GeschiktObject,
		Eur_yr)
		{
			attribute<Eur_yr> Totaal (GeschiktObject) := ='add('+AsItemList(Classifications/BemeterdeGebouwInput/name)+')';
		}

	attribute<Eur_yr> VastRechtBijdrageG (GeschiktObject) := BO/nrAansluitingen[GeschiktObject/BO_rel] * float64(metervraag/gas != 0[GJ_yr])* Kengetallen/Infra/jaarlijksVast_g, Descr = "VastRechtBijdrage voor aansluiting op het gasnet";
	

	attribute<Eur_yr> Kji_LTAS (GeschiktObject)                := Ki_LTAS * NcwRefs/_30/AnnualisationFactor;
	attribute<Eur_yr> Kji_Gnet (GeschiktObject)                := Ki_gnet * NcwRefs/_30/AnnualisationFactor;
	attribute<Eur_yr> Kji30_LO (GeschiktObject/xProduct)       := Ki30_LO * NcwRefs/_30/AnnualisationFactor;
	attribute<Eur_yr> Kji20_LO (GeschiktObject/xProduct)       := Ki20_LO * NcwRefs/_20/AnnualisationFactor;
	attribute<Eur_yr> Kji_DK   (GeschiktObject)                := Ki_DK   * NcwRefs/_20/AnnualisationFactor;
	attribute<Eur_yr> Kji_KK   (GeschiktObject)                := Ki_KK   * NcwRefs/_15/AnnualisationFactor;
	attribute<Eur_yr> Kji_VT   (GeschiktObject)                := Ki_VT   * NcwRefs/_15/AnnualisationFactor;
	attribute<Eur_yr> Kji15_LO (GeschiktObject/xProduct)       := Ki15_LO * NcwRefs/_15/AnnualisationFactor;
	attribute<Eur_yr> Kji_LO   (GeschiktObject/xProduct)       := Kji30_LO + Kji20_LO + Kji15_LO;

	attribute<Eur_yr> Kmi_LTAS (GeschiktObject)                := Ki_LTAS * NCW/mr30/AnnualisationFactor;
	attribute<Eur_yr> Kmi_gnet (GeschiktObject)                := Ki_Gnet * NCW/mr50/AnnualisationFactor;
	attribute<Eur_yr> Kmi30_LO (GeschiktObject/xProduct)       := Ki30_LO * NCW/mr30/AnnualisationFactor;
	attribute<Eur_yr> Kmi20_LO (GeschiktObject/xProduct)       := Ki20_LO * NCW/mr20/AnnualisationFactor;
	attribute<Eur_yr> Kmi_DK   (GeschiktObject)                := Ki_DK   * NCW/mr20/AnnualisationFactor;
	attribute<Eur_yr> Kmi_KK   (GeschiktObject)                := Ki_KK   * NCW/mr15/AnnualisationFactor;
	attribute<Eur_yr> Kmi_VT   (GeschiktObject)                := Ki_VT   * NCW/mr15/AnnualisationFactor;
	attribute<Eur_yr> Kmi15_LO (GeschiktObject/xProduct)       := Ki15_LO * NCW/mr15/AnnualisationFactor;
	attribute<Eur_yr> Kmi_LO   (GeschiktObject/xProduct)       := Kmi30_LO + Kmi20_LO + Kmi15_LO;

	unit<uint32> ZBS := ZichtjaarBeleid/subsidie, ishidden = "True";
	container SubsidieBerekening := for_each_ne(ZBS/name, 'ImplSubs(ZBS/V/'+ZBS/name+')');
	template ImplSubs : ishidden = "True"
	{
		//case parameters
		parameter<ZBS> ZBS_rel;
		//end case parameters

		parameter<ratio>   Aandeel        := makedefined(ZBS/Subsidieaandeel[ZBS_rel],0.0[ratio]) , ishidden = "True";
		parameter<eur_asl> Bedrag         := makedefined(ZBS/Subsidiebedrag[ZBS_rel],0.0[eur_asl]), ishidden = "True";
		parameter<eur_m2>  PerOpp         := makedefined(ZBS/SubsidiePerOpp[ZBS_rel],0.0[eur_m2]) , ishidden = "True";
		parameter<float64> MinMaatregelen := ZBS/Voorwaarde/MinMaatregelen[ZBS_rel], ishidden = "True";
		parameter<float64> nrLabelStappen := ZBS/Voorwaarde/nrLabelStappen[ZBS_rel], ishidden = "True";

		attribute<bool>    InDoelgroep    (GeschiktObject) := ='OR('+AsItemList(replace('ZBS/doelgroep/@ED[ZBS_rel] && BO/Eigendom_rel[geschiktobject/BO_rel] == Classifications/Eigendom/V/@ED','@ED',Classifications/Eigendom/label))+')', ishidden = "True";
		attribute<eur>     MaxBedrag      (GeschiktObject) := BO/nrAansluitingen[GeschiktObject/BO_rel] * ZBS/Voorwaarde/MaxBedrag_asl[ZBS_rel], ishidden = "True";
		attribute<float64> LabelStappenNu (GeschiktObject) := float64(Bo/Energielabel_rel[GeschiktObject/BO_rel] - energielabelberekening/Energielabel_rel);
		attribute<bool>    nrStappenOk    (GeschiktObject) := LabelStappenNu == nrLabelStappen;

		//====== Bepaling subsidiebedragen
		attribute<ratio>   Rs          (GeschiktObject/xProduct) := ='Aandeel * float64(OR('+AsItemList(replace('ZBS/Maatregel/installatie/@IN[ZBS_rel] && GeschiktObject/xProduct/Installatie_rel == Classifications/Installatie/V/@IN','@IN',Classifications/Installatie/label))+'))', ishidden = "True";
		attribute<eur>     Oi          (GeschiktObject/xProduct) := ='BO/nrAansluitingen[GeschiktObject/BO_rel[GeschiktObject/xProduct/GeschiktObject_rel]] * Bedrag * float64(OR('+AsItemList(replace('ZBS/Maatregel/installatie/@IN[ZBS_rel] && GeschiktObject/xProduct/Installatie_rel == Classifications/Installatie/V/@IN','@IN',Classifications/Installatie/label))+'))', ishidden = "True";	
		attribute<Eur>     Oi30_LO_i   (GeschiktObject/xProduct) := Ki30_LO * Rs + (Ki30_LO == max_elem(Ki30_LO,Ki20_LO,Ki15_LO) ? Oi : 0.0[eur]), ishidden = "True";
		attribute<Eur>     Oi20_LO_i   (GeschiktObject/xProduct) := Ki20_LO * Rs + (Ki20_LO == max_elem(Ki30_LO,Ki20_LO,Ki15_LO) ? Oi : 0.0[eur]), ishidden = "True";
		attribute<Eur>     Oi15_LO_i   (GeschiktObject/xProduct) := Ki15_LO * Rs + (Ki15_LO == max_elem(Ki30_LO,Ki20_LO,Ki15_LO) ? Oi : 0.0[eur]), ishidden = "True";
		attribute<Eur>     Oi_Gnet_i   (GeschiktObject)          := float64(ZBS/Maatregel/Infra/Gasafsluiting[ZBS_rel]) * (BO/nrAansluitingen[GeschiktObject/BO_rel] * Bedrag + Ki_Gnet * Aandeel), ishidden = "True";
		attribute<Eur>     Oi_GV_i     (GeschiktObject)          := float64(ZBS/Maatregel/Isolatie/Alle[ZBS_rel])       * (BO/nrAansluitingen[GeschiktObject/BO_rel] * Bedrag + Ki_gv * Aandeel) + Oi_gv_bouwdeel/totaal, ishidden = "True";
		attribute<Eur>     Oi_ZonPV_i  (GeschiktObject)          := float64(ZBS/Maatregel/Installatie/ZonPV[ZBS_rel])   * (BO/nrAansluitingen[GeschiktObject/BO_rel] * Bedrag + OverigeInstallaties/ZonPV/Afweging/Ki_zonPV  * Aandeel), ishidden = "True";
		attribute<Eur>     Oi_ZonB_i   (GeschiktObject)          := float64(ZBS/Maatregel/Installatie/ZonB[ZBS_rel])    * (BO/nrAansluitingen[GeschiktObject/BO_rel] * Bedrag + OverigeInstallaties/ZonB/Afweging/Ki_zonB    * Aandeel), ishidden = "True";

		container BouwdeelMaatregelen := for_each_nedv(
  			  Classifications/Bouwdeel/name
			, 'combine_data(Classifications/BouwdeelIsolatie'
							', Classifications/Bouwdeel/V/'+Classifications/Bouwdeel/name+''
							', GeschiktObject/Bouwdelen/Isolatie/'+Classifications/Bouwdeel/name+''
							')'
			, GeschiktObject
			, Classifications/BouwdeelIsolatie
			), ishidden = "True";

		container IsolatieMaatregel := for_each_nedv(
			  Classifications/Bouwdeel/name
		, 'rlookup(classifications/BouwdeelIsolatie/name[BouwdeelMaatregelen/'+Classifications/Bouwdeel/name+'] , Classifications/IsolatieMaatregel/name)'
			, GeschiktObject
			, Classifications/IsolatieMaatregel
			), ishidden = "True";

		container Oi_gv_bouwdeel := for_each_nedv(
				classifications/Bouwdeel/name,
				replace(
					'(makedefined(GeschiktObject/Bouwdelen/@BD'
					'*  Classifications/IsolatieMaatregel/xUitvoering/kosten['
							'combine_data(Classifications/IsolatieMaatregel/xUitvoering'
							', IsolatieMaatregel/@BD'
							', BO/Activatie/Uitvoering[GeschiktObject/BO_Rel]'
							')'
						']'
					', 0.0[Eur]) * Aandeel + Bedrag * BO/nrAansluitingen[GeschiktObject/BO_rel]) + PerOpp * GeschiktObject/Bouwdelen/@BD'+
					'* float64(OR('+AsItemList(replace('ZBS/Maatregel/Isolatie/@BM[ZBS_rel] && IsolatieMaatregel/@BD == Classifications/IsolatieMaatregel/V/@BM','@BM',Classifications/IsolatieMaatregel/name))+')) '+
					'* float64(GeschiktObject/Bouwdelen/Isolatie/@BD != BO/Bouwdelen/Isolatie/@BD[GeschiktObject/BO_rel])'
				,	'@BD', classifications/Bouwdeel/name),
				GeschiktObject, Eur), ishidden = "True"
		{
			attribute<eur> Totaal (GeschiktObject) := ='add('+AsItemList(Classifications/Bouwdeel/name)+')';
		}

		//===== check aantal maatregelen
		attribute<float64> nrMaatregelen     (GeschiktObject) := ='add('+AsItemList(replace('float64(GeschiktObject/Bouwdelen/Isolatie/@BD != BO/Bouwdelen/Isolatie/@BD[GeschiktObject/BO_rel])','@BD',Classifications/Bouwdeel/name))+')';
		attribute<bool>    genoegMaatregelen (GeschiktObject) := nrMaatregelen >= MinMaatregelen;

		//===== correctie voor maximumbedrag en indien niet genoeg maatregelen = nul
		attribute<ratio>   Korting     (GeschiktObject)          := =ZBS/Voorwaarde/MaxBedrag_asl[ZBS_rel] > 0.0[eur_asl] ? 'max_elem(((Oi_GV_i + Oi_ZonPV_i + Oi_ZonB_i + sum(Oi30_LO_i + Oi20_LO_i + Oi15_LO_i, GeschiktObject/xProduct/GeschiktObject_rel)) / MaxBedrag)[ratio],1.0[ratio])' : 'const(1.0[ratio],GeschiktObject)', ishidden = "True";
		attribute<Eur>     Oi30_LO     (GeschiktObject/xProduct) := float64(InDoelgroep[GeschiktObject/xProduct/GeschiktObject_rel] && genoegMaatregelen[GeschiktObject/xProduct/GeschiktObject_rel] && nrStappenOk[GeschiktObject/xProduct/GeschiktObject_rel]) * Oi30_LO_i / Korting[GeschiktObject/xProduct/GeschiktObject_rel];
		attribute<Eur>     Oi20_LO     (GeschiktObject/xProduct) := float64(InDoelgroep[GeschiktObject/xProduct/GeschiktObject_rel] && genoegMaatregelen[GeschiktObject/xProduct/GeschiktObject_rel] && nrStappenOk[GeschiktObject/xProduct/GeschiktObject_rel]) * Oi20_LO_i / Korting[GeschiktObject/xProduct/GeschiktObject_rel];
		attribute<Eur>     Oi15_LO     (GeschiktObject/xProduct) := float64(InDoelgroep[GeschiktObject/xProduct/GeschiktObject_rel] && genoegMaatregelen[GeschiktObject/xProduct/GeschiktObject_rel] && nrStappenOk[GeschiktObject/xProduct/GeschiktObject_rel]) * Oi15_LO_i / Korting[GeschiktObject/xProduct/GeschiktObject_rel];
		attribute<Eur>     Oi_GV       (GeschiktObject)          := float64(InDoelgroep && genoegMaatregelen && nrStappenOk) * Oi_GV_i   / Korting;
		attribute<Eur>     OiDK_LO     (GeschiktObject)          := float64(InDoelgroep && genoegMaatregelen && nrStappenOk) * (Oi_ZonB_i + Oi_ZonPV_i) / Korting;
		attribute<Eur>     Oi_Gnet     (GeschiktObject)          := float64(InDoelgroep && genoegMaatregelen && nrStappenOk) * Oi_Gnet_i / Korting;

		
	}

	//====== Optellen alle subsidies over alle beleidsinstrumenten
	attribute<Eur>    Oi30_LO  (GeschiktObject/xProduct) := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/Oi30_LO')+')' : 'const(0.0[Eur],GeschiktObject/xProduct)';
	attribute<Eur>    Oi20_LO  (GeschiktObject/xProduct) := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/Oi20_LO')+')' : 'const(0.0[Eur],GeschiktObject/xProduct)';
	attribute<Eur>    Oi15_LO  (GeschiktObject/xProduct) := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/Oi15_LO')+')' : 'const(0.0[Eur],GeschiktObject/xProduct)';
	attribute<Eur>    OiDK_LO  (GeschiktObject)          := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/OiDK_LO')+')' : 'const(0.0[Eur],GeschiktObject)';
	attribute<Eur>    Oi_GV    (GeschiktObject)          := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/Oi_GV')+')' : 'const(0.0[Eur],GeschiktObject)';
	attribute<Eur>    Oi_Gnet  (GeschiktObject)          := = count(ZBS/subsidiebedrag) > 0u ? 'add('+AsItemList('Subsidieberekening/'+ZBS/name+'/Oi_Gnet')+')' : 'const(0.0[Eur],GeschiktObject)';
	
	//====== Annualiseren subsidies tot jaarlijkse opbrengsten
	attribute<Eur_yr> Oji30_LO (GeschiktObject/xProduct) := Oi30_LO * NcwRefs/_30/AnnualisationFactor;
	attribute<Eur_yr> Oji20_LO (GeschiktObject/xProduct) := Oi20_LO * NcwRefs/_20/AnnualisationFactor;
	attribute<Eur_yr> Oji15_LO (GeschiktObject/xProduct) := Oi15_LO * NcwRefs/_15/AnnualisationFactor;
	attribute<Eur_yr> OjiDK_LO (GeschiktObject)          := OiDK_LO * NcwRefs/_20/AnnualisationFactor;
	attribute<Eur_yr> Oji_GV   (GeschiktObject)          := Oi_GV   * NcwRefs/_30/AnnualisationFactor;
	attribute<Eur_yr> Oji_Gnet (GeschiktObject)          := Oi_Gnet * NcwRefs/_30/AnnualisationFactor;

	attribute<ratio>   SplitIncentiveVerbruik  (GeschiktObject) :=
		  BO/Eigendom_rel[GeschiktObject/BO_rel] == Classifications/Eigendom/V/PartHuur ? SpecifiekeInstellingen/Beleid/SplitIncentive/SplitIncentiveVerbruik_PH
		: BO/Eigendom_rel[GeschiktObject/BO_rel] == Classifications/Eigendom/V/WoonCorp ? SpecifiekeInstellingen/Beleid/SplitIncentive/SplitIncentiveVerbruik_WC
		: 1.0[ratio];
	attribute<ratio>   SplitIncentiveVastrecht (GeschiktObject) :=
		  BO/Eigendom_rel[GeschiktObject/BO_rel] == Classifications/Eigendom/V/PartHuur ? SpecifiekeInstellingen/Beleid/SplitIncentive/SplitIncentiveVastrecht_PH
		: BO/Eigendom_rel[GeschiktObject/BO_rel] == Classifications/Eigendom/V/WoonCorp ? SpecifiekeInstellingen/Beleid/SplitIncentive/SplitIncentiveVastrecht_WC
		: 1.0[ratio];	
	
	attribute<Eur_yr> Kj_gv_incentive (GeschiktObject) := 
		  Kj_verbruik/totaal       * SplitIncentiveVerbruik
		+ jaarlijks/Kj_vastrecht_g * SplitIncentiveVastrecht;

	attribute<Eur_yr> kosten_j (GeschiktObject) :=
		  (Kji_GV - Oji_GV)      * BTW_Factor_gv
		+ sum(Kji_LO - (Oji30_LO+Oji20_LO+Oji15_LO), GeschiktObject/xProduct/GeschiktObject_rel) * BTW_Factor
		+ Kji_LTAS               * BTW_Factor
		+ (Kji_gnet - Oji_gnet)  * BTW_Factor
		+ (Kji_DK - OjiDK_LO)    * BTW_Factor
		+ Kji_KK                 * BTW_Factor
		+ Kji_VT                 * BTW_Factor
		+ sum(jaarlijks/Kj_OH_LO + jaarlijks/Kj_Adm_LO, GeschiktObject/xProduct/GeschiktObject_rel) * BTW_Factor
		+ Kj_gv_incentive        * BTW_Factor
		;

	attribute<Eur_yr> kosten_m(GeschiktObject) := 
		  Kmi_GV
		+ sum(Kmi_LO, GeschiktObject/xProduct/GeschiktObject_rel)
		+ Kmi_DK
		+ Kmi_KK
		+ Kmi_VT
		+ Kmi_Gnet
		+ Kmi_LTAS
		+ sum(jaarlijks/Kj_OH_LO + jaarlijks/Kj_Adm_LO, GeschiktObject/xProduct/GeschiktObject_rel)
		+ Km_verbruik/totaal;
		
	attribute<Eur_yr> kosten (GeschiktObject) := =(LocatieSpecifiekeOpties/GebouwOpties/KiesMaatschappelijk) ? 'kosten_m' : 'kosten_j';
	attribute<Eur_yr> kosten_or_undefined (GeschiktObject) := select_data(GeschiktObject, Criterium) ? kosten : (0/0)[Eur_yr];

	container energielabelberekening := CalculationSchemes/NTA8800(GeschiktObject);
	attribute<Classifications/Schillabel> Energielabel_rel     (GeschiktObject) := energielabelberekening/Energielabel_rel;

	unit<uint32> results := GeschiktObject 
	{
		attribute<Float64> P50P                (GeschiktObject) := float64(Classifications/Scurve_gebouwoptie/p50p[combine_data(Classifications/Scurve_gebouwoptie, BO/Eigendom_rel[GeschikteOptie/BO_rel], GeschikteOptie/GebouwOptie_rel)]);
		attribute<Eur_yr>  kosten_tbv_afweging (GeschiktObject) := kosten_or_undefined * P50P; 
		
		container BemeterdeGebouwInput_rel := Func2Meter/BemeterdeGebouwInput_rel;
		
		container eenmalig
		{
			attribute<Eur> Ki30_LO (GeschiktObject) := sum(.../Ki30_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> Ki20_LO (GeschiktObject) := sum(.../Ki20_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> Ki15_LO (GeschiktObject) := sum(.../Ki15_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> Ki_gv   (GeschiktObject) := .../Ki_gv;
			attribute<Eur> Ki_LTAS (GeschiktObject) := .../Ki_LTAS;
			attribute<Eur> Ki_gnet (GeschiktObject) := .../Ki_Gnet;
			attribute<Eur> Ki_DK   (GeschiktObject) := .../Ki_DK;
			attribute<Eur> Ki_VT   (GeschiktObject) := .../Ki_VT;
			attribute<Eur> Ki_KK   (GeschiktObject) := .../Ki_KK;
			attribute<Eur> Oi_GV   (GeschiktObject) := .../Oi_GV;
			attribute<Eur> Oi30_LO (GeschiktObject) := sum(.../Oi30_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> Oi20_LO (GeschiktObject) := sum(.../Oi20_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> Oi15_LO (GeschiktObject) := sum(.../Oi15_LO, xProduct/GeschiktObject_rel);
			attribute<Eur> OiDK_LO (GeschiktObject) := .../OiDK_LO;
		}
		container jaarlijks
		{
			attribute<Eur_yr> Kj_elek        (GeschiktObject) := .../jaarlijks/Kj_elek;
			attribute<Eur_yr> Kji_gv         (GeschiktObject) := .../Kji_gv;
			attribute<Eur_yr> Kmi_gv         (GeschiktObject) := .../Kmi_gv;
			attribute<Eur_yr> Kj_oh_LO       (GeschiktObject) := sum(.../jaarlijks/Kj_oh_LO, xProduct/GeschiktObject_rel);
			attribute<Eur_yr> Kj_adm_LO      (GeschiktObject) := sum(.../jaarlijks/Kj_adm_LO, xProduct/GeschiktObject_rel);
			attribute<Eur_yr> Kj_gas         (GeschiktObject) := .../jaarlijks/Kj_gas;
			attribute<Eur_yr> Kj_biomassa    (GeschiktObject) := .../jaarlijks/Kj_biomassa;
			attribute<Eur_yr> Kj_olie        (GeschiktObject) := .../jaarlijks/Kj_olie;
			attribute<Eur_yr> Kj_olie_eh     (GeschiktObject) := .../jaarlijks/Kj_olie_eh;
			attribute<Eur_yr> Kj_pellets     (GeschiktObject) := .../jaarlijks/Kj_pellets;
			attribute<Eur_yr> Kj_H2          (GeschiktObject) := .../jaarlijks/Kj_H2;
			attribute<Eur_yr> Kj_Vastrecht_g (GeschiktObject) := .../jaarlijks/Kj_Vastrecht_g;
		}
				
		container metervraag := Func2Meter/result;
	}	
}
