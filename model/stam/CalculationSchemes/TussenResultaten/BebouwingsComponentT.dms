//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template BebouwingsComponentT
{
	// begin case parameters
	parameter<string> BCname;
	//parameter<bool>   HasResterendFactors;
	//container         ResterendFactorMaps;
	unit<uint32>      ModelObject;
	container         LocatieSpecifiekeOpties;
	container         VraagKentallenComponent;
	// end of case parameters
	
	parameter<Classifications/BebouwingsSectorBase> BcSector :=	rlookup(BCname, Classifications/BebouwingsSectorBase/name), ishidden = "True";
	
	unit<uint32> PrevObject := ='PrevState/Bebouwing/'+ BCname, ishidden = "True";
	unit<uint16> ModelObjectKeyDomein : ishidden = "true";

	unit<uint32> BeginSituatieResults := = 'BeginSituatie/StartingStateComponenten/'+ BCname +'/Results', ishidden = "True"
	{
		attribute<bool> actief_bouwjaar := BeginSituatieResults/bouwjaar == Zichtjaar_jaar;
	}
	unit<uint32>   HistorischBouw := select_unit(BeginSituatieResults/actief_bouwjaar)
		, Descr = "bebouwing dat in het huidige historische zichtjaar is gebouwd", Source = "BAG", ishidden = "True"
	{
		attribute<Geography/rdc_meter>                       point             := select_data(HistorischBouw, BeginSituatieResults/point);
		attribute<Geography/rdc_meter>                       geometry          := select_data(HistorischBouw, BeginSituatieResults/geometry);
		attribute<string>                                    code              := select_data(HistorischBouw, BeginSituatieResults/code);
		attribute<string>                                    label             := select_data(HistorischBouw, BeginSituatieResults/label), DialogType = "labelText";
		attribute<Invoer/RuimtelijkeData/StudieGebied/buurt> Buurt_rel         := select_data(HistorischBouw, BeginSituatieResults/Buurt_rel);
		attribute<Invoer/RuimtelijkeData/BestaandeWarmtenetten/Aflevergebied_data> Aflevergebied_rel := select_data(HistorischBouw, BeginSituatieResults/Aflevergebied_rel);
		attribute<BebouwingsTypeDomein>                      BebouwingsType    := select_data(HistorischBouw, BeginSituatieResults/BebouwingsType);
		attribute<units/yr_uint16>                           Bouwjaar          := select_data(HistorischBouw, BeginSituatieResults/Bouwjaar);
		attribute<units/yr_uint16>                           Sloopjaar         := select_data(HistorischBouw, BeginSituatieResults/Sloopjaar);
		attribute<Classifications/Eigendom>                  Eigendom_rel      := select_data(HistorischBouw, BeginSituatieResults/Eigendom_rel);
		attribute<ModelObjectKeyDomein>                      ModelObjectKey    := select_data(HistorischBouw, BeginSituatieResults/ModelObjectKey);
		attribute<nrAsl>                                     nrAansluitingen_i := select_data(HistorischBouw, BeginSituatieResults/nrAansluitingen_i);
		attribute<m2>                                        Oppervlakte_i     := select_data(HistorischBouw, BeginSituatieResults/Oppervlakte_i);
		attribute<pand_asl>                                  pand_aandeel      := select_data(HistorischBouw, BeginSituatieResults/pand_aandeel);
		attribute<Invoer/RuimtelijkeData/BAG/import/pand>    pand_rel          := select_data(HistorischBouw, BeginSituatieResults/pand_rel);
		attribute<Geography/rdc_grids/m100>                  gridm100_rel      := point[Geography/rdc_grids/m100];
		attribute<Classifications/Schillabel>                Schillabel_rel    := select_data(HistorischBouw, BeginSituatieResults/Schillabel_rel);
		attribute<Classifications/Schillabel>                Defaultlabel_rel  := select_data(HistorischBouw, BeginSituatieResults/Defaultlabel_rel);
		attribute<bool>                                      Gasloos           := select_data(HistorischBouw, BeginSituatieResults/Gasloos);
		
		attribute<ModelObject>                               ModelObject_rel := rlookup(ModelObjectKey, ModelObject/ModelObjectKey);
		
		container Functioneel
		{
			container RV_N0_per_Bouwdeel := for_each_nedv(
				Classifications/bouwdeel/name,
				'select_data(HistorischBouw, BeginSituatieResults/Functioneel/RV_N0_per_Bouwdeel/'+Classifications/bouwdeel/name+')',
				..,
				GJ_yr);
			container BaseValues := for_each_nedv(
				Classifications/functionelevraag/name,
				'select_data(HistorischBouw, BeginSituatieResults/Functioneel/BaseValues/'+Classifications/functionelevraag/name+')',
				..,
				GJ_yr);
		}
		container InstallatiePerProduct := for_each_nedv(Classifications/Product/name
		,	replace('select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/@PN)'
			,	'@PN', Classifications/Product/name
			)
		,	HistorischBouw, Classifications/Installatie)
		{
			container LastVervanging := for_each_nedv(Classifications/Product/name
			,	replace('select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/LastVervanging/@PN)'
				,	'@PN', Classifications/Product/name)
			, HistorischBouw, yr_uint16)
			{
				attribute<yr_uint16> KK (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/LastVervanging/KK);
				attribute<yr_uint16> VT (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/LastVervanging/VT);
				attribute<yr_uint16> DK (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/LastVervanging/DK);
			}
			
			attribute<Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes> KK (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/KK);
			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> VT (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/VT);
			attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> DK (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/DK);
			attribute<Classifications/AfgifteSysteem>                             AS (HistorischBouw) := select_data(HistorischBouw, BeginSituatieResults/InstallatiePerProduct/AS);
		}
		
		container BemeterdeGebouwInput_rel :=  for_each_nedv(
				classifications/Product/name,
				'select_data(HistorischBouw, BeginSituatieResults/BemeterdeGebouwInput_rel/'+classifications/Product/name+')',
			.,
				Classifications/BemeterdeGebouwInput );

		container SPF := for_each_nedv(
			classifications/Product/name,
			'select_data(HistorischBouw, BeginSituatieResults/SPF/'+Classifications/Product/name+')',
			.,
			float64)
		{
			container PerformancePerProduct :=  for_each_nedv(
			classifications/Product/name,
			'select_data(HistorischBouw, BeginSituatieResults/SPF/PerformancePerProduct/'+Classifications/Product/name+')',
			..,
			Classifications/Performance);
		}
		
		container Bouwdelen := for_each_nedv(
			classifications/bouwdeel/name,
			'select_data(HistorischBouw, BeginSituatieResults/bouwdelen/'+classifications/bouwdeel/name+')',
			.,
			m2)
		{
			container Isolatie := for_each_nedv(
				classifications/bouwdeel/name,
				'select_data(HistorischBouw, BeginSituatieResults/bouwdelen/Isolatie/'+classifications/bouwdeel/name+')',
				..,
				Classifications/IsolatieNiveau);
				
			attribute<m2>    Totaal             (..) := select_data(HistorischBouw, BeginSituatieResults/Bouwdelen/Totaal);
			attribute<ratio> Norm_verliesfactor (..) := select_data(HistorischBouw, BeginSituatieResults/Bouwdelen/Norm_verliesfactor);
		}
		
		container Gebied
		{
			attribute<float64> n      (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/n);
			attribute<float64> MEAN_x (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/MEAN_x);
			attribute<float64> MEAN_y (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/MEAN_y);
			attribute<float64> SSD_xx (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/SSD_xx);
			attribute<float64> SSD_xy (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/SSD_xy);
			attribute<float64> SSD_yy (..) := select_data(HistorischBouw, BeginSituatieResults/Gebied/SSD_yy);
		}
		
		container Kapitaallasten := for_each_nedv(
			Classifications/Gebouw_Kapitaallasten/name,
			'select_data(HistorischBouw, BeginSituatieResults/Kapitaallasten/'+ Classifications/Gebouw_Kapitaallasten/name +')',
			.,
			Eur_yr);
		container jaarlijks
		{
			attribute<eur_yr> Kj_OH_LO  (..) := const(0.0[eur_yr],..);
			attribute<eur_yr> Kj_ADM_LO (..) := const(0.0[eur_yr],..);
			attribute<eur_yr> oh_KD     (..) := const(0.0[eur_yr],..);
			attribute<eur_yr> adm_KD    (..) := const(0.0[eur_yr],..);
		}
	}
	unit<uint32>      NieuwbouwBron :=
		= Classifications/Periode/isHistorisch[Periode_rel] ? 'HistorischBouw' :
		 doNieuwbouw  ? 'Invoer/RuimtelijkeData/Vastgoed/NieuwbouwObjecten/' + Classifications/Periode/name[Periode_rel] + '/BebouwingsObject'   :
		'HistorischBouw', ishidden = "True";
	
	container NCWrefs : ishidden = "True"
	{
		//====== AF = AnnualisationFactor
		attribute<_yr> AF_15 (BO) := ='merge(uint8(BO/Eigendom_rel), _yr,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w15/AnnualisationFactor') +')';
		attribute<_yr> AF_20 (BO) := ='merge(uint8(BO/Eigendom_rel), _yr,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w20/AnnualisationFactor') +')';
		attribute<_yr> AF_28 (BO) := ='merge(uint8(BO/Eigendom_rel), _yr,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w28/AnnualisationFactor') +')';
		attribute<_yr> AF_30 (BO) := ='merge(uint8(BO/Eigendom_rel), _yr,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w30/AnnualisationFactor') +')';
		attribute<_yr> AF_50 (BO) := ='merge(uint8(BO/Eigendom_rel), _yr,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w50/AnnualisationFactor') +')';

		container _28
		{
			attribute<float64> StartDiscountfactor (BO) := ='merge(uint8(BO/Eigendom_rel), float64,'+ AsItemList('NCW/'+Classifications/Eigendom/c+'w28/StartDiscountFactor') +')';
		}
	}

	unit<uint8> PrijzenElektriciteit := Prijzen/Elektriciteit/Staffel, ishidden = "true";
	unit<uint8> PrijzenAardgas       := Prijzen/AardGas/Staffel, ishidden = "true";

	unit<uint8> BebouwingsTypeDomein := BO/BebouwingsTypeDomein, ishidden = "True";

	// TODO parametriseren huidig jaar en welke jaren hebben nieuwe nieuwbouwperiode
	parameter<bool> doNieuwbouw := Zichtjaar_jaar <= 2021w || Zichtjaar_jaar == 2031w || zichtjaar_jaar == 2041w ? Invoer/DefaultInstellingen/Basis/RuimtelijkeOntwikkelingAan : false, ishidden = "True";
	container union := = doNieuwbouw ? 'CalculationSchemes/BebouwingsOperaties/union(PrevObject, NieuwbouwBron)' : '..';

	unit<uint32> BO_base := = doNieuwbouw ? 'union/result' : 'PrevObject', ishidden = "True";
	
	unit<uint32> BO := BO_base
	{
		container BebouwingsComponent_UpRef := .., ishidden = "True"; // TODO_OV: used sporadically, try to avoid this CHECK
		container NcwRefs                   := BebouwingsComponent_UpRef/NcwRefs, ishidden = "True";
		container Opbrengsten               := BebouwingsComponent_UpRef/BC_kentallen/Opbrengsten, ishidden = "True";
		container VraagKentallen            := VraagKentallenComponent, ishidden = "True";
		container BcKentallen               := BC_kentallen, ishidden = "True";

		unit<uint32>    ModelObjectDomain := ModelObject, ishidden = "True";
		attribute<Invoer/SpecifiekeInstellingen/PlanRegio> PlanRegio_rel  (BO) := BO/buurt_rel, ishidden = "True";
		attribute<ModelObject>                             Model_rel      (BO) := rlookup(BO/ModelObjectKey, ModelObject/ModelObjectKey), KeepData = "True", ishidden = "True";
		attribute<PrevObject>                              PrevObject_rel (BO) := = doNieuwbouw ? 'rlookup(BO/code, PrevObject/code)' : 'ID(PrevObject)', ishidden = "True";

		parameter<string> BCname := ../BCname, ishidden = "True";
		attribute<Classifications/Schillabel>  Schillabel_rel       (BO) := PrevObject/Schillabel_rel[PrevObject_rel];
		attribute<Classifications/Schillabel>  PrevEnergielabel_rel (BO) := PrevObject/Energielabel_rel[PrevObject_rel];
		
		container EnergielabelBerekening := CalculationSchemes/NTA8800(BO)
		{
			container OverigeInstallaties
			{
				container Ventilatie
				{
					attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> InstallatieType_rel (BO) := InstallatiePerProduct/VT;
				}
			}
			container Func2Meter := MetervraagBerekening;
		}
		attribute<Classifications/Schillabel> Energielabel_rel := EnergielabelBerekening/Energielabel_rel, IntegrityCheck = "Isdefined(Energielabel_rel)";

		attribute<bool> LabelGelijk := Energielabel_rel == PrevEnergielabel_rel || RekenstapName == 'StartJaar';

		attribute<Classifications/WarmteOptie> WarmteOptie_rel (BO) := Isdefined( Bebouwingscomponenten/woning/BO/Aflevergebied_rel ) && Bebouwingscomponenten/Woning/BO/Aansluitingen/Warmtenet ? Classifications/WarmteOptie/V/Gebied_zK : PrevObject/WarmteOptie_rel[PrevObject_rel], ishidden = "True";
		attribute<BO> Bo_rel (BO) := id(.), ishidden = "True";
		
		container Aansluitingen
		{
			container BWN := Invoer/RuimtelijkeData/BestaandeWarmtenetten, ishidden = "True";
			
			attribute<nrAsl> PrevGNet          (BO) := = 'makedefined(PrevObject/Aansluitingen/GNet[BO/PrevObject_rel], nrAansluitingen *'+ (doNieuwbouw ? 'float64(not(union/Result/Gasloos))' : 'float64(const(true,BO))') +')' , ishidden = "True";
			attribute<bool>  GeenGasafsluiting (BO) := not(WarmteNet) && (PrevGNet > 0[nrAsl]), ishidden = "True";
			attribute<nrAsl> GNet              (BO) := ='makedefined(GeenGasafsluiting ? PrevObject/Aansluitingen/GNet[Bo/PrevObject_rel] : 0[nrAsl], nrAansluitingen *'+ (doNieuwbouw ? 'float64(not(union/Result/Gasloos))' : 'float64(const(true,BO))') +')';
			attribute<bool>  WarmteNet         (BO) := MakeDefined(BWN/Aflevergebied_data/Startjaar, 0[yr_uint16])[BO/Aflevergebied_rel] <= Zichtjaar_jaar;
			attribute<bool>  KoudeNet          (BO) := WarmteNet && BWN/AfleverType/HeeftKoude[ BWN/Aflevergebied_data/Type_rel[ BO/Aflevergebied_rel ] ];
		}
		
		// Vervang Installaties voor RV en TW wanneer Gas zojuist is afgesloten.
		// Gebruik daarvoor kenmerk Classifications/Installatie/Gasvervanger
		container InstallatiePerProduct := for_each_nedv(Classifications/Product/name
		,	replace('Aansluitingen/GeenGasafsluiting'
				'? BO_base/InstallatiePerProduct/@PN '
				': Classifications/Installatie/Gasvervanger[BO_base/InstallatiePerProduct/@PN]'
			,	'@PN', Classifications/Product/name
			)
		,	BO, Classifications/Installatie)
		{
			container LastVervanging := for_each_nedv(Classifications/Product/name
			,	replace('(Aansluitingen/GeenGasafsluiting || Classifications/Installatie/GeenGasvervanger[BO_base/InstallatiePerProduct/@PN])'
					'? BO_base/InstallatiePerProduct/LastVervanging/@PN '
					': zichtjaar_jaar'
				,	'@PN', Classifications/Product/name
				)
			, BO, yr_uint16)
			{
				attribute<yr_uint16> KK (BO) := (Aansluitingen/GeenGasafsluiting || Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes/GeenGasvervanger[BO_base/InstallatiePerProduct/KK]) ? BO_base/InstallatiePerProduct/LastVervanging/KK : zichtjaar_jaar;
				attribute<yr_uint16> VT (BO) := (Aansluitingen/GeenGasafsluiting || Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/GeenGasvervanger[BO_base/InstallatiePerProduct/VT]) ? BO_base/InstallatiePerProduct/LastVervanging/VT : zichtjaar_jaar;
				attribute<yr_uint16> DK (BO) := (Aansluitingen/GeenGasafsluiting || Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/GeenGasvervanger[BO_base/InstallatiePerProduct/DK]) ? BO_base/InstallatiePerProduct/LastVervanging/DK : zichtjaar_jaar;
			}
			
			attribute<Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes> KK (BO) := Aansluitingen/GeenGasafsluiting ? BO_base/InstallatiePerProduct/KK : Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes/Gasvervanger[BO_base/InstallatiePerProduct/KK];
			attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes> VT (BO) := Aansluitingen/GeenGasafsluiting ? BO_base/InstallatiePerProduct/VT : Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/Gasvervanger[BO_base/InstallatiePerProduct/VT];
			attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes> DK (BO) := Aansluitingen/GeenGasafsluiting ? BO_base/InstallatiePerProduct/DK : Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes/Gasvervanger[BO_base/InstallatiePerProduct/DK];
			attribute<Classifications/AfgifteSysteem>                             AS (BO) := BO_base/InstallatiePerProduct/AS; // TODO: blijft na afsluiting gas; igv LT warmtenet, daar regelen?
		}
		attribute<Classifications/GebouwOptie>    GebouwOptie_rel    (BO) := PrevObject/GebouwOptie_rel[PrevObject_rel], ishidden = "True"; // REMOVE, OBSOLETE, vervangen door Schillabel_rel, InstallatiePerProduct en GebiedsOptie
		attribute<Geography/rdc_grids/m100>       gridm100_rel       (BO) := point[Geography/rdc_grids/m100], ishidden = "True";

		attribute<bool> IsMeergezins (.) := BebouwingsTypeDomein/IsMeergezins[BebouwingsType];

		attribute<ratio> ResterendFactorBase := =Classifications/Periode/isHistorisch[Periode_rel] ? 'const(1.0[ratio],.)'
			: 'MakeDefined(merge(BebouwingsType, ratio, '+AsItemList('/Invoer/RuimtelijkeData/Vastgoed/VastgoedPeriode/'+Classifications/Periode/name[Periode_rel]+'/ResterendFactor/'+Classifications/WoningType/name+'[gridm100_rel]')+'), 1.0)';
		attribute<ratio> ResterendFactor     := =Classifications/Periode/isHistorisch[Periode_rel] ? 'const(1.0[ratio],.)'
			: 'Bouwjaar > 2021w ? 1.0 : ResterendFactorBase * float64(IsDefined(PlanRegio_rel))'; //TODO: huidig jaar parametriseren
		attribute<bool>  Afbouw              := =Classifications/Periode/isHistorisch[Periode_rel] ? 'const(false,.)'
			: 'PrevObject/ResterendNu[PrevObject_rel] > 0.0 && ResterendFactor <= 0.0';
		
		attribute<ratio> Aanbouwfactor   := = Invoer/DefaultInstellingen/Basis/RuimtelijkeOntwikkelingAan ? 
			'Aanbouw ?
				min_elem(float64(Zichtjaar_jaar + 1[yr_uint16] - 
					(Bouwjaar > 2040[yr_uint16] ? 2041[yr_uint16] : 
						Bouwjaar > 2030[yr_uint16] ? 2031[yr_uint16] : 
						Bouwjaar > 2020[yr_uint16] ? 2021[yr_uint16] : Bouwjaar)) * 1[_yr] * 0.1[ratio], 1.0[ratio]) : 1.0[ratio]' :
				'const(1.0[ratio],.)';

		attribute<ratio> ResterendNu     := =Classifications/Periode/isHistorisch[Periode_rel] ?
			'isdefined(Sloopjaar) && Sloopjaar <= Zichtjaar_jaar ? 0.0 : 1.0'
			: 'isdefined(Sloopjaar) && Sloopjaar <= Zichtjaar_jaar ? 0.0 :
			Afbouw ? max_elem(PrevObject/ResterendNu[PrevObject_rel] - 0.1, 0.0) :
			Aanbouw ? Aanbouwfactor :
			ResterendFactor';
			//TODO: 0.1 dynamisch afleiden van aantal zichtjaren, werkt nu alleen bij 1-jaar stappen
						
		attribute<nrAsl> nrAansluitingen := ResterendNu * nrAansluitingen_i;
		attribute<m2>    Oppervlakte     := ResterendNu * Oppervlakte_i;
		
		attribute<ratio> StartAfbouw     := PrevObject/ResterendNu[PrevObject_rel] - ResterendNu;

		container AansluitCapaciteit
		{
			attribute<kW> RV    (..) := functioneel/RV * 0.25[kW_GJ_yr];
			attribute<kW> TW    (..) := ASW_TWmt;
			attribute<kW> Warmte(..) := RV + TW;
			attribute<kW> KD    (..) := ASW_K;
			attribute<kW> eWP   (..) := max_elem(RV, TW);
			attribute<kW> eZ    (..) := ASW_Eapp;
			attribute<kW> eM    (..) := ASW_Eewp;

			parameter<ratio> GTF_RVmt := Vermogens/GTF_RVmt[BCsector];
			parameter<ratio> GTF_RVlt := Vermogens/GTF_RVlt[BCsector];
			parameter<ratio> GTF_TW   := Vermogens/GTF_TW[BCsector];
			parameter<ratio> GTF_K    := Vermogens/GTF_K[BCsector];
			parameter<ratio> GTF_Enet := Vermogens/GTF_Enet[BCsector];

 			attribute<kW>    ASW_RVmt (..) := =
 				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
 				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_RVmt_wh_opp + nrAansluitingen * Vermogens/ASW_RVmt_wh_asl : Oppervlakte * Vermogens/ASW_RVmt_wl_opp + nrAansluitingen * Vermogens/ASW_RVmt_wl_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_RVmt_u_opp_specific[BebouwingsType]'
 				: 'const(0.0[kW],..)';
 			
 			attribute<kW>    ASW_RVlt (..) := =
 				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
 				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_RVlt_wh_opp + nrAansluitingen * Vermogens/ASW_RVlt_wh_asl : Oppervlakte * Vermogens/ASW_RVlt_wl_opp + nrAansluitingen * Vermogens/ASW_RVlt_wl_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_RVlt_u_opp_specific[BebouwingsType]'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_TWmt (..) := = 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_TWmt_wl_opp + nrAansluitingen * Vermogens/ASW_TWmt_wl_asl : Oppervlakte * Vermogens/ASW_TWmt_wh_opp + nrAansluitingen * Vermogens/ASW_TWmt_wh_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_TWmt_u_opp + nrAansluitingen * Vermogens/ASW_TWmt_u_asl'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_TWlt (..) :== 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_TWlt_wl_opp + nrAansluitingen * Vermogens/ASW_TWlt_wl_asl : Oppervlakte * Vermogens/ASW_TWlt_wh_opp + nrAansluitingen * Vermogens/ASW_TWlt_wh_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_TWlt_u_opp + nrAansluitingen * Vermogens/ASW_TWlt_u_asl'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_K    (..) := = 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_K_wl_opp + nrAansluitingen * Vermogens/ASW_K_wl_asl : Oppervlakte * Vermogens/ASW_K_wh_opp + nrAansluitingen * Vermogens/ASW_K_wh_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_K_u_opp + nrAansluitingen * Vermogens/ASW_K_u_asl'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_Eapp (..) := = 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_Eapp_wl_opp + nrAansluitingen * Vermogens/ASW_Eapp_wl_asl : Oppervlakte * Vermogens/ASW_Eapp_wh_opp + nrAansluitingen * Vermogens/ASW_Eapp_wh_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_Eapp_u_opp + nrAansluitingen * Vermogens/ASW_Eapp_u_asl'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_Ehwp (..) := = 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_Ehwp_wl_opp + nrAansluitingen * Vermogens/ASW_Ehwp_wl_asl : Oppervlakte * Vermogens/ASW_Ehwp_wh_opp + nrAansluitingen * Vermogens/ASW_Ehwp_wh_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_Ehwp_u_opp + nrAansluitingen * Vermogens/ASW_Ehwp_u_asl'
 				: 'const(0.0[kW],..)';
 			
			attribute<kW>    ASW_Eewp (..) := = 
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ?
				  	'IsMeergezins ? Oppervlakte * Vermogens/ASW_Eewp_wh_opp + nrAansluitingen * Vermogens/ASW_Eewp_wh_asl : Oppervlakte * Vermogens/ASW_Eewp_wl_opp + nrAansluitingen * Vermogens/ASW_Eewp_wl_asl'
 				: BCsector == Classifications/BebouwingsSectorBase/V/Util   ? 'Oppervlakte * Vermogens/ASW_Eewp_u_opp + nrAansluitingen * Vermogens/ASW_Eewp_u_asl'
 				: 'const(0.0[kW],..)';
		}

		container Functioneel_base := =doNieuwbouw ? 'union/result' : 'PrevObject'
		{
			container Gedragfactor := SpecifiekeInstellingen/RuimtelijkeVraag/Gedrag;
			attribute<ratio> Klimaateffect (..) := KlimaatFactorMap[gridm100_rel];

			attribute<GJ_yr> VT (..) := BaseValues/VT * Gedragfactor/VT * ResterendNu;
			attribute<GJ_yr> KK (..) := BaseValues/KK * Gedragfactor/KK * ResterendNu;
			attribute<GJ_yr> EA (..) := BaseValues/EA * Gedragfactor/EA * ResterendNu;
			attribute<GJ_yr> TW (..) := BaseValues/TW * Gedragfactor/TW * ResterendNu;
			attribute<GJ_yr> KD (..) := BaseValues/KD * Gedragfactor/KD * ResterendNu * ( 1.0 + 3.66 *(1.0 - Klimaateffect));
			
			attribute<ratio> Fitfactor_VT (..) := =(doNieuwbouw ? 'union/result' : 'PrevObject') + '/Functioneel/Fitfactor_VT';
			container BaseValues := for_each_nedv(
				Classifications/FunctioneleVraag/name,
				(doNieuwbouw ? 'union/result' : 'PrevObject') + '/Functioneel/BaseValues/' + Classifications/FunctioneleVraag/name,
				..,
				GJ_yr);
			container RV_N0_per_bouwdeel := for_each_nedv(
				Classifications/bouwdeel/name,
				(doNieuwbouw ? 'union/result' : 'PrevObject') + '/Functioneel/RV_N0_per_bouwdeel/' + Classifications/bouwdeel/name,
				..,
				GJ_yr);
				
			container RV_N0_per_bouwdeel_now := for_each_nedv(
				Classifications/bouwdeel/name,
				'RV_N0_per_bouwdeel/'+ Classifications/bouwdeel/name+ ' * Gedragfactor/RV * ResterendNu * Klimaateffect',
				..,
				GJ_yr);
		}
		container Functioneel          := CalculationSchemes/BerekenFunctioneel(BO, BO, id(.));
		container MetervraagBerekening := CalculationSchemes/FunctioneelToMetervraag(BO, false); 
		container Metervraag           := MetervraagBerekening/result, ishidden = "True";

		#include <Activatie.dms>

		container jaarlijks := BO_base/jaarlijks
		{
			attribute<eur_yr> Kj_elek        (BO) := Metervraag/e        * Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl [classify( metervraag/e , Prijzen/Elektriciteit/Staffel/ClassBreak )];
			attribute<eur_yr> Kj_gas         (BO) := Metervraag/gas      * Prijzen/AardGas/Staffel/KGJ_eindgebruik_excl [classify( metervraag/gas , Prijzen/AardGas/Staffel/ClassBreak )];
			attribute<eur_yr> Kj_biomassa    (BO) := Metervraag/biomassa * Prijzen/Overig/Pj_biomassa;
			attribute<eur_yr> Kj_olie        (BO) := Metervraag/olie     * Prijzen/Overig/Pj_olie;
			attribute<eur_yr> Kj_olie_eh     (BO) := Metervraag/olie     * Prijzen/Overig/Pj_olie_EH;
			attribute<eur_yr> Kj_pellets     (BO) := Metervraag/pellets  * Prijzen/Overig/Pj_pellets;
			attribute<eur_yr> Kj_H2          (BO) := Metervraag/H2       * Prijzen/Overig/Pj_H2;

			attribute<eur_yr> Kj_vastrecht_G (BO) := Aansluitingen/GNet > 0.0[nrAsl] ? BC_kentallen/Opbrengsten/VastRechtBijdrageG : 0.0[eur_yr];
			attribute<eur_yr> Kj_vastrecht_E (BO) := BC_kentallen/Opbrengsten/VastrechtBijdrageE;
			attribute<eur_yr> Kj_vastrecht_W (BO) := Aansluitingen/Warmtenet ? BC_kentallen/Opbrengsten/VastrechtMT_T : 0.0[eur_yr];
			attribute<eur_yr> Kj_vastrecht_K (BO) := Aansluitingen/Koudenet  ? BC_kentallen/Opbrengsten/VastrechtK_T  : 0.0[eur_yr];

			attribute<eur_yr> Km_elek        (BO) := Metervraag/e   * Prijzen/Elektriciteit/Staffel/KGJ_Maatschappelijk [classify( metervraag/e , Prijzen/Elektriciteit/Staffel/ClassBreak )];
			attribute<eur_yr> Km_gas         (BO) := Metervraag/gas * Prijzen/AardGas/Staffel/KGJ_Maatschappelijk [classify( metervraag/gas , Prijzen/AardGas/Staffel/ClassBreak )];
			attribute<eur_yr> Km_biomassa    (BO) := Kj_biomassa;
			attribute<eur_yr> Km_olie        (BO) := Kj_olie;
			attribute<eur_yr> Km_pellets     (BO) := Kj_pellets;
			attribute<eur_yr> Km_H2          (BO) := Kj_H2;
			attribute<eur_yr> Kj_oh_LO       (BO) := = doNieuwbouw ? 'union/result/jaarlijks/Kj_oh_LO' : 'PrevObject/jaarlijks/Kj_oh_LO';
			attribute<eur_yr> kj_adm_LO      (BO) := = doNieuwbouw ? 'union/result/jaarlijks/Kj_adm_LO': 'PrevObject/jaarlijks/Kj_adm_LO';
		}
	}
		
	//============== Voorwaarden voor het wel of niet toepassen van een gebouwoptie. Welke geldt is opgegeven in de runinstellingen per Rekenstap.
	container Criteria : ishidden = "True"
	{
		//"always" : Wordt altijd toegepast indien technisch mogelijk
		attribute<bool> always (BO) := const(true,  BO);

		//"never" : Wordt nooit toegepast en er worden geen berekeningen voor deze optie gedaan
		attribute<bool> never  (BO) := const(false, BO);

		//"gebied" : Wordt niet op gebouwniveau toegepast maar kan wel in werking gaan bij toekenning van een gebiedsoptie
		attribute<bool> gebied (BO) := never;

		//"Alloc" : Wordt alleen toegepast bij kandidaten die al deelnemer zijn in een gebiedsoptie
		attribute<bool> Alloc  (BO) := Classifications/WarmteOptie/isGebiedsOptie[BO/WarmteOptie_rel];

		//"NoAlloc" : Wordt alleen toegepast bij kandidaten die geen deelnemer zijn in een gebiedsoptie
		attribute<bool> NoAlloc(BO) := not(Alloc);

		//"isUtilGroot" : Wordt alleen toegepast bij kandidaten met een capaciteitsvraag voor verwarming van meer dan 100 kilowatt
		attribute<bool> isUtilGroot(BO) := BO/AansluitCapaciteit/RV >= 100 kW;

		//"isUtilKlein" : Wordt alleen toegepast bij kandidaten met een capaciteitsvraag voor verwarming van meer dan 100 kilowatt
		attribute<bool> isUtilKlein(BO) := not(isUtilGroot);
	}
	
	container BC_kentallen := kengetallen/BCkentallen/BC_kentallen(
		LeerCurves/LTAS/Curve,
		Schuiven/KostenMin,
		Schuiven/KostenMax,
		BebouwingsTypeDomein,
		BO), ishidden = "True"
	{
		container Opbrengsten
		{
			attribute<float64> AansluitTarief (BO) :=
				  (VraagKentallenComponent/Opbrengsten/AansluitTarief_min[BO/BebouwingsType] * Schuiven/OpbrMin
				+  VraagKentallenComponent/Opbrengsten/AansluitTarief_max[BO/BebouwingsType] * Schuiven/OpbrMax);
	
			parameter<Eur_yr_asl> VastRechtMT :=
				( VraagKentallenComponent/Opbrengsten/VastRechtMT_Min * Schuiven/OpbrMin 
				+ VraagKentallenComponent/Opbrengsten/VastRechtMT_Max * Schuiven/OpbrMax);
	
			parameter<Eur_yr_asl> VastRechtLT :=
				( VraagKentallenComponent/Opbrengsten/VastRechtLT_Min * Schuiven/OpbrMin 
				+ VraagKentallenComponent/Opbrengsten/VastRechtLT_Max * Schuiven/OpbrMax);
	
			parameter<Eur_yr_asl> VastRechtK  :=
				( VraagKentallenComponent/Opbrengsten/VastRechtK_Min  * Schuiven/OpbrMin 
				+ VraagKentallenComponent/Opbrengsten/VastRechtK_Max  * Schuiven/OpbrMax);
	
			attribute<Eur>    AansluitBijdrageG  (BO) := =
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ? 
					'BO/isNuNieuw ? BO/nrAansluitingen * Kengetallen/Infra/AansluitTarief_nw_g : BO/nrAansluitingen * Kengetallen/Infra/AansluitTarief_bw_g'
				: BCsector == Classifications/BebouwingsSectorBase/V/Util ?   
					'BO/isNuNieuw ? BO/oppervlakte     * Kengetallen/Infra/AansluitTarief_nu_g : BO/oppervlakte     * Kengetallen/Infra/AansluitTarief_bu_g'
				: 'const(0[Eur],BO)', Descr = "Aansluitbijdrage voor nieuwe aansluiting op het gasnet";
				
			attribute<Eur>    AansluitBijdrageE  (BO) := =
				  BCsector == Classifications/BebouwingsSectorBase/V/Woning ? 
					'BO/isNuNieuw ? BO/nrAansluitingen * Kengetallen/Infra/AansluitTarief_nw_e : BO/nrAansluitingen * Kengetallen/Infra/AansluitTarief_bw_e'
				: BCsector == Classifications/BebouwingsSectorBase/V/Util ?   
					'BO/isNuNieuw ? BO/oppervlakte     * Kengetallen/Infra/AansluitTarief_nu_e : BO/oppervlakte     * Kengetallen/Infra/AansluitTarief_bu_e'
				: 'const(0[Eur],BO)', Descr = "Aansluitbijdrage voor nieuwe aansluiting op het elektriciteitsnetnet";
				
			attribute<Eur>    AansluitBijdrageT  (BO) := BO/nrAansluitingen * AansluitTarief                 , Descr = "Aansluitbijdrage voor nieuwe aansluiting op warmtenetten";
			attribute<Eur_yr> VastRechtBijdrageG (BO) := BO/nrAansluitingen * Prijzen/AardGas/vastrecht      , Descr = "VastRechtBijdrage voor aansluiting op het gasnet";
			attribute<Eur_yr> VastRechtBijdrageE (BO) := BO/nrAansluitingen * prijzen/Elektriciteit/Vastrecht, Descr = "VastRechtBijdrage voor aansluiting op het elektriciteitsnetnet";
			attribute<Eur_yr> VastRechtMT_T      (BO) := BO/nrAansluitingen * VastRechtMT                    , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten";
			attribute<Eur_yr> VastRechtLT_T      (BO) := BO/nrAansluitingen * VastRechtLT                    , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten";
			attribute<Eur_yr> VastRechtK_T       (BO) := BO/nrAansluitingen * VastRechtK                     , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten";
	
			attribute<Eur_yr> HeffingskortingE   (BO) := BO/nrAansluitingen * Prijzen/Heffingskorting;
		}
	}
	container VerbruiksOpties : ishidden = "True"
	{		
		unit<uint32> Installatie := Classifications/Installatie
		{
			attribute<Eur_asl>   Ki_asl := =replace('(Ki_asl_min_@BC * Schuiven/KostenMin + Ki_asl_max_@BC * Schuiven/KostenMax)* merge(LeerCurve_rel, float64, '+AsItemList('const(LeerCurves/'+Classifications/LeerCurves/name+'/Curve, .)')+')', '@BC', 'w');
			attribute<Eur_kW>    Ki_cap := =replace('(Ki_cap_min_@BC * Schuiven/KostenMin + Ki_cap_max_@BC * Schuiven/KostenMax)* merge(LeerCurve_rel, float64, '+AsItemList('const(LeerCurves/'+Classifications/LeerCurves/name+'/Curve, .)')+')', '@BC', 'w');
			attribute<Eur_m2>    Ki_opp := =replace('(Ki_opp_min_@BC * Schuiven/KostenMin + Ki_opp_max_@BC * Schuiven/KostenMax)* merge(LeerCurve_rel, float64, '+AsItemList('const(LeerCurves/'+Classifications/LeerCurves/name+'/Curve, .)')+')', '@BC', 'w');

			attribute<Eur_kW>    Ki30_cap   := Ki_cap * AT30;
			attribute<Eur_kW>    Ki20_cap   := Ki_cap * AT20;
			attribute<Eur_kW>    Ki15_cap   := Ki_cap * AT15;
			attribute<Eur_yr_kW> Kj_OH_cap  := Ki_cap * R_OH;
			attribute<Eur_yr_kW> Kj_Adm_cap := Ki_cap * R_Adm;
		}
		
		unit<uint8> GebouwOptie := Classifications/GebouwOptie
		{
			unit<uint32> uInstallatie := Classifications/GebouwOptie/uInstallatie;
		}
	}
	parameter<ratio> BTW_Factor         := ='1.0 '+(Classifications/BebouwingsSectorBase/DraagtBTW[BcSector] ? '+ Invoer/Kengetallen/BTW/BTW[Zichtjaar_rel]         / 100[percent]' : ''), ishidden = "True";
	parameter<ratio> BTW_Factor_gv      := ='1.0 '+(Classifications/BebouwingsSectorBase/DraagtBTW[BcSector] ? '+ Invoer/Kengetallen/BTW/BTW_gv[Zichtjaar_rel]      / 100[percent]' : ''), ishidden = "True";
	parameter<ratio> BTW_Factor_energie := ='1.0 '+(Classifications/BebouwingsSectorBase/DraagtBTW[BcSector] ? '+ Invoer/Kengetallen/BTW/BTW_energie[Zichtjaar_rel] / 100[percent]' : ''), ishidden = "True";
	
	#include <ProductActieveWoning.dms>
	#include <BouwdeelActieveWoning.dms>
	#include <ActieveWoning.dms>
	
	unit<uint32> BebouwingsObjectMetGebouwOptie := ActieveWoning/BebouwingsObjectMetGebouwOptie, ishidden = "True"
	{
		container BC_kentallen := ../BC_kentallen;
		
		container functioneel := ActieveWoning/BebouwingsObjectMetGebouwOptie/functioneel
		{
			container BaseValues := BO/Functioneel_base/BaseValues; // TODO: check of dit weg kan
			container RV_N0_Per_Bouwdeel := BO/Functioneel_base/RV_N0_Per_Bouwdeel; // TODO: check of dit weg kan
		}		
	}
	
	unit<uint32> Results := BebouwingsObjectMetGebouwOptie, ishidden = "True"
	{
		container Opbrengsten := BC_kentallen/Opbrengsten;
		container Functioneel := BebouwingsObjectMetGebouwOptie/Functioneel;
		container Metervraag  := BebouwingsObjectMetGebouwOptie/Metervraag;
		
		container OrgKapitaallasten := =doNieuwbouw ? 'Union/result/Kapitaallasten' : 'PrevObject/Kapitaallasten';
		
		container Warmtewet
		{
			attribute<GJ_yr> V_RV     (BO) := Functioneel/RV;
			attribute<GJ_yr> V_TW     (BO) := Functioneel/TW;
			attribute<GJ_yr> V_Warmte (BO) := V_RV + V_TW;
			attribute<GJ_yr> V_gas    (BO) := Metervraag/gas;
			

			unit<uint32> GasVerbruikers := select_unit(V_Gas > 0.0[gj_yr])
			{
				attribute<GJ_yr> V_gas    := select_data(GasVerbruikers, ../V_gas);
				attribute<GJ_yr> V_warmte := select_data(GasVerbruikers, ../V_warmte);
				//TODO_OV bij V_warmte aftrek eventuele installaties die al een deel van de vraag vullen binnen deze groep (bijv Zonneboilers, future use)
			}
			parameter<float64> PrijsFactor_impl :=  sum(GasVerbruikers/V_gas) / sum(GasVerbruikers/V_warmte) * Voorkeuren/MinderDanAndersFactor;
			parameter<float64> PrijsFactor_corr :=  MakeDefined(PrijsFactor_impl, 1.25);
			parameter<float64> PrijsFactor      :=  = Classifications/BebouwingsSectorBase/name[Classifications/BebouwingsSectorBase/Warmtewet_BC_rel[BCsector]]+'/results/Warmtewet/PrijsFactor_corr';

			parameter<Eur_GJ> WarmtePrijs := = Voorkeuren/IsVasteWarmtePrijs 
				? 'Voorkeuren/VasteWarmtePrijs'
				: 'PrijsFactor * PrijzenAardgas/KGJ_eindgebruik_excl[classify(0[GJ_yr], PrijzenAardgas/ClassBreak)]';
		
			parameter<ratio> KoudeFactor := =BcSector == Classifications/BebouwingsSector/V/Woning ?
				'1.0[ratio] / SpecifiekeInstellingen/WarmtenetTarieven/KoudePrijs/Woningen'  :
				'1.0[ratio] / SpecifiekeInstellingen/WarmtenetTarieven/KoudePrijs/Utiliteit' ;
			parameter<Eur_GJ> KoudePrijs := KoudeFactor * PrijzenElektriciteit/KGJ_eindgebruik_excl[classify(0[GJ_yr], PrijzenElektriciteit/ClassBreak)];
		}
	}
}