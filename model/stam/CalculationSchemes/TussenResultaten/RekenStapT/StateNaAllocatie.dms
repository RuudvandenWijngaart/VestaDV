//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container StateNaAllocatie := GebiedsAllocatie/ResultState
{
	parameter<bool> IsBeginSituatie := false;

	container Cumulaties := for_each_ne(Classifications/GebiedsOptie/name,
		replace(IsDefined(invert(Voorkeuren/ActieveGebiedsOptie/GebiedsOptie_rel))
			? 'CalculationSchemes/AanbodOpties/GebiedsAllocatieResultaat_cumuleren(PrevState/Cumulaties/@GO@, ../@GO@)'
			: 'PrevState/Cumulaties/@GO@'
		, '@GO@'
		, Classifications/GebiedsOptie/name)
	);

	container Totaal := = #Voorkeuren/ActieveGebiedsOptie > 0
		? 'CalculationSchemes/AanbodOpties/GebiedsAllocatieResultaat_verzamelen(Cumulaties, Voorkeuren/ActieveGebiedsOptie)'
		: 'BeginSituatie/LegeGebiedsOptie';

	container nrAansluitinging := for_each_nedv(Classifications/WarmteOptie/name, 
		replace(
			'add('+
				AsItemList(
					replace('sum(Bebouwing/@BC@/nrAansluitingen * float64(Bebouwing/@BC@/WarmteOptie_rel == Classifications/WarmteOptie/V/@WO@), Bebouwing/@BC@/PlanRegio_rel)'
					, '@BC@', Classifications/BebouwingsComponent/name)
				)
			+')'
		,	'@WO@', Classifications/WarmteOptie/name
		)
	, Planregio, nrAsl
	);

	container bebouwingImpl := for_each_ne(Classifications/BebouwingsComponent/name,
			replace('StateNaAllocatieComponent(GebiedsAllocatie/ResultState/bebouwing/@BC)','@BC', Classifications/BebouwingsComponent/name)
		);

	container bebouwing := for_each_nex(Classifications/BebouwingsComponent/name, 'bebouwingImpl/'+Classifications/BebouwingsComponent/name+'/BO', uint32);

	container Infra_per_buurt
	{
		container infrakosten := Kengetallen/netbeheer/infrakosten(LeerCurves/infra/Curve, Schuiven/KostenMin, Schuiven/KostenMax);

		attribute<nrAsl> aant_g_aansl                      (PlanRegio) := AllocatieResultaten/aant_g_aansl_per_planregio;
		attribute<nrAsl> aant_g_aansl_woning               (PlanRegio) := AllocatieResultaten/aant_g_aansl_woning_per_planregio;
		attribute<nrAsl> aant_g_aansl_woning_hoogb         (PlanRegio) := AllocatieResultaten/aant_g_aansl_woning_hoogb_per_planregio;
		attribute<nrAsl> aant_g_aansl_woning_laagb         (PlanRegio) := AllocatieResultaten/aant_g_aansl_woning_laagb_per_planregio;
		attribute<nrAsl> aant_g_aansl_util                 (PlanRegio) := AllocatieResultaten/aant_g_aansl_util_per_planregio;

		attribute<nrAsl> aant_g_aansl_woning_gasloos       (PlanRegio) := PrevState/Infra_per_buurt/aant_g_aansl_woning - aant_g_aansl_woning;
		attribute<nrAsl> aant_g_aansl_woning_hoogb_gasloos (PlanRegio) := PrevState/Infra_per_buurt/aant_g_aansl_woning_hoogb - aant_g_aansl_woning_hoogb;
		attribute<nrAsl> aant_g_aansl_woning_laagb_gasloos (PlanRegio) := PrevState/Infra_per_buurt/aant_g_aansl_woning_laagb - aant_g_aansl_woning_laagb;
		attribute<nrAsl> aant_g_aansl_util_gasloos         (PlanRegio) := PrevState/Infra_per_buurt/aant_g_aansl_util - aant_g_aansl_util;
		attribute<ratio> fractie_laagb_gasloos             (PlanRegio) := makedefined(aant_g_aansl_woning_laagb_gasloos / aant_g_aansl_woning_gasloos, 0d);
		attribute<ratio> fractie_hoogb_gasloos             (PlanRegio) := makedefined(aant_g_aansl_woning_hoogb_gasloos / aant_g_aansl_woning_gasloos, 0d);

		attribute<m>     lengte_grogel_LD_net              (PlanRegio) := AllocatieResultaten/GrondroeringInPlanRegio ? 0[m] : PrevState/Infra_per_buurt/lengte_grogel_LD_net;
		attribute<m>     lengte_LD_net                     (PlanRegio) := aant_g_aansl == 0[nrAsl] ? 0[m] : PrevState/Infra_per_buurt/lengte_LD_net;
		attribute<m>     lengte_LS_net                     (PlanRegio) := PrevState/Infra_per_buurt/lengte_LS_net;

		attribute<nrAsl>           aant_won_elec           (PlanRegio) :=
			  AllocatieResultaten/BestaandeWoning/Aansluitingen/Enet_ewp
			+ AllocatieResultaten/BestaandeWoning/Aansluitingen/Enet_hwp
			+ AllocatieResultaten/BestaandeWoning/Aansluitingen/LT_wp;
		attribute<nrAsl>           aant_util_elec          (PlanRegio) :=
			  AllocatieResultaten/BestaandeUtil/Aansluitingen/Enet_ewp
			+ AllocatieResultaten/BestaandeUtil/Aansluitingen/Enet_hwp
			+ AllocatieResultaten/BestaandeUtil/Aansluitingen/LT_wp;

		attribute<ratio>          fractie_3x25_aansl       (PlanRegio) := PrevState/Infra_per_buurt/fractie_3x25_aansl;

		attribute<kW>    vermogensvraag_woningen           (PlanRegio) :=
			= 'add(const(0[kW], PlanRegio)' + AsList(replace(', AllocatieResultaten/@C/CapaciteitsvraagE', '@C', Classifications/WoningComponent/name),'')+')';

		attribute<kW>    vermogensvraag_util               (PlanRegio) :=
			= 'add(const(0[kW], PlanRegio)' + AsList(replace(', AllocatieResultaten/@C/CapaciteitsvraagE', '@C', Classifications/UtilComponent/name),'')+')';

		attribute<kW>    vermogensvraag_gltb               (PlanRegio) := 
			= Classifications/BebouwingsSector/isActive[Classifications/BebouwingsSector/V/GLTB] ? 'AllocatieResultaten/BestaandeGltb/CapaciteitsvraagE + AllocatieResultaten/NieuwbouwGltb/CapaciteitsvraagE': 'const(0[kW], PlanRegio)';

		attribute<MSR>   aant_nieuwe_MSR                   (PlanRegio) := (max_elem((vermogensvraag_woningen + vermogensvraag_util - PrevState/Infra_per_buurt/capaciteit_buurt), 0[kW]) / infrakosten/P_MSR) ;
		attribute<kW>    capaciteit_buurt                  (PlanRegio) := PrevState/Infra_per_buurt/capaciteit_buurt + (aant_nieuwe_MSR * infrakosten/P_MSR);

		attribute<m_kW>  relatieve_belasting_LS            (PlanRegio) := lengte_LS_net / (vermogensvraag_woningen + vermogensvraag_util + vermogensvraag_gltb);

		container Kosten
		{
			attribute<Eur> kosten_verv_LD_net        (PlanRegio) := infrakosten/Ki_verv_LD_len      * (AllocatieResultaten/GrondroeringInPlanRegio ? PrevState/Infra_per_buurt/lengte_grogel_LD_net : 0[m]);
			attribute<Eur> kosten_verw_g_aansl_laagb (PlanRegio) := infrakosten/Ki_verw_g_laagb_asl * aant_g_aansl_woning_laagb_gasloos;
			attribute<Eur> kosten_verw_g_aansl_hoogb (PlanRegio) := infrakosten/Ki_verw_g_hoogb_asl * aant_g_aansl_woning_hoogb_gasloos;
			attribute<Eur> kosten_verw_g_aansl_util  (PlanRegio) := infrakosten/Ki_verw_g_util_asl  * aant_g_aansl_util_gasloos;
			attribute<Eur> kosten_verw_g_aansl       (PlanRegio) := kosten_verw_g_aansl_laagb + kosten_verw_g_aansl_hoogb + kosten_verw_g_aansl_util;
			attribute<Eur> kosten_verw_LD_net        (PlanRegio) := infrakosten/Ki_verw_LD_len      * (PrevState/Infra_per_buurt/lengte_LD_net - lengte_LD_net);
			attribute<Eur> kosten_verzw_e_aansl      (PlanRegio) := infrakosten/Ki_verzw_e_asl      * aant_won_elec * (1d - fractie_3x25_aansl);
			attribute<Eur> kosten_verzw_MSR          (PlanRegio) := infrakosten/Ki_MSR * aant_nieuwe_MSR;
			attribute<Eur> kosten_verzw_LS           (PlanRegio) := infrakosten/Ki_verzw_LS_len     * relatieve_belasting_LS * max_elem((vermogensvraag_woningen - PrevState/Infra_per_buurt/capaciteit_buurt), 0[kW]);

			container cumulatief
			{
				attribute<Eur> kosten_verv_LD_net        (PlanRegio) := PrevState/Infra_per_buurt/kosten/cumulatief/kosten_verv_LD_net        + kosten/kosten_verv_LD_net;
				attribute<Eur> kosten_verw_g_aansl_laagb (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_laagb + kosten/kosten_verw_g_aansl_laagb;
				attribute<Eur> kosten_verw_g_aansl_hoogb (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_hoogb + kosten/kosten_verw_g_aansl_hoogb;
				attribute<Eur> kosten_verw_g_aansl_util  (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl_util  + kosten/kosten_verw_g_aansl_util;
				attribute<Eur> kosten_verw_g_aansl       (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verw_g_aansl       + kosten/kosten_verw_g_aansl;
				attribute<Eur> kosten_verw_LD_net        (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verw_LD_net        + kosten/kosten_verw_LD_net;
				attribute<Eur> kosten_verzw_e_aansl      (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_e_aansl      + kosten/kosten_verzw_e_aansl;
				attribute<Eur> kosten_verzw_MSR          (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_MSR          + kosten/kosten_verzw_MSR;
				attribute<Eur> kosten_verzw_LS           (PlanRegio) := PrevState/Infra_per_buurt/Kosten/cumulatief/kosten_verzw_LS           + kosten/kosten_verzw_LS;
			}
		}
	}
}