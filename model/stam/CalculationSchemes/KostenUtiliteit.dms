//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template KostenUtiliteit
{
	// begin case parameters
	unit<uint32> BO;
	unit<uint32> PlanRegio;
	// end case parameters

	parameter<kW> afkapgrens_klein := 100[kW];

	// ========== oppervlak, aansluitingen en vermogensvraag
	attribute<nrAsl>    nrAansluitingen  (BO) := ='WarmteVraag/' + BO/BCname + '/nrAansluitingen';
	attribute<kW>       P_id             (BO) := ='WarmteVraag/' + BO/BCname + '/AansluitCapaciteit';
	attribute<kW>       P_sec            (BO) := ='WarmteVraag/' + BO/BCname + '/P_sec';
	attribute<bool>     isGroot          (BO) := P_id > afkapgrens_klein;
	
	// ========== investeringskosten wijkdistributie
	attribute<pand_asl> pand_aandeel    (BO) := const(1[pand_asl], BO);
	attribute<m_pand>   L_aansl         (BO) := isGroot ? 0[m_pand] : /Invoer/RuimtelijkeData/buislengte/per_buurt/L_aansl_pand[BO/Planregio_rel];
	container LeidingKosten := BuisKosten(BO, P_id * 0.001[ MW / kW]);
	attribute<Eur>      Ki_wd_aansl     (BO) := isGroot ? 0[Eur] : L_aansl * Leidingkosten/K_m * nrAansluitingen * pand_aandeel;
	attribute<Eur>      Ki_wd_OS        (BO) := isGroot ? 0[Eur] : P_id * Kostenkentallen/K_OS;

	// ========== investeringskosten inpandige distributie
	attribute<Eur>      Ki_id_leiding   (BO) := isGroot ? 0[Eur] : Kostenkentallen/Ki_id_grondgeb * nrAansluitingen;
	attribute<Eur>      Ki_id_dist      (BO) := isGroot ? 0[Eur] : Ki_id_leiding + Kostenkentallen/Ki_afleverset * nrAansluitingen;
	attribute<Eur>      Ki_id_OS        (BO) := isGroot ? P_id * Kostenkentallen/K_OS : 0[Eur];
	attribute<Eur>      Ki_id_Wmtr      (BO) := isGroot ? Kostenkentallen/K_Wmtr_vast * nrAansluitingen + Kostenkentallen/K_Wmtr_var * P_id : 0[Eur];

	// ========== jaarlijkse kosten voor onderhoud en beheer
	attribute<Eur_yr>  Kj_wd_OS_oh      (BO) := Kengetallen/Onderhoud/R_OS_onderhoud   * Ki_wd_OS;
	attribute<Eur_yr>  Kj_wd_aansl_oh   (BO) := Kengetallen/Onderhoud/R_wd_onderhoud   * Ki_wd_aansl;
	attribute<Eur_yr>  Kj_id_OS_oh      (BO) := Kengetallen/Onderhoud/R_OS_onderhoud   * Ki_id_OS;
	attribute<Eur_yr>  Kj_id_dist_oh    (BO) := Kengetallen/Onderhoud/R_id_onderhoud   * Ki_id_dist;
	attribute<Eur_yr>  Kj_id_Wmtr_oh    (BO) := Kengetallen/Onderhoud/R_Inst_Onderhoud * Ki_id_Wmtr;

	// ========== sommering berekeningen op objectniveau naar planregioniveau
	container PR
	{
		attribute<Eur>    Ki_id     (PlanRegio) := sum(Ki_id_dist + Ki_id_OS + Ki_id_Wmtr             , BO/Planregio_rel);
		attribute<Eur>    Ki_wd     (PlanRegio) := sum(Ki_wd_OS + Ki_wd_aansl                         , BO/Planregio_rel);
		attribute<Eur_yr> Kj_id_adm (PlanRegio) := sum(Kengetallen/Onderhoud/R_Inst_Admin * Ki_id_Wmtr, BO/Planregio_rel);
		attribute<Eur_yr> Kj_id_oh  (PlanRegio) := sum(Kj_id_OS_oh + Kj_id_dist_oh + Kj_id_Wmtr_oh    , BO/Planregio_rel);
		attribute<Eur_yr> Kj_wd_oh  (PlanRegio) := sum(Kj_wd_OS_oh + Kj_wd_aansl_oh                   , BO/Planregio_rel);
		attribute<kW>     P_sec     (PlanRegio) := sum(../P_sec                                       , BO/Planregio_rel);
	}
}