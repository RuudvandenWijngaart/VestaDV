//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template GeoThermie
{
	// begin case parameters
	container BeginStatus;
	container AanbodKentallen;
	container EnergiePrijzen;
	parameter<units/yr_uint16> Jaar;
	// end case parameters

	unit<uint32> PlanRegio := Invoer/SpecifiekeInstellingen/PlanRegio;
	container TypeInfo     := Kengetallen/WarmteBronnen/GeoThermie/TypeInfo;

	container KostenBaten := CalculationSchemes/KostenBaten(BeginStatus/Bebouwing, invoer/Energieprijzen);

	parameter<kg_GJ> CO2_GJb := 0[kg_GJ];
	parameter<g_GJ>  NOx_GJb := 0[g_GJ];
	parameter<g_GJ>  SO2_GJb := 0[g_GJ];
	parameter<g_GJ>  VOS_GJb := 0[g_GJ];
	parameter<g_GJ>  TS_GJb  := 0[g_GJ];

	unit<uint32> bron := PlanRegio
	{
		unit<uint32>    ContourSet := RuimtelijkeData/WarmteBronnen/GeoThermie/ContourSet;
		unit<uint32>    PointSet   := ContourSet/PointSet;
		attribute<bool> InContour  := IsDefined(ContourSet/grid[point[Geography/rdc_grids/m100]]), FreeData = "False"; // > value(0, ContourSet);

		unit<uint32> OutsideContourSet := Subset(not(InContour))
		{
			attribute<rdc_meter> Usage_point := bron/point[nr_OrgEntity];
			attribute<PointSet>  Connections := connect(PointSet/Point, Usage_Point);
			attribute<rdc_meter> Drill_point := PointSet/Point[Connections];
			attribute<m>         Dist        := value(dist(Usage_Point, Drill_Point), m);
		}
		attribute<m> Dist := InContour ? 0[m] : OutsideContourSet/Dist[invert(OutsideContourSet/nr_OrgEntity)];
	}

	container Kosten
	{
		attribute<Eur>		Ki_ow           (PlanRegio)	:=	(TypeInfo/Ki_kW_min * Schuiven/KostenMin + TypeInfo/Ki_kW_max * Schuiven/KostenMax)
															* max_elem(KostenBaten/CapaciteitsVraagD, TypeInfo/MWth_min) * 1000[kW / MW]
															* LeerCurves/MTnet/Curve;
		attribute<Eur_yr>	Kji_ow          (PlanRegio)	:=	Ki_ow * NCW/ow28/AnnualisationFactor;
		attribute<Eur_yr>	Kmi_ow          (PlanRegio)	:=	Ki_ow * NCW/mr28/AnnualisationFactor;

		attribute<Eur>		Ki_pt           (PlanRegio)	:=	KostenBaten/KostenD/PrimairNet/LeidingKosten/aK_m * bron/dist;
		attribute<Eur_yr>	Kji_pt          (PlanRegio)	:=	Ki_pt * NCW/pt28/AnnualisationFactor;
		attribute<Eur_yr>	Kmi_pt          (PlanRegio)	:=	Ki_pt * NCW/mr28/AnnualisationFactor;
		attribute<Eur>		Ki_Totaal       (PlanRegio)	:=	Ki_ow + ki_pt;

		parameter<Eur_GJ>	K_GJ := ( Jaar < 2020[units/yr_uint16] ? TypeInfo/K_GJ10
									: Jaar < 2030[units/yr_uint16] ? TypeInfo/K_GJ20
									: Jaar < 2040[units/yr_uint16] ? TypeInfo/K_GJ30
									: Jaar < 2050[units/yr_uint16] ? TypeInfo/K_GJ40
																   : TypeInfo/K_GJ50);
		parameter<Eur_GJ>	Km_GJ := K_GJ;

		attribute<Eur_yr>	Kj_pt_adm       (PlanRegio)	:=	Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_pt_admin     * Ki_pt;
		attribute<Eur_yr>	Kj_pt_oh        (PlanRegio)	:=	Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_pt_onderhoud * Ki_pt;
		attribute<Eur_yr>	Kj_ow_adm       (PlanRegio)	:=	Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_ow_admin     * Ki_ow;
		attribute<Eur_yr>	Kj_ow_oh        (PlanRegio)	:=	Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_ow_onderhoud * Ki_ow;
		
		attribute<Eur_yr>	Kj_Totaal       (PlanRegio)	:=	Kj_pt_adm + Kj_pt_oh + Kj_ow_oh + Kj_ow_oh + Kj_WarmteWaarde;
		attribute<Eur_yr>	Km_Totaal       (PlanRegio)	:=	Kj_pt_adm + Kj_pt_oh + Kj_ow_oh + Kj_ow_oh + Km_WarmteWaarde;
		attribute<Eur_yr>	Oj_pt_EEA       (PlanRegio)	:=	SpecifiekeInstellingen/Beleid/EEA/pt * (Kj_pt_adm + Kj_pt_oh);
		attribute<Eur_yr>	Oj_ow_EEA       (PlanRegio)	:=	MakeDefined(SpecifiekeInstellingen/Beleid/EEA/ow[TypeInfo/TypeInfoList_rel], 0.0) * (Kj_ow_adm + Kj_ow_oh);
		
		attribute<Eur_yr>	Kj_WarmteWaarde (PlanRegio)	:=	K_GJ  * KostenBaten/Vj_WarmtePrimairD;
		attribute<Eur_yr>	Km_WarmteWaarde (PlanRegio)	:=	Km_GJ * KostenBaten/Vj_WarmtePrimairD;
		attribute<Eur_yr>	Oj_SDE          (PlanRegio)	:=	KostenBaten/Vj_WarmtePrimairD * SpecifiekeInstellingen/Beleid/EEA/SDE[TypeInfo/TypeInfoList_rel];
	}

	attribute<Eur   > Oi_Totaal            (PlanRegio) := KostenBaten/Oi_Aansluitbijdrage;
	attribute<Eur   > Ki_Totaal            (PlanRegio) := KostenBaten/Ki_wd_bruto + Kosten/Ki_Totaal; // eenmalige bruto maatschappelijke investeringskosten (dus zonder aftrenk van aansluitbijdrage)
	attribute<Eur   > Ki_Netto             (PlanRegio) := Ki_Totaal - NCW/ow28/StartDiscountFactor * Oi_Totaal; // : eenmalige investering minus netto aansluitbijdrage
	attribute<Eur_yr> Km_Totaal            (PlanRegio) := KostenBaten/Km_TotaalD + Kosten/Km_Totaal; // jaarlijkse bruto kosten zonder aftrek van vastrecht en verbruiksvergoeding
	attribute<Eur_yr> Oji_Netto            (PlanRegio) := KostenBaten/Oji_NettoD  - Kosten/Kj_Totaal; // Rentabiliteit: jaarlijkse opbrengst
	attribute<kg_yr>  CO2_Primair          (PlanRegio) := const(0[kg_yr], PlanRegio);
	attribute<kg_yr>  CO2                  (PlanRegio) := KostenBaten/CO2_BijstookD + CO2_Primair;
	attribute<GJ_yr>  GJe_Totaal           (PlanRegio) := const(0[GJ_yr], PlanRegio);
	attribute<KG_yr>  CO2_e                (PlanRegio) := const(0[KG_yr], PlanRegio);
	attribute<Eur_yr> Oj_Afweging          (PlanRegio) := 
		Oji_Netto + kosten/Oj_SDE + kosten/Oj_pt_EEA + kosten/Oj_ow_EEA
		- Kosten/Ki_ow * NCW/ow28/AnnualisationFactor * ((1.0 - SpecifiekeInstellingen/Beleid/EIA/ow))[TypeInfo/TypeInfoList_rel]
		- Kosten/Kji_pt * ((1.0 - SpecifiekeInstellingen/Beleid/EIA/pt));
	attribute<bool> RentabiliteitsAfweging (PlanRegio) := Oj_Afweging > 0[Eur_yr];

	attribute<bool> OldGebruik             (PlanRegio) := IsDefined(BeginStatus/PlanRegioWarmteAllocatie_REMOVE);                                  // een optie (deze of andere) was al in gebruik
	attribute<bool> GebruiktOptie          (PlanRegio) := BeginStatus/PlanRegioWarmteAllocatie_REMOVE == Classifications/GebiedsOptie/V/Geothermie; // deze optie was al in gebruik;
	attribute<bool> NewGebruik             (PlanRegio) := RentabiliteitsAfweging && bron/InContour && not(OldGebruik);
	attribute<bool> GebruikOptie           (PlanRegio) := NewGebruik || GebruiktOptie;

	attribute<Eur_yr> Kj_Netto             (PlanRegio) := Ki_Netto * NCW/ow28/AnnualisationFactor - Oji_Netto; // Rentabiliteit: jaarlijkse opbrengsten voor de energieproducent
	
	container Results := BeginStatus
	{
		container BebouwingMutatie :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'PlanRegioOptieResultaat(BeginStatus/Bebouwing/'+Classifications/BebouwingsComponent/Name+', Classifications/GebiedsOptie/V/GeoThermie, .../GebruiktOptie)'
			);
		container Bebouwing :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'BebouwingMutatie/'+Classifications/BebouwingsComponent/Name+'/result'
			);
				
		container Factoren
		{
			parameter<float64> Aardgas       := 0.0;
			parameter<float64> Biogas        := 0.0;
			parameter<float64> Biovast       := 0.0;
			parameter<float64> Ondergrond    := 1.0;
			parameter<float64> Opwekking     := 0.0;
			parameter<float64> Elektriciteit := 1.0 / 30.0;
		}

		attribute<Classifications/GebiedsOptie> PlanRegioWarmteAllocatie_REMOVE (PlanRegio) :=
			../GebruikOptie
				? Classifications/GebiedsOptie/V/GeoThermie
				: BeginStatus/PlanRegioWarmteAllocatie_REMOVE;
		container Geothermie {
		
			container VorigePeriode := BeginStatus/GeoThermie;
			container jaarlijks
			{
				container ge {
					attribute<Eur_yr> Kj_ge_hv        (PlanRegio) := (NewGebruik ? KostenBaten/Kj_ge_hv  : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kj_ge_hv;
					attribute<Eur_yr> Kji_ge_ov       (PlanRegio) := (NewGebruik ? KostenBaten/Kji_ge_ov : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kji_ge_ov;
					attribute<Eur_yr> Kji_ge_pm       (PlanRegio) := (NewGebruik ? KostenBaten/Kji_ge_pm : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kji_ge_pm;
					
					attribute<Eur_yr> Kmi_ge_ov       (PlanRegio) := (NewGebruik ? KostenBaten/Kmi_ge_ov : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kmi_ge_ov;
					attribute<Eur_yr> Kmi_ge_pm       (PlanRegio) := (NewGebruik ? KostenBaten/Kmi_ge_pm : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kmi_ge_pm;
					attribute<Eur_yr> Om_ge_comfort   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Bji_ge_Aansl    (PlanRegio) := Eenmalig/Oi_lv_Aansl * NCW/bw28/AnnualisationFactor;
					attribute<Eur_yr> Oji_ge_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_ge_EIA * NCW/bw28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Oji_ge_EIA;
					attribute<Eur_yr> Omi_ge_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_ge_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Omi_ge_EIA;
				}
				
				container id {
					attribute<Eur_yr> Kj_id_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_id_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_id_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_id_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_id_oh        (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Kj_id_oh : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kj_id_oh;
					attribute<Eur_yr> Kj_id_adm       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kji_id          (PlanRegio) := (NewGebruik ? KostenBaten/Kji_id : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kji_id;
					attribute<Eur_yr> Kmi_id          (PlanRegio) := (NewGebruik ? KostenBaten/Kmi_id : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kmi_id;
					
					attribute<Eur_yr> Oji_id_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_id_EIA * NCW/id28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Oji_id_EIA;
					attribute<Eur_yr> Omi_id_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_id_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Omi_id_EIA;
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_id_EEA       (PlanRegio) := (NewGebruik ? (Kj_id_oh + Kj_id_adm + Kj_id_gas + Kj_id_Elek) * SpecifiekeInstellingen/Beleid/EEA/wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Oj_id_EEA;
					attribute<Eur_yr> Oj_id_SDE       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				
				container wd {
					attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas			: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_gas;
					attribute<Eur_yr> Kj_wd_gas_EH    (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas_EH		: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_gas_EH;
					attribute<Eur_yr> Kj_wd_gas_CO2   (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas_CO2		: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_gas_CO2;
					attribute<Eur_yr> Km_wd_gas       (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Bijstook/Km_wd_gas			: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Km_wd_gas;
					
					attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek		: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_elek;
					attribute<Eur_yr> Kj_wd_elek_EH   (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek_eh	: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_elek_eh;
					attribute<Eur_yr> Kj_wd_elek_CO2  (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek_co2	: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_elek_co2;
					attribute<Eur_yr> Km_wd_elek      (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/SecundairNet/Km_wd_elek		: 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Km_wd_elek;
					
					attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Kj_wd_oh        : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_oh;
					attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := (NewGebruik ? KostenBaten/KostenD/Kj_wd_adm       : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_adm;
					attribute<Eur_yr> Kji_wd          (PlanRegio) := (NewGebruik ? KostenBaten/Kji_wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kji_wd;
					attribute<Eur_yr> Kmi_wd          (PlanRegio) := (NewGebruik ? KostenBaten/Kmi_wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kmi_wd;
					
					attribute<Eur_yr> Oji_wd_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_wd_EIA * NCW/wd28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Oji_wd_EIA;
					attribute<Eur_yr> Omi_wd_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_wd_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Omi_wd_EIA;
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_wd_EEA      (PlanRegio) := (NewGebruik ? (Kj_wd_oh + Kj_wd_adm + Kj_wd_gas + Kj_wd_Elek) * SpecifiekeInstellingen/Beleid/EEA/wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Oj_wd_EEA;
					attribute<Eur_yr> Oj_wd_SDE       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container pt {				
					attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := (NewGebruik ? Kosten/Kj_pt_oh        : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Kj_pt_oh;
					attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := (NewGebruik ? Kosten/Kj_pt_adm       : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Kj_pt_adm;
					
					attribute<Eur_yr> Kji_pt          (PlanRegio) := (NewGebruik ? Kosten/Kji_pt : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Kji_pt;
					attribute<Eur_yr> Kmi_pt          (PlanRegio) := (NewGebruik ? Kosten/Ki_pt * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Kmi_pt;
					
					attribute<Eur_yr> Oji_pt_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_pt_EIA * NCW/ow28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Oji_pt_EIA;
					attribute<Eur_yr> Omi_pt_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_pt_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Omi_pt_EIA;
					
					attribute<Eur_yr> Oj_pt_EEA       (PlanRegio) := (NewGebruik  ? Kosten/Oj_pt_EEA : 0[Eur_yr]) + VorigePeriode/jaarlijks/pt/Oj_pt_EEA;
				}
				container ow {
					attribute<Eur_yr> Kj_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					// TODO: gebruik /Invoer/Kengetallen/WarmteBronnen/MetaData/RekenTypes/GeoThermie/Elektriciteit_Factor en EH, en CO2 van elektriciteit en preciseer Kosten/Kj_WarmteWaarde.
					attribute<Eur_yr> Kj_ow_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_ow_productie (PlanRegio) := GebruikOptie ? Kosten/Kj_WarmteWaarde : 0[Eur_yr]; // geen opsplitsing naar EH, CO2 etc.
					
					attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := (NewGebruik ? Kosten/Kj_ow_oh        : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kj_ow_oh;
					attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := (NewGebruik ? Kosten/Kj_ow_adm       : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kj_ow_adm;
					
					attribute<Eur_yr> Kji_ow          (PlanRegio) := (NewGebruik ? Kosten/Kji_ow : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kji_ow;
					attribute<Eur_yr> Kmi_ow          (PlanRegio) := (NewGebruik ? Kosten/Ki_ow * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kmi_ow;
					
					attribute<Eur_yr> Oji_ow_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_ow_EIA * NCW/ow28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Oji_ow_EIA;
					attribute<Eur_yr> Omi_ow_EIA      (PlanRegio) := (NewGebruik ? Eenmalig/Oi_ow_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Omi_ow_EIA;
					
					attribute<Eur_yr> Oj_ow_EEA       (PlanRegio) := (NewGebruik ? Kosten/Oj_ow_EEA : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Oj_ow_EEA;						
					attribute<Eur_yr> Oj_ow_SDE       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container lv
				{
					attribute<Eur_yr> Oji_lv_Aansl    (PlanRegio) := Eenmalig/Oi_lv_Aansl * NCW/ow28/AnnualisationFactor;
					attribute<Eur_yr> Omi_lv_Aansl    (PlanRegio) := Eenmalig/Oi_lv_Aansl * NCW/mr28/AnnualisationFactor;
					
					attribute<Eur_yr> Oj_lv_verbruik  (PlanRegio) := GebruikOptie ? KostenBaten/Opbrengsten/JaarlijksD/Warmtevraag  : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_vastrecht (PlanRegio) := GebruikOptie ? KostenBaten/Opbrengsten/JaarlijksD/Oj_vastrecht : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_SDE       (PlanRegio) := (NewGebruik ? kosten/Oj_SDE    : 0[Eur_yr])+VorigePeriode/jaarlijks/lv/Oj_lv_SDE;
				}
			}

			container eenmalig
			{				
				attribute<Eur> Oi_lv_Aansl    (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/EenmaligD/Oi_Totaal : 0[Eur];
				
				attribute<Eur> Ki_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_ov : 0[Eur];
				attribute<Eur> Ki_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_pm : 0[Eur];
				attribute<Eur> Oi_ge_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Oi_ge_subsidie: 0[Eur];
				
				attribute<Eur> Ki_id          (PlanRegio) := NewGebruik ? KostenBaten/Ki_id_Bruto : 0[Eur];
				attribute<Eur> Oi_id_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Ki_id_Bruto * SpecifiekeInstellingen/Beleid/EIA/id : 0[Eur];
				
				attribute<Eur> Ki_wd          (PlanRegio) := NewGebruik ? KostenBaten/Ki_wd_Bruto : 0[Eur];
				attribute<Eur> Oi_wd_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Ki_wd_Bruto * SpecifiekeInstellingen/Beleid/EIA/wd : 0[Eur];
				
				attribute<Eur> Ki_pt          (PlanRegio) := NewGebruik ? Kosten/Ki_pt : 0[Eur];
				attribute<Eur> Oi_pt_EIA      (PlanRegio) := NewGebruik ? Kosten/Ki_pt * SpecifiekeInstellingen/Beleid/EIA/pt : 0[Eur];
				
				attribute<Eur> Ki_ow          (PlanRegio) := NewGebruik ? Kosten/Ki_ow : 0[Eur];
				attribute<Eur> Oi_ow_EIA      (PlanRegio) := NewGebruik ? Kosten/Ki_ow * SpecifiekeInstellingen/Beleid/EIA/ow[TypeInfo/TypeInfoList_rel] : 0[Eur];
			}
			
			container Verbruik
			{
				attribute<Gj_yr>	V_id_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_gas		(PlanRegio)		:=	NewGebruik ? KostenBaten/KostenD/Bijstook/Vj_Bijstook : 0[GJ_Yr];
				attribute<Gj_yr>	V_ow_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				
				attribute<Gj_yr>	V_id_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_elek		(PlanRegio)		:=	NewGebruik ? KostenBaten/KostenD/SecundairNet/V_wd_elek : 0[GJ_Yr];
				
				// TODO: gebruik /Invoer/Kengetallen/WarmteBronnen/MetaData/RekenTypes/GeoThermie/Elektriciteit_Factor en EH, en CO2 van elektriciteit
				attribute<Gj_yr>	V_ow_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen elektriciteitsverbruik bij LT bronnen TODO: controle met FO
				
				attribute<Gj_yr>	V_id_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // niet specifiek benoemd, alles in WD, TODO: specifiek maken
				attribute<Gj_yr>	V_wd_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_pt_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen primair transport bij LT netten
				
				attribute<Gj_yr>	V_warmte		(PlanRegio)		:=	NewGebruik ? KostenBaten/KostenD/Vj_Warmte : 0[GJ_yr];
				attribute<Gj_yr>	V_koude			(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen koude bij RW.
				
				// generieke netto warmte onttrekking:
				attribute<Gj_yr> V_id_warmte(PlanRegio):= V_warmte    - (V_id_gas + V_id_elek - V_id_verlies);
				attribute<Gj_yr> V_wd_warmte(PlanRegio):= V_id_warmte - (V_wd_gas + V_wd_elek - V_wd_verlies);
				attribute<Gj_yr> V_ow_warmte(PlanRegio):= V_wd_warmte - (V_ow_gas + V_ow_elek - V_pt_verlies);				
				attribute<Gj_yr> V_ow_koude (PlanRegio):= V_Koude;
			}
			
			container Uitstoot
			{
				attribute<KG_yr>	CO2_id_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_wd_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_ow_gas		(PlanRegio)		:=	KostenBaten/Vj_WarmtePrimairD * MakeDefined(CO2_GJb, 0[KG_GJ]);
				
				attribute<KG_yr>	CO2_id_elek		(PlanRegio)		:=	Verbruik/V_id_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_wd_elek		(PlanRegio)		:=	Verbruik/V_wd_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_ow_elek		(PlanRegio)		:=	Verbruik/V_ow_elek * prijzen/Elektriciteit/CO2_GJ;				
			}

		}
	}
}
