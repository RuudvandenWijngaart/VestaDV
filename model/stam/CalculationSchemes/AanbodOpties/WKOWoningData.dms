//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WKOWoningData
{
	// begin case parameters
	container BCdata;
	parameter<Eur_GJ> WarmtePrijs;
	unit<uint8> Type;
	container KostenBase;
	parameter<bool> KoudeBeschikbaar;
	
	// end case parameters

	unit<uint32>		BebouwingsObject						:=	BCdata/BebouwingsObject;
	attribute<bool>		IsAppt				(BebouwingsObject)	:=	Type/IsMeergezins[BCdata/RuimtelijkeVraagComponent/BebouwingsType];
	parameter<bool>		IsNieuwbouw								:=	BCdata/IsNieuwbouw;

	attribute<NrWoningen> nr_won(BebouwingsObject) := BCdata/WarmteVraag/deelnemers/nrAansluitingen * 1[NrWoningen / NrAansl], Descr = "Aantal woningen";
	attribute<GJ_yr> V_RV       (BebouwingsObject) := BCdata/WarmteVraag/deelnemers/Verbruik_RV,	Descr = "metervraag naar ruimteverwarming";
	attribute<GJ_yr> V_APP      (BebouwingsObject) := BCdata/Functioneel/Apparatuur,	Descr = "vraag naar elektriciteit voor apparatuur";
	attribute<GJ_yr> V_WW       (BebouwingsObject) := BCdata/WarmteVraag/deelnemers/Verbruik_WW,	Descr = "metervraag naar warm tapwater";
	attribute<GJ_yr> V_Warmte   (BebouwingsObject) := V_RV + V_WW,	Descr = "totale metervraag naar warmte";
	attribute<GJ_yr> V_Koude    (BebouwingsObject) := KoudeBeschikbaar ? BCdata/Aandeel/WarmteVraag * BCdata/Functioneel/Koude : const(0[GJ_Yr], BebouwingsObject), Descr = "metervraag naar koude indien koude geleverd wordtm";
	
	attribute<float64>    SPF_ind_rv  (BebouwingsObject) := 
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/Aplus  ? Efficiency/SPF_ind_A_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelB ? Efficiency/SPF_ind_B_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelE ? Efficiency/SPF_ind_CE_rv : 1.0, Descr = "Efficientie van de individuele warmtepomp voor eigen ruimteverwarming";
		
	attribute<float64>    AEO_ind_rv  (BebouwingsObject) := 
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/Aplus  ? Efficiency/AEO_ind_A_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelB ? Efficiency/AEO_ind_B_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelE ? Efficiency/AEO_ind_CE_rv : 1.0, Descr = "Aandeel eigen opwekking, afhankelijk van schillabel";
	
	//volumevraag gebouwzijde
	attribute<GJ_yr>	V_id_RV			(BebouwingsObject)	:= V_RV * AEO_ind_rv,						Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_id_WW			(BebouwingsObject)	:= V_WW * Efficiency/AEO_ind_ww,			Descr = "Aandeel van de eigen volumevraag naar warm tapwater dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_id_Koude		(BebouwingsObject)	:= V_Koude * Efficiency/AEO_ind_K,			Descr = "Aandeel van de eigen volumevraag naar koude dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_RV_sec		(BebouwingsObject)	:= V_RV - V_id_RV,							Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming geleverd uit het secundaire net";
	attribute<GJ_yr>	V_WW_sec		(BebouwingsObject)	:= V_WW - V_id_WW,							Descr = "Aandeel van de eigen volumevraag naar warm tapwater geleverd uit het secundaire net";
	
	//volumevraag jaarlijks aan het secundaire net (inclusief verrekening leidingverlies)
	attribute<GJ_yr>	V_Warmte_sec	(BebouwingsObject)	:= V_RV_sec + V_WW_sec,						Descr = "Aandeel van de eigen volumevraag naar warmte uit het secundaire net";
	attribute<GJ_yr>	V_Koude_sec		(BebouwingsObject)	:= V_Koude - V_id_Koude,					Descr = "Aandeel van de eigen volumevraag naar koude uit het secundaire net";
	attribute<GJ_yr>	V_Wsec_netto	(BebouwingsObject)	:= V_Warmte_sec / (1d - Kengetallen/Efficiency/Leidingverlies_LT * Leercurves/Verl/Curve),		Descr ="Warmte te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr>	V_Ksec_netto	(BebouwingsObject)	:= V_Koude_sec / (1d - Kengetallen/Efficiency/Leidingverlies_LT * Leercurves/Verl/Curve),		Descr ="Koude te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr>	V_wd_verlies	(BebouwingsObject)	:= (V_warmte_sec - V_Wsec_netto) + (V_koude_sec - V_Ksec_netto);
	
	
	//gelijktijdige capaciteitsvraag aan het secundaire net inclusief verrekening vermogensverlies)
	attribute<kW>		P_Warmte_sec	(BebouwingsObject)	:= Kengetallen/Aansluitwaarde/GTF_woning_w * (IsAppt ? Kengetallen/Aansluitwaarde/ASW_wa_lt : Kengetallen/Aansluitwaarde/ASW_wc_lt) * nr_won;
	attribute<kW>		P_Koude_sec		(BebouwingsObject)	:= KoudeBeschikbaar ? (Kengetallen/Aansluitwaarde/GTF_woning_k * (IsAppt ? Kengetallen/Aansluitwaarde/ASW_ka_LT : Kengetallen/Aansluitwaarde/ASW_kc_LT) * nr_won) : 0[kW];
	attribute<kW>		P_Wsec_netto	(BebouwingsObject)	:= P_Warmte_sec / (1d - Kengetallen/Efficiency/Vermogenverlies_LT),	Descr ="Vermogen warmte te realiseren in het secundaire net met verrekening leidingverlies op vermogen";
	attribute<kW>		P_Ksec_netto	(BebouwingsObject)	:= P_Koude_sec / (1d - Kengetallen/Efficiency/Vermogenverlies_LT),		Descr ="Vermogen koude te realiseren in het secundaire net met verrekening leidingverlies op vermogen";
	attribute<kW>		P_sec			(BebouwingsObject)	:= max_elem(P_Wsec_netto, P_Ksec_netto),	Descr = "capaciteitsvraag aan secundair net";
	
	//elektriciteitsverbruik individueel (toegekend aan inpandig distributeur)
	attribute<GJ_yr>	V_id_RV_Elek	(BebouwingsObject)	:= V_id_RV / SPF_ind_rv,					Descr = "Elektriciteitsverbruik voor eigen opwekking tbv ruimteverwarming";
	attribute<GJ_yr>	V_id_WW_Elek	(BebouwingsObject)	:= V_id_WW / Efficiency/SPF_ind_ww,			Descr = "Elektriciteitsverbruik voor eigen opwekking tbv warm tapwater";
	attribute<GJ_yr>	V_id_W_Elek		(BebouwingsObject)	:= V_id_RV_Elek + V_id_WW_Elek,				Descr = "Elektriciteitsverbruik voor eigen opwekking tbv zowel ruimteverwarming als warm tapwater";
	attribute<GJ_yr>	V_id_K_elek		(BebouwingsObject)	:= V_id_Koude / Efficiency/SPF_ind_k,		Descr = "Elektriciteitsverbruik voor eigen opwekking tbv koude";
	attribute<GJ_yr>	V_id_Elek		(BebouwingsObject)	:= V_id_W_Elek + V_id_K_elek,				Descr = "Totaal elektriciteitsverbruik door afnemer";

	//elektriciteitsverbruik collectief (toegekend aan wijkdistributeur
	attribute<GJ_yr>	V_wd_W_Elek		(BebouwingsObject)	:= V_Wsec_netto / Efficiency/SPF_coll_w,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor warmteproductie";
	attribute<GJ_yr>	V_wd_K_Elek		(BebouwingsObject)	:= V_Ksec_netto / Efficiency/SPF_coll_k,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor koudeproductie";
	attribute<GJ_yr>	V_wd_D_Elek		(BebouwingsObject)	:= (V_Wsec_netto + V_Ksec_netto) * Kengetallen/Efficiency/Pompenergie_Wnet,		Descr = "Totaal elektriciteitsverbruik voor pompenergie distributienet";
	attribute<GJ_yr>	V_wd_Elek		(BebouwingsObject)	:= V_wd_W_Elek + V_wd_K_Elek + V_wd_D_Elek,	Descr = "Totaal elektriciteitsverbruik door wijkdistributeur";

	//extra berekeningen voor WKO
	attribute<GJ_yr> V_Warmte_ow		(BebouwingsObject)	:= V_Wsec_netto - V_wd_W_Elek; // warmte geleverd aan centrale eWP door opwekker tbv warmtelevering aan woning
	attribute<GJ_yr> V_Koude_ow			(BebouwingsObject)	:= V_Ksec_netto  + V_wd_K_Elek; // warmte onttrokken aan cenrale eWP door opwekker tbv koude levering aan woning
	attribute<GJ_yr> V_WKO				(BebouwingsObject)	:= V_Warmte_ow - V_Koude_ow; // netto aan de bodem of andere leverancier onttrokken warmte.

	attribute<classifications/gebruiksgrootteklasse> ind_gebruiksgrootteklasse_rel(BebouwingsObject) := classify((V_id_Elek + V_APP), Prijzen_elec/ClassBreak);
	parameter<classifications/gebruiksgrootteklasse> ow_gebruiksgrootteklasse_rel := last(ID(Prijzen_elec));

	container Kosten
	{
		//Investeringskosten binnen gebouw
		attribute<float64>		Pand_aandeel		(BebouwingsObject)	:= IsAppt ? (Schuiven/VerketelingMax * BebouwingsObject/pand_aandeel +  Schuiven/VerketelingMin) : 1.0,	Descr = "verrekening individuele verketeling";
		attribute<Eur_woning>	Ki_id_inpandig		(BebouwingsObject)	:= IsAppt ? Kostenkentallen/Ki_inpandig * Pand_aandeel : 0 [Eur_woning],	Descr = "inpandig leidingwerk bij gestapelde bouw";
		attribute<Eur_woning>	Ki_id_afleverset	(BebouwingsObject)	:= (T_sec <= 55[Celsius]) ? 0[Eur_woning] : Kostenkentallen/Ki_afleverset * Pand_aandeel,			Descr = "inpandige afleverset, met indidivuele verketeling verrekend";
		attribute<Eur>			Ki_id_wv			(BebouwingsObject)	:= (Ki_id_inpandig + Ki_id_afleverset) * nr_won,	Descr = "inpandige afleverset en inpandig leidingwerk met individuele verketeling verrekend";
		attribute<Eur>			Ki_id_EigenOpwek	(BebouwingsObject)	:= 
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/Aplus  ) ? ((T_sec <= 55[Celsius]) ? Kostenkentallen/Ki_Booster : 0.0[eur]) :
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/LabelB ) ? ((T_sec <= 35[Celsius]) ? Kostenkentallen/Ki_eWP_B : (T_sec <= 55[Celsius]) ? Kostenkentallen/Ki_booster : 0.0[eur]) :
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/LabelE ) ? ((T_sec <= 55[Celsius]) ? Kostenkentallen/Ki_eWP_CDE : 0.0[eur]) 
			: 1000000.0[eur],																								Descr = "investeringskosten voor eigen opwekkingsinstallatie, afhankelijk van temperatuurniveau en schillabel";
		attribute<Eur>			Ki_id_LTAS			(BebouwingsObject)	:= (T_sec <= 55[Celsius]) ? (BCdata/Sprongen/VerbeterKosten_i/LTAS * nr_won) : 0 [Eur],	Descr = "investeringskosten laag-temperatuur afgiftesysteem, e.g. LT-radiatoren of vloerverwarming";			
		attribute<Eur>			Ki_id				(BebouwingsObject)	:= Ki_id_LTAS + Ki_id_wv + Ki_id_EigenOpwek,		Descr = "Investeringskosten systeem bij woningen en appartementen, F 54..56";
			
		//Investeringskosten vergoedingen gebouweigenaar
		attribute<Eur>			Ki_ge_pm			(BebouwingsObject)	:= KostenBase/Ki_ge_pm_o,							Descr = "Investeringskosten vergoeding projectmanagement aan gebouweigenaar";
		attribute<Eur>			Ki_ge_ov			(BebouwingsObject)	:= IsNieuwbouw ? 0.0[Eur] : KostenBase/Ki_ge_ov_o,	Descr = "Investeringskosten vergoeding ongerief aan gebouweigenaar";
		attribute<Eur>			Oi_ge_pm_EIA		(BebouwingsObject)	:= IsNieuwbouw ? Ki_ge_pm * SpecifiekeInstellingen/Beleid/Eia/E_nw/Rs_ProjectManagement
																			: Ki_ge_pm * SpecifiekeInstellingen/Beleid/Eia/E_bw/Rs_ProjectManagement,	Descr = "investeringssubsidie vergoeding projectmanagement aan gebouweigenaar";
		attribute<Eur>			Oi_ge_ov_EIA		(BebouwingsObject)	:= IsNieuwbouw ? Ki_ge_ov * SpecifiekeInstellingen/Beleid/Eia/E_nw/Rs_OngeriefsVergoeding
																			: Ki_ge_ov * SpecifiekeInstellingen/Beleid/Eia/E_bw/Rs_OngeriefsVergoeding,	Descr = "investeringssubsidie vergoeding ongerief aan gebouweigenaar";
		attribute<Eur>			Ki_ge				(BebouwingsObject)	:= Ki_ge_ov + Ki_ge_pm,								Descr = "Investeringskosten vergoedingen aan gebouweigenaar";
		attribute<Eur>			Oi_ge_EIA			(BebouwingsObject)	:= Oi_ge_pm_EIA + Oi_ge_ov_EIA,						Descr = "Investeringssubsidies vergoedingen aan gebouweigenaar";

		//investeringskosten wijkdistributie gebouwgerelateerd, zonder distributieleidingen
		attribute<Eur>			Ki_wd_OS			(BebouwingsObject)	:= Kostenkentallen/Ki_OnderStation [Eur_KW] * P_sec [KW], Descr = "Investeringskosten onderstations";
		attribute<m_aansl>		L_aansl				(BebouwingsObject)	:= /Invoer/RuimtelijkeData/buislengte/per_buurt/L_aansl_pand[BCdata/PlanRegio_rel];
		container				LeidingKosten							:=	BuisKostenBC(BebouwingsObject, P_sec * 0.001[ MW / kW]);
		attribute<Eur_m>		K_aansl_m			(BebouwingsObject)	:=	Leidingkosten/K_m;	
		attribute<Eur>			Ki_wd_aansluiting	(BebouwingsObject)	:=	L_aansl * K_aansl_m * BebouwingsObject/pand_aandeel * 1[nraansl], Descr = "Investeringskosten aansluitleidingen van pand tot OS";
		attribute<Eur>			Ki_wd				(BebouwingsObject)	:= Ki_wd_OS + Ki_wd_aansluiting,					Descr = "Investeringskosten wijkdistributie exclusief distributieleidingen";
		
		//Jaarlijkse kosten elektriciteitsverbruik
		attribute<Eur_yr>		Kj_id_elek			(BebouwingsObject)	:= V_id_Elek * Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten individueel eindgebruiker",	Source = "FO v7a p 82 F 57";
		attribute<Eur_yr>		Km_id_elek			(BebouwingsObject)	:= V_id_Elek * Prijzen_elec/KGJ_maatschappelijk [ind_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten individueel maatschappelijk",	Source = "FO C6 Tabel 6 WKO";
		attribute<Eur_yr>		Kj_wd_elek			(BebouwingsObject)	:= V_wd_elek * Prijzen_elec/KGJ_eindgebruik_excl[ow_gebruiksgrootteklasse_rel],		Descr = "jaarlijkse elektriciteitskosten collectief eindgebruiker",		Source = "FO v7a p 82 F 57";
		attribute<Eur_yr>		Km_wd_elek			(BebouwingsObject)	:= V_wd_elek * Prijzen_elec/KGJ_maatschappelijk [ow_gebruiksgrootteklasse_rel],		Descr = "jaarlijkse elektriciteitskosten collectief maatschappelijk",	Source = "FO C6 Tabel 6 WKO";	
		attribute<Eur_yr>		Kj_elek				(BebouwingsObject)	:= Kj_id_elek + Kj_wd_elek,						Descr = "jaarlijkse kosten elektriciteitsverbruik eindgebruiker";
		attribute<Eur_yr>		Km_elek				(BebouwingsObject)	:= Km_id_elek + Km_wd_elek,						Descr = "jaarlijkse kosten elektriciteitsverbruik maatschappelijk";
		
		//energieheffing en co2prijs
		attribute<Eur_yr>		Kj_id_elek_EH		(BebouwingsObject)	:= V_id_Elek * Prijzen_elec/KGJ_EnergieHeffing[ind_gebruiksgrootteklasse_rel],		Descr = "energieheffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>		Kj_id_elek_CO2		(BebouwingsObject)	:= V_id_Elek * Prijzen_elec/KGJ_CO2Heffing[ind_gebruiksgrootteklasse_rel],			Descr = "co2heffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>		Kj_wd_elek_EH		(BebouwingsObject)	:= V_wd_elek * Prijzen_elec/KGJ_EnergieHeffing[ow_gebruiksgrootteklasse_rel],		Descr = "energieheffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>		Kj_wd_elek_CO2		(BebouwingsObject)	:= V_wd_elek * Prijzen_elec/KGJ_CO2Heffing[ow_gebruiksgrootteklasse_rel],			Descr = "co2heffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>		Kj_elek_EH			(BebouwingsObject)	:= Kj_id_elek_EH  + Kj_wd_elek_EH,						Descr = "totaal energieheffing over elektriciteitsverbruik";
		attribute<Eur_yr>		Kj_elek_CO2			(BebouwingsObject)	:= Kj_id_elek_CO2 + Kj_wd_elek_CO2,						Descr = "totaal co2heffing over elektriciteitsverbruik";		
	}

	container Opbrengsten
	{
		attribute<Eur_GJ>		KoudePrijs			(BebouwingsObject)	:= Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel] / BCdata/Efficiency/Koude/Elektriciteit, Descr = "Prijs per GJ koude met als referentie individuele elektrische koudeproductie";

		attribute<Eur_yr>		Oj_Warmte			(BebouwingsObject)	:= V_Warmte * WarmtePrijs,							Descr = "opbrengsten voor leverancier uit warmtelevering";
		attribute<Eur_yr>		Oj_Koude			(BebouwingsObject)	:= V_Koude * KoudePrijs,							Descr = "opbrengsten voor leverancier uit koudelevering", Source = "F 63";

		attribute<Eur>			Oi_Aansl			(BebouwingsObject)	:= BCdata/Opbrengsten/AansluitBijdrageD,			Descr = "eenmalige opbrengsten uit initiele aansluitbijdrage";
		attribute<Eur_yr>		Oj_Vastrecht		(BebouwingsObject)	:= BCdata/Opbrengsten/VastrechtBijdrageD,			Descr = "jaarlijkse opbrengsten uit vastrecht", Source = "F 60+64 jo F 6";
		attribute<Eur_yr>		Om_Comfort			(BebouwingsObject)	:= Oj_Koude,										Descr = "jaarlijkse maatschappelijke opbrengsten in de vorm van comfort door koudelevering, gewaardeerd als de opbrengsten uit koude";
	}
}