//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WKOUtilData
{
	// begin case parameters
	unit<uint32> BO;
	container WarmteVraagComponent;
	parameter<bool> IsNieuwbouw;
	parameter<bool> KoudeBeschikbaar;
	container KostenBase;
	// end case parameters

	parameter<Eur_GJ> WarmtePrijs  :=BO/WarmteWet/WarmtePrijs;

	attribute<GJ_yr> Lq4bis   (BO) := KoudeBeschikbaar ? WarmteVraagComponent/V_K : const(0[GJ_yr], BO), Descr = "metervraag naar koude indien koude geleverd wordt";
	attribute<m2>    Lu       (BO) := WarmteVraagComponent/Oppervlakte;

	attribute<GJ_yr> V_RV     (BO) := WarmteVraagComponent/V_RV;
	attribute<GJ_yr> V_APP    (BO) := BO/Functioneel/EA, Descr = "vraag naar elektriciteit voor apparatuur";
	attribute<GJ_yr> V_TW     (BO) := WarmteVraagComponent/V_TW;
	attribute<GJ_yr> V_Warmte (BO) := V_RV + V_TW;
	attribute<GJ_yr> V_Koude  (BO) := Lq4bis;

	attribute<float64> SPF_ind_rv (BO) :=
		BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/A
			? Efficiency/SPF_ind_A_rv
			: BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/B
				? Efficiency/SPF_ind_B_rv
				: BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/E
					? Efficiency/SPF_ind_CE_rv
					: 1.0, Descr = "Efficientie van de individuele warmtepomp voor eigen ruimteverwarming";

	attribute<float64> AEO_ind_rv (BO) :=
		BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/A
			? Efficiency/AEO_ind_A_rv
			: BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/B
				? Efficiency/AEO_ind_B_rv
				: BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/E
					? Efficiency/AEO_ind_CE_rv
					: 1.0, Descr = "Aandeel eigen opwekking, afhankelijk van schillabel";

	attribute<float64> SPF_ind_tw (BO) := 
		(BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/A)
			? ((T_sec <= 55[Celsius])
				? Efficiency/SPF_booster_tw
				: 1.0)
			: (BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/B)
				? ((T_sec <= 35[Celsius])
					? Efficiency/SPF_ind_tw
					: (T_sec <= 55[Celsius])
						? Efficiency/SPF_booster_tw
						: 1.0)
				: (BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/E)
					? ((T_sec <= 55[Celsius])
						? Efficiency/SPF_ind_tw
						: 1.0)
					: 1.0;

	//volumevraag gebouwzijde
	attribute<GJ_yr> V_id_RV      (BO) := V_RV * AEO_ind_rv             , Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming dat in het gebouw wordt opgewekt";
	attribute<GJ_yr> V_id_TW      (BO) := V_TW * Efficiency/AEO_ind_tw  , Descr = "Aandeel van de eigen volumevraag naar warm tapwater dat in het gebouw wordt opgewekt";
	attribute<GJ_yr> V_id_Koude   (BO) := V_Koude * Efficiency/AEO_ind_K, Descr = "Aandeel van de eigen volumevraag naar koude dat in het gebouw wordt opgewekt";
	attribute<GJ_yr> V_RV_sec     (BO) := V_RV - V_id_RV                , Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming geleverd uit het secundaire net";
	attribute<GJ_yr> V_TW_sec     (BO) := V_TW - V_id_TW                , Descr = "Aandeel van de eigen volumevraag naar warm tapwater geleverd uit het secundaire net";

	//volumevraag jaarlijks aan het secundaire net (inclusief verrekening leidingverlies)
	attribute<GJ_yr> V_Warmte_sec (BO) := V_RV_sec + V_TW_sec                                                      , Descr = "Aandeel van de eigen volumevraag naar warmte uit het secundaire net";
	attribute<GJ_yr> V_Koude_sec  (BO) := V_Koude - V_id_Koude                                                     , Descr = "Aandeel van de eigen volumevraag naar koude uit het secundaire net";
	attribute<GJ_yr> V_Wsec_netto (BO) := V_Warmte_sec / (1d - Efficiencies/Leidingverlies * Leercurves/Verl/Curve), Descr = "Warmte te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr> V_Ksec_netto (BO) := V_Koude_sec  / (1d - Efficiencies/Leidingverlies * Leercurves/Verl/Curve), Descr = "Koude te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr> V_wd_verlies (BO) := (V_warmte_sec - V_Wsec_netto) + (V_koude_sec - V_Ksec_netto);

	//gelijktijdige capaciteitsvraag aan het secundaire net inclusief verrekening vermogensverlies)
	parameter<ratio> Vermogensverlies    := =(T_sec <= 55[Celsius]) ? 'Efficiencies/Vermogenverlies_LT' : 'Efficiencies/Vermogenverlies_MT';

	attribute<kW>    P_id         (BO) := KostenBase/P_id;
	attribute<kW>    P_sec_bruto  (BO) := KostenBase/P_sec;
	attribute<kW>    P_sec        (BO) := P_sec_bruto / (1d - Vermogensverlies);

	//elektriciteitsverbruik individueel (toegekend aan inpandig distributeur)
	attribute<GJ_yr> V_id_RV_Elek (BO) := V_id_RV / SPF_ind_rv              , Descr = "Elektriciteitsverbruik voor eigen opwekking tbv ruimteverwarming";
	attribute<GJ_yr> V_id_TW_Elek (BO) := V_id_TW / SPF_ind_tw              , Descr = "Elektriciteitsverbruik voor eigen opwekking tbv warm tapwater";
	attribute<GJ_yr> V_id_W_Elek  (BO) := V_id_RV_Elek + V_id_TW_Elek       , Descr = "Elektriciteitsverbruik voor eigen opwekking tbv zowel ruimteverwarming als warm tapwater";
	attribute<GJ_yr> V_id_K_elek  (BO) := V_id_Koude / Efficiency/SPF_ind_k , Descr = "Elektriciteitsverbruik voor eigen opwekking tbv koude";
	attribute<GJ_yr> V_id_Elek    (BO) := V_id_W_Elek + V_id_K_elek         , Descr = "Totaal elektriciteitsverbruik door afnemer";

	//elektriciteitsverbruik collectief (toegekend aan wijkdistributeur
	attribute<GJ_yr> V_wd_W_Elek  (BO) := V_Wsec_netto / Efficiency/SPF_coll_w                          , Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor warmteproductie";
	attribute<GJ_yr> V_wd_K_Elek  (BO) := V_Ksec_netto / Efficiency/SPF_coll_k                          , Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor koudeproductie";
	attribute<GJ_yr> V_wd_D_Elek  (BO) := (V_Wsec_netto + V_Ksec_netto) * Efficiencies/Pompenergie_Wnet , Descr = "Totaal elektriciteitsverbruik voor pompenergie distributienet";
	attribute<GJ_yr> V_wd_Elek    (BO) := V_wd_W_Elek + V_wd_K_Elek + V_wd_D_Elek                       , Descr = "Totaal elektriciteitsverbruik door wijkdistributeur";

	//extra berekeningen voor WKO
	attribute<GJ_yr> V_Warmte_ow  (BO) := V_Wsec_netto - V_wd_W_Elek; // warmte geleverd aan centrale eWP door opwekker tbv warmtelevering aan woning
	attribute<GJ_yr> V_Koude_ow   (BO) := V_Ksec_netto  + V_wd_K_Elek; // warmte onttrokken aan cenrale eWP door opwekker tbv koude levering aan woning
	attribute<GJ_yr> V_WKO        (BO) := V_Warmte_ow - V_Koude_ow; // netto aan de bodem of andere leverancier onttrokken warmte.

	attribute<classifications/gebruiksgrootteklasse> ind_gebruiksgrootteklasse_rel(BO) := classify((V_id_Elek + V_APP), Prijzen_elec/ClassBreak);
	parameter<classifications/gebruiksgrootteklasse> ow_gebruiksgrootteklasse_rel      := last(id(Prijzen_elec));

	container Kosten
	{
		//investeringskosten inpandige systemen
		attribute<Eur> Ki_id_dist (BO) := =(T_sec <= 55[Celsius]) ? 'KostenBase/Ki_id_leiding' : 'KostenBase/Ki_id_dist';
		attribute<Eur> Ki_eWP     (BO) :=
			P_sec_bruto >= 100[kw] 
				? RetabiliteitWP/BodemWP_G/Ki15_LO - 0.5 * RetabiliteitWP/BodemWP_G/Ki15_cap
				: RetabiliteitWP/BodemWP_K/Ki15_LO - 0.5 * RetabiliteitWP/BodemWP_K/Ki15_cap ; //capaciteitsgerelateerde deel wordt gehalveerd (in LT)
		attribute<Eur>  Ki_id_EigenOpwek (BO) :=
			(BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/A)
				? ((T_sec <= 55[Celsius])
					? Kostenkentallen/Ki_Booster
					: 0[Eur])
				: (BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/B)
					? ((T_sec <= 35[Celsius])
						? Ki_eWP
						: (T_sec <= 55[Celsius])
							? Kostenkentallen/Ki_booster
							: 0[Eur])
					: (BO/SchillabelOrDefault_rel <= Classifications/Schillabel/V/E )
						? ((T_sec <= 55[Celsius])
							? Ki_eWP
							: 0[Eur])
						: 1000000[Eur], Descr = "investeringskosten voor eigen opwekkingsinstallatie, afhankelijk van temperatuurniveau en schillabel";
		attribute<Eur_asl> K_LTAS_asl (BO) := const(BO/Bckentallen/Ki_LTAS_l_asl, BO);
		attribute<Eur_m2>  K_LTAS_opp (BO) := const(BO/Bckentallen/Ki_LTAS_opp, BO);
		attribute<Eur>     Ki_LTAS    (BO) := K_LTAS_opp * WarmteVraagComponent/oppervlakte + K_LTAS_asl * WarmteVraagComponent/nrAansluitingen;
		attribute<Eur>     Ki_id_LTAS (BO) := (T_sec <= 55[Celsius]) ? Ki_LTAS : 0 [Eur]  ,Descr = "investeringskosten laag-temperatuur afgiftesysteem, e.g. LT-radiatoren of vloerverwarming";
		attribute<Eur>     Ki_id_OS   (BO) := KostenBase/Ki_id_OS + KostenBase/Ki_id_Wmtr , Descr = "Investeringskosten onderstations";
		attribute<Eur>     Ki_id      (BO) := Ki_id_dist + Ki_id_EigenOpwek + Ki_id_LTAS + Ki_id_OS;

		//geen gebouweigenaar-kosten bij utiliteit
		attribute<Eur>     Ki_ge_pm   (BO) := const(0[Eur], BO), Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>     Ki_ge_ov   (BO) := const(0[Eur], BO), Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>     Oi_ge_EIA  (BO) := const(0[Eur], BO), Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>     Ki_ge      (BO) := const(0[Eur], BO), Descr = "geen gebouweigenaar-kosten bij utiliteit";

		//investeringskosten wijkdistributie gebouwgerelateerd, zonder distributieleidingen
		attribute<Eur>     Ki_wd_pandOS (BO) := const(0[Eur],BO), Descr = "Extra WD tussen pand-straat aansluiting en onderstations";
		attribute<Eur>     Ki_wd        (BO) := KostenBase/Ki_wd_aansl + KostenBase/Ki_wd_OS;

		//jaarlijkse kosten elektriciteitsverbruik
		attribute<Eur_yr> Kj_id_elek (BO) := V_id_Elek * Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel], Descr = "jaarlijkse elektriciteitskosten individueel eindgebruiker"  , Source = "FO v7a p 82 F 57";
		attribute<Eur_yr> Km_id_elek (BO) := V_id_Elek * Prijzen_elec/KGJ_Maatschappelijk [ind_gebruiksgrootteklasse_rel], Descr = "jaarlijkse elektriciteitskosten individueel Maatschappelijk", Source = "FO C6 Tabel 6 WKO";
		attribute<Eur_yr> Kj_wd_elek (BO) := V_wd_elek * Prijzen_elec/KGJ_eindgebruik_excl[ow_gebruiksgrootteklasse_rel] , Descr = "jaarlijkse elektriciteitskosten collectief eindgebruiker"   , Source = "FO v7a p 82 F 57";
		attribute<Eur_yr> Km_wd_elek (BO) := V_wd_elek * Prijzen_elec/KGJ_Maatschappelijk [ow_gebruiksgrootteklasse_rel] , Descr = "jaarlijkse elektriciteitskosten collectief Maatschappelijk" , Source = "FO C6 Tabel 6 WKO";	
		attribute<Eur_yr> Kj_elek    (BO) := Kj_id_elek + Kj_wd_elek, Descr = "jaarlijkse kosten elektriciteitsverbruik eindgebruiker";
		attribute<Eur_yr> Km_elek    (BO) := Km_id_elek + Km_wd_elek, Descr = "jaarlijkse kosten elektriciteitsverbruik Maatschappelijk";

		//energieheffing en co2prijs
		attribute<Eur_yr> Kj_id_elek_EH  (BO) := V_id_Elek * Prijzen_elec/KGJ_EnergieHeffing[ind_gebruiksgrootteklasse_rel] , Descr = "energieheffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr> Kj_id_elek_CO2 (BO) := V_id_Elek * Prijzen_elec/KGJ_CO2Heffing[ind_gebruiksgrootteklasse_rel]     , Descr = "co2heffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr> Kj_wd_elek_EH  (BO) := V_wd_elek * Prijzen_elec/KGJ_EnergieHeffing[ow_gebruiksgrootteklasse_rel]  , Descr = "energieheffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr> Kj_wd_elek_CO2 (BO) := V_wd_elek * Prijzen_elec/KGJ_CO2Heffing[ow_gebruiksgrootteklasse_rel]      , Descr = "co2heffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr> Kj_elek_EH     (BO) := Kj_id_elek_EH  + Kj_wd_elek_EH                                             , Descr = "totaal energieheffing over elektriciteitsverbruik";
		attribute<Eur_yr> Kj_elek_CO2	 (BO) := Kj_id_elek_CO2 + Kj_wd_elek_CO2                                            , Descr = "totaal co2heffing over elektriciteitsverbruik";
	}

	container Opbrengsten
	{
		attribute<Eur_GJ> KoudePrijs     (BO) := Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel] / SpecifiekeInstellingen/NMDA_Prijzen/KoudePrijs/Utiliteit, Descr = "Prijs per GJ koude met als referentie individuele elektrische koudeproductie";

		attribute<Eur_yr> Oj_Warmte      (BO) := V_Warmte * WarmtePrijs                                             , Descr = "opbrengsten voor leverancier uit warmtelevering";
		attribute<Eur_yr> Oj_Koude       (BO) := KoudePrijs * Lq4bis                                                , Descr = "opbrengsten voor leverancier uit koudelevering", Source = "F 71";
		attribute<Eur>    Oi_Aansl       (BO) := WarmteVraagComponent/Oi_AansluitBijdrage                           , Descr = "eenmalige opbrengsten uit initiele aansluitbijdrage", Source = "F 65 jo F 10";
		attribute<Eur_yr> Oj_VastrechtMT (BO) := float64(T_sec >  55[Celsius]) * WarmteVraagComponent/Oj_VastrechtMT, Descr = "jaarlijkse opbrengsten uit vastrecht voor temperaturen 70 of hoger";
		attribute<Eur_yr> Oj_VastrechtLT (BO) := float64(T_sec <= 55[Celsius]) * WarmteVraagComponent/Oj_VastrechtLT, Descr = "jaarlijkse opbrengsten uit vastrecht voor temperaturen onder 70";
		attribute<Eur_yr> Oj_VastrechtK  (BO) := float64(KoudeBeschikbaar)     * WarmteVraagComponent/Oj_VastrechtK , Descr = "jaarlijkse opbrengsten uit vastrecht voor koudeaansluiting";
		attribute<Eur_yr> Oj_Vastrecht   (BO) := Oj_VastrechtMT + Oj_VastrechtLT + Oj_VastrechtK;
		attribute<Eur_yr> Om_Comfort     (BO) := Oj_Koude                                                           , Descr = "jaarlijkse Maatschappelijke opbrengsten in de vorm van comfort door koudelevering, gewaardeerd als de opbrengsten uit koude";
	}

	container RetabiliteitWP
	{
		attribute<kw>      cap_RV (BO) := BO/AansluitCapaciteit/RV;
		attribute<float64> S1AofB (BO) := Invoer/RuimtelijkeData/leidraad_oktober_2019/buurt/S1_variant_keuze_code[BO/Planregio_rel];

		parameter<string> criterium := 'Criteria/Always';

		container VerbruiksOpties := BO/BebouwingsComponent_UpRef/VerbruiksOpties; // to be used in GebouwOptieT
		container LuchtWP         := CalculationSchemes/TussenResultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/LeWPu, criterium);
		container BodemWP_G       := CalculationSchemes/TussenResultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/BeWPuG, criterium);
		container BodemWP_K       := CalculationSchemes/TussenResultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/BeWPuK, criterium);
		container HRketel         := CalculationSchemes/TussenResultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/HRu, criterium);

		attribute<Eur_yr> Kj_totaal_HR (BO) := HRketel/kosten_m;
		attribute<Eur_yr> Kj_totaal_WP (BO) := S1AofB == 0d? LuchtWP/kosten_m : cap_RV > 100[kw] ? BodemWP_G/kosten_m :BodemWP_K/kosten_m;
		attribute<Eur_yr> Delta_totaal (BO) := =Voorkeuren/Afweging/LTversusWP ? 'Kj_totaal_WP - Kj_totaal_HR' : 'const(0[Eur_yr], BO)';
	}
}