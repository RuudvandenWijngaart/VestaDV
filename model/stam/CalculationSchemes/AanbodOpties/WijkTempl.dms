//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WijkTempl
{
	// begin case parameters
	container BeginStatus;
	container AanbodKentallen;
	container EnergiePrijzen;
	parameter<units/yr_uint16> Jaar;
	container TypeInfo;
	parameter<Classifications/GebiedsOptie> Optie;
	parameter<string> StartJaarAttr;
	parameter<PJ_yr> MaxTotalDemand;
	// end case parameters

	unit<uint32> PlanRegio := SpecifiekeInstellingen/PlanRegio
	{
		attribute<units/yr_uint16> StartJaar := =StartJaarAttr != '' ? StartJaarAttr : 'const(2000[units/yr_uint16], .)';
	}

	container KostenBaten := CalculationSchemes/KostenBatenT(BeginStatus/Bebouwing);
	
	container Kosten
	{
		parameter<Eur_GJ> K_GJ    := ='TypeInfo/K_GJ/'+RekenstapName; 
		parameter<Eur_GJ> Km_GJ   := ='TypeInfo/Km_GJ/'+RekenstapName;
		parameter<kg_GJ>  CO2g_GJ := TypeInfo/RekenType/Factoren/Aardgas       * Prijzen/AardGas/CO2_GJ      + TypeInfo/RekenType/Factoren/BioGas * Prijzen/AardGas/CO2_GJ;
		parameter<kg_GJ>  CO2e_GJ := TypeInfo/RekenType/Factoren/Elektriciteit * Prijzen/Elektriciteit/CO2_GJ;
		parameter<kg_GJ>  CO2_GJ  := CO2g_GJ + CO2e_GJ, Source = "FO v7a C.5 WijkWKK";
		parameter<_yr>    R_OenM  := 0.5 * TypeInfo/Rj_Onderhoud;

		attribute<Eur>    Ki_ow_nu (PlanRegio) := 
			 (TypeInfo/Ki_kW_min * Schuiven/KostenMin + TypeInfo/Ki_kW_max * Schuiven/KostenMax)
			* max_elem(TypeInfo/P_cap * KostenBaten/CapaciteitsVraagD, TypeInfo/MWth_min) 
			* 1000[kW / MW];

		attribute<Eur>    Ki_ow              (PlanRegio) := Ki_ow_nu * LeerCurves/MTnet/curve;
		attribute<Eur_yr> Kji_ow             (PlanRegio) := Ki_ow * NCW/ow28/AnnualisationFactor;
		attribute<Eur>    Oi_ow_EIA          (PlanRegio) := Ki_ow * SpecifiekeInstellingen/Beleid/EIA/ow[TypeInfo/TypeInfoList_rel];
		attribute<Eur_yr> Oji_ow_EIA         (PlanRegio) := Oi_ow_EIA * NCW/ow28/AnnualisationFactor;
		attribute<Eur_yr> Kj_wd_gas          (PlanRegio) := (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/Kj_wd_gas;
		attribute<Eur_yr> Kj_ow_adm          (PlanRegio) := Leercurves/OenM/Curve * R_OenM * Ki_ow;
		attribute<Eur_yr> Kj_ow_oh           (PlanRegio) := Leercurves/OenM/Curve * R_OenM * Ki_ow;
		attribute<Eur_yr> Kj_ow_WarmteWaarde (PlanRegio) := TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * K_GJ;
		attribute<Eur_yr> Km_ow_WarmteWaarde (PlanRegio) := TypeInfo/P_vol * TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * Km_GJ;
		attribute<Eur_yr> Kj_ow              (PlanRegio) := Kj_ow_adm + Kj_ow_oh + Kj_ow_WarmteWaarde;
		attribute<Eur_yr> Km_ow              (PlanRegio) := Kj_ow_adm + Kj_ow_oh + Km_ow_WarmteWaarde;
		attribute<Eur_yr> Oj_lv_SDE          (PlanRegio) := TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * SpecifiekeInstellingen/Beleid/SDE/SDE[TypeInfo/TypeInfoList_rel];
	}

	attribute<Eur>    Oi_Totaal   (PlanRegio) := KostenBaten/Oi_Aansluitbijdrage ;
	attribute<Eur>    Ki_Totaal   (PlanRegio) := KostenBaten/Ki_wd_bruto + Kosten/Ki_ow;
	attribute<Eur_yr> Km_Totaal   (PlanRegio) := KostenBaten/Km_TotaalD + Kosten/Km_ow;
	attribute<kg_yr>  CO2_Primair (PlanRegio) := TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * Kosten/CO2_GJ;
	attribute<GJ_yr>  GJe_Totaal  (PlanRegio) := TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * TypeInfo/RekenType/Factoren/Elektriciteit;
	attribute<kg_yr>  CO2_e       (PlanRegio) := TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * Kosten/CO2e_GJ;
	attribute<kg_yr>  CO2         (PlanRegio) := (1d - TypeInfo/P_vol) * KostenBaten/CO2_BijstookD + CO2_Primair;
	
	attribute<Eur_yr> Oji_Yield   (PlanRegio) :=
		KostenBaten/Oji_NettoD - (Kosten/Kji_ow) - (Kosten/Kj_ow - Kosten/Oji_ow_EIA) + KostenBaten/KostenD/Bijstook/Kj_wd_gas - Kosten/Kj_wd_gas;

	attribute<Eur_GJ> YieldPerGJ  (PlanRegio) := (Oji_Yield + Kosten/Oj_lv_SDE) / max_elem(TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD, 0.001[GJ_yr]);
	parameter<Eur_GJ> Threshold := -nth_element_weighted( -YieldPerGJ  * float64(not(OldGebruik)), MaxTotalDemand * 1000000[GJ / PJ], TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD);
	
	attribute<bool> RentabiliteitsAfweging (PlanRegio) := VoorkEuren/Afweging/AlwaysBIO 
		? const(true, Planregio)
		: YieldPerGJ > max_elem(Threshold, 0[Eur_GJ]);

	attribute<bool> OldGebruik   (PlanRegio) := IsDefined(BeginStatus/PlanRegioWarmteAllocatie_REMOVE); // een optie (deze of andere) was al in gebruik
	attribute<bool> NewGebruik   (PlanRegio) := RentabiliteitsAfweging && (PlanRegio/StartJaar <= Jaar) && not(OldGebruik);
	attribute<bool> GebruikOptie (PlanRegio) := NewGebruik || (BeginStatus/PlanRegioWarmteAllocatie_REMOVE == Optie);

	container Results := BeginStatus
	{
		container Factoren := TypeInfo/RekenType/Factoren;

		attribute<Classifications/GebiedsOptie> PlanRegioWarmteAllocatie_REMOVE (PlanRegio) := ../NewGebruik ? Optie : BeginStatus/PlanRegioWarmteAllocatie_REMOVE;

		container BebouwingMutatie :=
			for_each_ne(Classifications/BebouwingsComponent/name
				, 'PlanRegioOptieResultaat(BeginStatus/Bebouwing/'+Classifications/BebouwingsComponent/name+', Optie, .../NewGebruik, BeginStatus/AfnameGebied)'
			);

		container Bebouwing := for_each_nex(Classifications/BebouwingsComponent/name, 'BebouwingMutatie/'+Classifications/BebouwingsComponent/name+'/results', uint32);

		container WijkResults 
		{
			container VorigePeriode := ='BeginStatus/'+Classifications/GebiedsOptie/name[optie];

			attribute<float64> P_vol(PlanRegio) := VorigePeriode/P_Vol;

			container jaarlijks
			{
				container ge
				{
					attribute<Eur_yr> Kj_ge_hv        (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kj_ge_hv  : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kj_ge_hv;
					attribute<Eur_yr> Kji_ge_ov       (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kji_ge_ov : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kji_ge_ov;
					attribute<Eur_yr> Kji_ge_pm       (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kji_ge_pm : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kji_ge_pm;

					attribute<Eur_yr> Kmi_ge_ov       (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kmi_ge_ov : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kmi_ge_ov;
					attribute<Eur_yr> Kmi_ge_pm       (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kmi_ge_pm : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Kmi_ge_pm;
					attribute<Eur_yr> Om_ge_comfort   (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Bji_ge_Aansl    (PlanRegio) := eenmalig/Oi_lv_Aansl * NCW/bw28/AnnualisationFactor;
					attribute<Eur_yr> Oji_ge_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_ge_EIA * NCW/bw28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Oji_ge_EIA;
					attribute<Eur_yr> Omi_ge_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_ge_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ge/Omi_ge_EIA;
				}

				container id
				{
					attribute<Eur_yr> Kj_id_gas       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_EH    (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_CO2   (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Km_id_gas       (PlanRegio) := const(0[Eur_yr], PlanRegio);

					attribute<Eur_yr> Kj_id_elek      (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_EH   (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_CO2  (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Km_id_elek      (PlanRegio) := const(0[Eur_yr], PlanRegio);

					attribute<Eur_yr> Kj_id_oh        (PlanRegio) := (impl/NewGebruik ? KostenBaten/KostenD/Kj_id_oh : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kj_id_oh;
					attribute<Eur_yr> Kj_id_adm       (PlanRegio) := const(0[Eur_yr], PlanRegio);

					attribute<Eur_yr> Kji_id          (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kji_id : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kji_id;
					attribute<Eur_yr> Kmi_id          (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kmi_id : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Kmi_id;

					attribute<Eur_yr> Oji_id_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_id_EIA * NCW/id28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Oji_id_EIA;
					attribute<Eur_yr> Omi_id_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_id_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/id/Omi_id_EIA;

					attribute<Eur_yr> Oj_id_SDE       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kji_id_LO       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kmi_id_LO       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_LO_oh     (PlanRegio) := const(0[Eur_yr], PlanRegio);
				}

				container wd
				{
					attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := GebruikOptie ? (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/Kj_wd_gas    : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_gas_EH    (PlanRegio) := GebruikOptie ? (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/Kj_wd_gas_EH : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_gas_CO2   (PlanRegio) := GebruikOptie ? (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/Kj_wd_gas_CO2: 0[Eur_yr];
					attribute<Eur_yr> Km_wd_gas       (PlanRegio) := GebruikOptie ? (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/Km_wd_gas    : 0[Eur_yr];

					attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek     : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_elek_EH   (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek_eh  : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_elek_CO2  (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/SecundairNet/Kj_wd_elek_co2 : 0[Eur_yr];
					attribute<Eur_yr> Km_wd_elek      (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/SecundairNet/Km_wd_elek     : 0[Eur_yr];

					attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := (impl/NewGebruik ? KostenBaten/KostenD/Kj_wd_oh        : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_oh;
					attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := (impl/NewGebruik ? KostenBaten/KostenD/Kj_wd_adm       : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kj_wd_adm;
					attribute<Eur_yr> Kji_wd          (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kji_wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kji_wd;
					attribute<Eur_yr> Kmi_wd          (PlanRegio) := (impl/NewGebruik ? KostenBaten/Kmi_wd : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Kmi_wd;

					attribute<Eur_yr> Oji_wd_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_wd_EIA * NCW/wd28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Oji_wd_EIA;
					attribute<Eur_yr> Omi_wd_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_wd_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/wd/Omi_wd_EIA;

					attribute<Eur_yr> Oj_wd_SDE       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kji_wd_Gnet     (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kmi_wd_Gnet     (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_Gnet_oh   (PlanRegio) := const(0[Eur_yr], PlanRegio);
				}

				container pt
				{
					attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kji_pt          (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kmi_pt          (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Oji_pt_EIA      (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Omi_pt_EIA      (PlanRegio) := const(0[Eur_yr], PlanRegio);
				}
				container ow
				{
					attribute<Eur_yr> Kj_ow_gas       (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_EH    (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_CO2   (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_gas       (PlanRegio) := const(0[Eur_yr], PlanRegio);

					attribute<Eur_yr> Kj_ow_elek      (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_EH   (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_CO2  (PlanRegio) := const(0[Eur_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_elek      (PlanRegio) := const(0[Eur_yr], PlanRegio);

					attribute<Eur_yr> Kj_ow_productie (PlanRegio) := GebruikOptie ? Kosten/Kj_ow_warmtewaarde : 0[Eur_yr];

					attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := (impl/NewGebruik ? Kosten/Kj_ow_oh  : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kj_ow_oh;
					attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := (impl/NewGebruik ? Kosten/Kj_ow_adm : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kj_ow_adm;

					attribute<Eur_yr> Kji_ow          (PlanRegio) := (impl/NewGebruik ? Kosten/Kji_ow : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kji_ow;
					attribute<Eur_yr> Kmi_ow          (PlanRegio) := (impl/NewGebruik ? Kosten/Ki_ow * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Kmi_ow;

					attribute<Eur_yr> Oji_ow_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_ow_EIA * NCW/ow28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Oji_ow_EIA;
					attribute<Eur_yr> Omi_ow_EIA      (PlanRegio) := (impl/NewGebruik ? eenmalig/Oi_ow_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr]) + VorigePeriode/jaarlijks/ow/Omi_ow_EIA;

					attribute<Eur_yr> Oj_ow_SDE       (PlanRegio) := const(0[Eur_yr], PlanRegio);
				}
				container lv
				{
					attribute<Eur_yr> Oji_lv_Aansl    (PlanRegio) := eenmalig/Oi_lv_Aansl * NCW/ow28/AnnualisationFactor;
					attribute<Eur_yr> Omi_lv_Aansl    (PlanRegio) := eenmalig/Oi_lv_Aansl * NCW/mr28/AnnualisationFactor;

					attribute<Eur_yr> Oj_lv_verbruik  (PlanRegio) := GebruikOptie ? KostenBaten/Opbrengsten/jaarlijksD/Oj_warmte  : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_vastrecht (PlanRegio) := GebruikOptie ? KostenBaten/Opbrengsten/jaarlijksD/Oj_vastrecht : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_SDE       (PlanRegio) := (impl/NewGebruik ? Kosten/Oj_lv_SDE  : 0[Eur_yr])+VorigePeriode/jaarlijks/lv/Oj_lv_SDE;
				}
			}

			container eenmalig
			{
				attribute<Eur> Oi_lv_Aansl    (PlanRegio) := impl/NewGebruik ? KostenBaten/Opbrengsten/eenmaligD/Oi_Totaal : 0[Eur];
				attribute<Eur> Ki_ge_ov       (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_ge_ov : 0[Eur];
				attribute<Eur> Ki_ge_pm       (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_ge_pm : 0[Eur];
				attribute<Eur> Oi_ge_EIA      (PlanRegio) := impl/NewGebruik ? KostenBaten/Oi_ge_subsidie: 0[Eur];

				attribute<Eur> Ki_id          (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_id_Bruto : 0[Eur];
				attribute<Eur> Oi_id_EIA      (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_id_Bruto * SpecifiekeInstellingen/Beleid/EIA/id : 0[Eur];

				attribute<Eur> Ki_wd          (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_wd_Bruto : 0[Eur];
				attribute<Eur> Oi_wd_EIA      (PlanRegio) := impl/NewGebruik ? KostenBaten/Ki_wd_Bruto * SpecifiekeInstellingen/Beleid/EIA/wd : 0[Eur];

				attribute<Eur> Ki_pt          (PlanRegio) := const(0[Eur], PlanRegio);
				attribute<Eur> Oi_pt_EIA      (PlanRegio) := const(0[Eur], PlanRegio);

				attribute<Eur> Ki_ow          (PlanRegio) := impl/NewGebruik ? Kosten/Ki_ow     : 0[Eur];
				attribute<Eur> Oi_ow_EIA      (PlanRegio) := impl/NewGebruik ? Kosten/Oi_ow_EIA : 0[Eur];

				attribute<Eur> Ki_wd_Gnet     (PlanRegio) := const(0[Eur], PlanRegio);
				attribute<Eur> 	Ki_id_lo      (PlanRegio) := const(0[Eur], PlanRegio);
			}

			container Verbruik
			{
				attribute<Gj_yr> V_id_gas     (PlanRegio) := const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr> V_wd_gas     (PlanRegio) := GebruikOptie ? (1d - TypeInfo/P_vol) * KostenBaten/KostenD/Bijstook/V_Bijstook : 0.0[GJ_yr];
				attribute<Gj_yr> V_ow_gas     (PlanRegio) := GebruikOptie ? TypeInfo/RekenType/Factoren/AardGas * TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD : 0.0[GJ_yr];

				attribute<Gj_yr> V_id_elek    (PlanRegio) := const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr> V_wd_elek    (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/SecundairNet/V_wd_elek : 0.0[GJ_yr];
				attribute<Gj_yr> V_ow_elek    (PlanRegio) := GebruikOptie ? TypeInfo/RekenType/Factoren/Elektriciteit * TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD : 0.0[GJ_yr];

				attribute<Gj_yr> V_id_verlies (PlanRegio) := const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr> V_wd_verlies (PlanRegio) := const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr> V_pt_verlies (PlanRegio) := const(0[Gj_yr], PlanRegio);

				attribute<Gj_yr> V_warmte     (PlanRegio) := GebruikOptie ? KostenBaten/KostenD/V_Warmte : 0[GJ_yr];
				attribute<Gj_yr> V_koude      (PlanRegio) := const(0[Gj_yr], PlanRegio);

				// generieke netto warmte onttrekking:
				attribute<Gj_yr> V_id_warmte  (PlanRegio) := V_warmte    - (V_id_gas + V_id_elek - V_id_verlies);
				attribute<Gj_yr> V_wd_warmte  (PlanRegio) := V_id_warmte - (V_wd_gas + V_wd_elek - V_wd_verlies);
				attribute<Gj_yr> V_ow_warmte  (PlanRegio) := V_wd_warmte - (V_ow_gas + V_ow_elek - V_pt_verlies);
				attribute<Gj_yr> V_ow_koude   (PlanRegio) := V_Koude;
				attribute<Gj_yr> V_H2         (PlanRegio) := const(0[Gj_yr], PlanRegio);
			}

			container Uitstoot
			{
				attribute<KG_yr> CO2_id_gas    (PlanRegio) := const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr> CO2_wd_gas    (PlanRegio) := float64(impl/NewGebruik) * (1d - TypeInfo/P_vol) * KostenBaten/CO2_BijstookD;
				attribute<KG_yr> CO2_ow_gas    (PlanRegio) := float64(impl/NewGebruik) * TypeInfo/P_vol * KostenBaten/V_WarmtePrimairD * MakeDefined(CO2_GJb, 0[kg_GJ]);

				attribute<KG_yr> CO2_id_elek   (PlanRegio) := Verbruik/V_id_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr> CO2_wd_elek   (PlanRegio) := Verbruik/V_wd_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr> CO2_ow_elek   (PlanRegio) := Verbruik/V_ow_elek * prijzen/Elektriciteit/CO2_GJ;
			}
		}
	}
}
