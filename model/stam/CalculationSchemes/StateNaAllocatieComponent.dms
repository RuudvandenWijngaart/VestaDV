//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template StateNaAllocatieComponent
{
	// begin case parameters
	unit<uint32> BO_base;
	// end case [arameters

	unit<uint32> BO := BO_base
	{
		container Metervraag := for_each_nedv('V_'+Classifications/BemeterdeGebouwInput/name
			, replace('merge(GebouwOptie_rel, GJ_yr, '+AsItemList('GebouwOpties/'+BcGebouwOptie/name+'/metervraag/@I')+')', '@I', Classifications/BemeterdeGebouwInput/name)
			, BO
			, GJ_yr
			);

		container WarmteVraag // CalculationSchemes/WarmteVraagT is hier expliciet geinstantieerd wegens iets andere toepassing bij herberekening achteraf.
		{
			// begin case parameters
			// attribute<Celsius> T_sec(BO) := Classifications/GebiedsOptie/T_sec[Classifications/WarmteOptie/GebiedsOptie_rel[WarmteOptie_rel]];
			attribute<bool>    IsLT(BO) := Classifications/WarmteOptie/IsLT[WarmteOptie_rel];
			attribute<bool>    Koudebeschikbaar(BO) := WarmteOptie_rel == Classifications/WarmteOptie/V/WKO; // TODO OV: Generaliseren
			attribute<float64> BeschikbaarheidsFactor(BO) := Float64(Classifications/WarmteOptie/IsGebiedsOptie[WarmteOptie_rel]);
			// end case parameters

			//Aansluitwaardes
			attribute<kW>     ASW_RV             (BO) := IsLT ? BO/AansluitCapaciteit/ASW_RVlt : BO/AansluitCapaciteit/ASW_RVmt;
			attribute<kW>     ASW_TW             (BO) := IsLT ? BO/AansluitCapaciteit/ASW_TWlt : BO/AansluitCapaciteit/ASW_TWmt;
			attribute<kW>     ASW_K              (BO) := KoudeBeschikbaar     ? BO/AansluitCapaciteit/ASW_K    : 0[kw];

			//Gelijktijdigheidsfactoren
			attribute<ratio>  GTF_RV             (BO) := IsLT ? BO/AansluitCapaciteit/GTF_RVlt : BO/AansluitCapaciteit/GTF_RVmt;
			attribute<ratio>  GTF_TW             (BO) := BO/AansluitCapaciteit/GTF_TW;
			attribute<ratio>  GTF_K              (BO) := BO/AansluitCapaciteit/GTf_K;

			//Vermogensvraag aan het secundaire net
			attribute<kW>     P_RV_sec           (BO) := ASW_RV * GTF_RV;
			attribute<kW>     P_TW_sec           (BO) := ASW_TW * GTF_TW;
			attribute<kW>     P_K_sec            (BO) := KoudeBeschikbaar ? ASW_K * GTF_K : 0.0[kw];

			attribute<kW>     AansluitCapaciteit (BO) := KoudeBeschikbaar ? max_elem(ASW_TW   + ASW_RV  , ASW_K  ) : ASW_TW   + ASW_RV  ;
			attribute<kW>     P_sec              (BO) := KoudeBeschikbaar ? max_elem(P_TW_sec + P_RV_sec, P_K_sec) : P_TW_sec + P_RV_sec;

			attribute<nrAsl>  nrAansluitingen    (BO) := BeschikbaarheidsFactor * BO/nrAansluitingen;
			attribute<m2>     Oppervlakte        (BO) := BeschikbaarheidsFactor * BO/Oppervlakte;

			//VRVS3FH
			parameter<bool>   IsS3FofS3H              := (Variantcode == 's3f' || Variantcode == 's3h') && RekenstapName == 'R2_2030' && BO/BebouwingsComponent_UpRef/BCname == 'BestaandeWoning';
			attribute<GJ_yr>  V_RV_def           (BO) := BeschikbaarheidsFactor * BO/BebouwingsComponent_UpRef/Functioneel2/V_RV;
			attribute<GJ_yr>  V_RV               (BO) := =IsS3FofS3H ? 'BeschikbaarheidsFactor * BO/GebouwOpties/NaarD_w/Functioneel/RV' : 'V_RV_def';

			attribute<GJ_yr>  V_TW               (BO) := BeschikbaarheidsFactor * BO/Functioneel/TW;
			attribute<GJ_yr>  V_K                (BO) := BeschikbaarheidsFactor * BO/Functioneel/K;
			attribute<GJ_yr>  V_Warmte           (BO) := V_RV + V_TW;
			attribute<Eur_yr> Oj_Warmte          (BO) := V_Warmte * BO/WarmteWet/WarmtePrijs, Source = "FO v7a F 8-10";
			attribute<Eur_yr> Oj_VastRechtMT     (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtMT_T;
			attribute<Eur_yr> Oj_VastRechtLT     (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtLT_T;
			attribute<Eur_yr> Oj_VastRechtK      (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtK_T;
			attribute<Eur>    Oi_AansluitBijdrage(BO) := BeschikbaarheidsFactor * BO/Opbrengsten/AansluitBijdrageT;
		}
		
		container eenmalig
		{
			//Aansluitbijdrages
			attribute<Eur> Ki_Asl_Enet (BO) := BC_kentallen/Ki_Aansl_e_asl * nraansluitingen + BC_kentallen/Ki_Aansl_e_opp * oppervlakte;
			attribute<Eur> Ki_Asl_Gnet (BO) := Metervraag/V_gas <= 0[GJ_yr]
				? 0[Eur]
				: BC_kentallen/Ki_Aansl_g_asl * nraansluitingen + BC_kentallen/Ki_Aansl_g_opp * oppervlakte;

			//Investeringen
			attribute<Eur> Ki30_LO (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Ki30_LO')+')';
			attribute<Eur> Ki20_LO (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Ki20_LO')+')';
			attribute<Eur> Ki15_LO (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Ki15_LO')+')';
			attribute<Eur> Ki_GV   (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Ki_GV'  )+')';
			attribute<Eur> Ki_LTAS (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Ki_LTAS')+')';
			
			attribute<Eur_yr> Kj_gv_incentive(BO) := ='merge(GebouwOptie_rel, Eur_yr, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Kj_gv_incentive')+')';

			//Subsidies
			attribute<Eur> Oi_s_LO_30 (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Oi30_s_LO')+')';
			attribute<Eur> Oi_s_LO_20 (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Oi20_s_LO')+')';
			attribute<Eur> Oi_s_LO_15 (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Oi15_s_LO')+')';
			attribute<Eur> Oi_s_GV    (BO) := ='merge(GebouwOptie_rel, Eur, '+AsItemList('GebouwOpties/' + BcGebouwOptie/name + '/Oi_s_GV')+')';
		}
		
		container Kapitaallasten
		{
			container PrevResults := BO_base/OrgKapitaallasten;

			//Aansluitbijdrages
			attribute<Eur_yr> Kji_Asl_Enet (BO) := PrevResults/Kji_Asl_Enet + eenmalig/Ki_Asl_Enet * NcwRefs/_50/AnnualisationFactor;
			attribute<Eur_yr> Kmi_Asl_Enet (BO) := PrevResults/Kmi_Asl_Enet + eenmalig/Ki_Asl_Enet * NCW/mr50/AnnualisationFactor;
			attribute<Eur_yr> Kji_Asl_Gnet (BO) := PrevResults/Kji_Asl_Gnet + eenmalig/Ki_Asl_Gnet * NcwRefs/_50/AnnualisationFactor;
			attribute<Eur_yr> Kmi_Asl_Gnet (BO) := PrevResults/Kmi_Asl_Gnet + eenmalig/Ki_Asl_Gnet * NCW/mr50/AnnualisationFactor;

			//Investeringen
			attribute<Eur_yr> Kji30_LO (BO) := PrevResults/Kji30_LO + eenmalig/Ki30_LO * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Kmi30_LO (BO) := PrevResults/Kmi30_LO + eenmalig/Ki30_LO * NCW/mr30/AnnualisationFactor;
			attribute<Eur_yr> Kji20_LO (BO) := PrevResults/Kji20_LO + eenmalig/Ki20_LO * NcwRefs/_20/AnnualisationFactor;
			attribute<Eur_yr> Kmi20_LO (BO) := PrevResults/Kmi20_LO + eenmalig/Ki20_LO * NCW/mr20/AnnualisationFactor;
			attribute<Eur_yr> Kji15_LO (BO) := eenmalig/Ki15_LO * NcwRefs/_15/AnnualisationFactor;
			attribute<Eur_yr> Kmi15_LO (BO) := eenmalig/Ki15_LO * NCW/mr15/AnnualisationFactor;
			attribute<Eur_yr> Kji_gv   (BO) := PrevResults/Kji_gv   + eenmalig/Ki_gv   * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Kmi_gv   (BO) := PrevResults/Kmi_gv   + eenmalig/Ki_gv   * NCW/mr30/AnnualisationFactor;
			attribute<Eur_yr> Kj_gv_incentive(BO) := PrevResults/Kj_gv_incentive+ Eenmalig/Kj_gv_incentive;
			
			attribute<Eur_yr> Kji_LTAS (BO) := PrevResults/Kji_LTAS + eenmalig/Ki_LTAS * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Kmi_LTAS (BO) := PrevResults/Kmi_LTAS + eenmalig/Ki_LTAS * NCW/mr30/AnnualisationFactor;

			//Subsidies
			attribute<Eur_yr> Oji_s_LO_30 (BO) := PrevResults/Oji_s_LO_30 + eenmalig/Oi_s_LO_30 * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_LO_20 (BO) := PrevResults/Oji_s_LO_20 + eenmalig/Oi_s_LO_20 * NcwRefs/_20/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_LO_15 (BO) := PrevResults/Oji_s_LO_15 + eenmalig/Oi_s_LO_15 * NcwRefs/_15/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_gv    (BO) := PrevResults/Oji_s_gv    + eenmalig/Oi_s_gv    * NcwRefs/_30/AnnualisationFactor;
			
			// Onderhoud en admin
			attribute<Eur_yr> Kj_OH_LO (BO) := ='merge(GebouwOptie_rel, Eur_yr, '+AsItemList('GebouwOpties/'+BcGebouwOptie/name+'/Kj_OH_LO')+')';
			attribute<Eur_yr> Kj_Adm_LO(BO) := ='merge(GebouwOptie_rel, Eur_yr, '+AsItemList('GebouwOpties/'+BcGebouwOptie/name+'/Kj_Adm_LO')+')';
		}
	}
}

