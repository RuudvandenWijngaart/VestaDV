//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container NieuwbouwWoningOperaties
{
	template Select
	{
		// begin case parameters
		attribute<nrAsl>                               DataWoningenGrid (Geography/rdc_grids/m100);
		parameter<Classifications/WoningtypeNieuwbouw> type;
		parameter<yr_uint16>                           ZichtJaar;
		// einde case parameters

		parameter<ratio> SpreidingRekenjaren := (1d / sum(float64(Classifications/ZichtJaar/jaar[Classifications/Rekenstap/ZichtJaar_rel_bebouwing] == ZichtJaar)))[ratio];
		attribute<nrAsl> NrWoningenGrid (Geography/rdc_grids/m100) := DataWoningenGrid * SpreidingRekenjaren;
		unit<uint32> BebouwingsObject := Subset(NrWoningenGrid > 0[nrWoning] && Invoer/RuimtelijkeData/StudieGebied/selected_per_m100),
			DialogType = "Map",
			DialogData = "point"
		{
			attribute<nrAsl>    NrAansluitingen_i := NrWoningenGrid[BebouwingsObject/nr_OrgEntity];
			attribute<pand_asl> pand_aandeel      := const(Classifications/WoningtypeNieuwbouw/pand_aandeel[type], BebouwingsObject);
			attribute<m2>       Oppervlakte_i     := NrAansluitingen_i * Classifications/WoningtypeNieuwbouw/oppervlak[type];
			
			attribute<Invoer/RuimtelijkeData/StudieGebied/buurt>    buurt_rel := Geography/rdc_grids/m100/buurt_rel[BebouwingsObject/nr_OrgEntity];
			attribute<Geography/rdc_meter>                    point           := Geography/rdc_grids/m100/point  [BebouwingsObject/nr_OrgEntity];
			attribute<Classifications/WoningtypeBestaand>     BebouwingsType  := const(Classifications/WoningtypeNieuwbouw/TypeBestaand_rel[Type],.);
			attribute<Classifications/BouwjaarWoningBestaand> Bouwjaar        := const(classify(value(ZichtJaar, yr)-value(0.5, yr),  classifications/BouwjaarWoningBestaand/ClassBreak),BebouwingsObject);
			
			container Gebied
			{
				attribute<float64> n      (..) := float64(NrAansluitingen_i);
				attribute<float64> MEAN_x (..) := float64(pointCol(point));
				attribute<float64> MEAN_y (..) := float64(pointRow(point));
				attribute<float64> SSD_xx (..) := const(10000.0 / 12.0, BebouwingsObject);
				attribute<float64> SSD_xy (..) := const(    0.0 / 12.0, BebouwingsObject);
				attribute<float64> SSD_yy (..) := const(10000.0 / 12.0, BebouwingsObject);
			}
			container Kapitaallasten := for_each_nedv(
				Classifications/Gebouw_Kapitaallasten/name,
				'const(0[Eur_yr], BebouwingsObject)',
				BebouwingsObject,
				Eur_yr);
		}
	}
}