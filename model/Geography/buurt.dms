//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       In deze configuratiefile wordt de regio indeling buurt met eigendommen etc.    //
//       geconfigureerd                                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

unit<uint32> Buurt:
	StorageName     = "%HestiaDataDir%/hulpbestanden/buurt/20220728_CBS_buurt_2020.dbf",
	DialogData      = "geometry",
	DialogType      = "Map",
	StorageReadOnly = "True",
	url             = "%HestiaDataDir%/hulpbestanden/buurt/20200114_update_buurtkaart_FvdM.txt"
{
	attribute<rdc_meter> geometry (poly)
	:	label           = "De CBS buurtkaart 2020"
	,	StorageName     = "%HestiaDataDir%/hulpbestanden/buurt/20220728_CBS_buurt_2020.shp"
	,	StorageReadOnly = "True";
	attribute<string>    BU_CODE;
	attribute<uint32>    code          := uint32(substr(BU_CODE,2,8));

	// bepalen centroide en oppervlakte o.b.v. buurtpolygoon
	attribute<rdc_meter> point         := centroid_or_mid(geometry);
	attribute<m2>        area          := area(geometry, m2);
	attribute<km2>       AreaInKm2     := area / value(1000000, m2 / km2);
	attribute<ha>        AreaInHa      := area / value(10000, m2 / ha);

	// ruimtelijke niveau's nationaal, provincie, gemeente en wijk worden afgeleid uit de buurtkaart
	attribute<NL>           NL_rel           := const(0[NL],.);
	attribute<Provincie>    Provincie_rel    := rlookup(PROV_NAAM, Provincie/Values);
	attribute<Energieregio> Energieregio_rel := rlookup(ER_NAAM, Energieregio/Values);
	attribute<Gemeente>     Gemeente_rel     := rlookup(GM_CODE, Gemeente/Values);
	attribute<Wijk>         Wijk_rel         := rlookup(WK_CODE, Wijk/Values);
	attribute<bool>         isWater          := H2O == 'JA';

	unit<uint32> GM_prov_er
			:	StorageName     = "%HestiaDataDir%/hulpbestanden/buurt/GM_prov_er.csv"
			,	StorageType     = "gdal.vect"
			,	StorageReadOnly = "True"
			{
				attribute<string> label :=  uppercase(GM_CODE);
			}

	attribute<string> ER_NAAM   := GM_prov_er/ER_NAAM[rlookup(GM_CODE,GM_prov_er/GM_CODE)];
	attribute<string> PROV_NAAM := GM_prov_er/PROV_NAAM[rlookup(GM_CODE,GM_prov_er/GM_CODE)];
	
	parameter<string> rjoin_per_jaar_expr_template :=
		'MakeDefined('
			+ AsList('rjoin(BU_CODE, j' + Invoer/RuimtelijkeData/CBSWijkEnBuurt/Jaar/reverse + '/src/gwb_code_10, j' + Invoer/RuimtelijkeData/CBSWijkEnBuurt/Jaar/reverse + '/src/@@@var@@@)' ,',')
		+ ')';

	// diverse kenmerken op buurtniveau, afgeleid uit de kerncijfers wijken en buurten van het CBS
	container Eigendom: Using = Invoer/RuimtelijkeData/CBSWijkEnBuurt/per_jaar
	{
		attribute<percent> p_koopw    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_koopw[percent]');  // Koopwoningen
		attribute<percent> p_huurw    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_huurw[percent]');  // Huurwoningen totaal
		attribute<percent> p_wcorpw   (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_wcorpw[percent]'); // In bezit woningcorporatie
		attribute<percent> p_ov_hw    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_ov_hw[percent]');  // In bezit overige verhuurders
		attribute<percent> p_e_o_w    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_e_o_w[percent]');  // Eigendom onbekend

		attribute<percent> p_checksum (..) := p_koopw + p_wcorpw + p_ov_hw + p_e_o_w;
	}

	container Inkomen : Using = Invoer/RuimtelijkeData/CBSWijkEnBuurt/per_jaar
	{
		attribute<percent> p_hh_li    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_hh_li[percent]'); // lage inkomens
		attribute<percent> p_hh_hi    (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_hh_hi[percent]'); // hoge inkomens
		attribute<percent> p_hh_mi    (..) := 100[percent] - (p_hh_li + p_hh_hi);                                       // middeninkomens
		attribute<percent> p_checksum (..) := p_hh_li + p_hh_mi + p_hh_hi;
	}
	
	// overige kenmerken die van belang zijn voor de modelberekening
	container Overigen : Using = Invoer/RuimtelijkeData/CBSWijkEnBuurt/per_jaar 
	{
		attribute<percent> p_stadsv  (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'p_stadsv[percent]'); // aandeel huidige aansluiting stadsverwarming
		attribute<percent> p_aardgas (..) := 100[percent] - (p_stadsv);                                                 // aandeel huidige aansluiting aardgas

		attribute<ha> a_opp_ha       (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'a_opp_ha[ha]'); // totaal oppervlak buurt
		attribute<ha> a_lan_ha       (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'a_lan_ha[ha]'); // landoppervlak buurt
		attribute<ha> a_wat_ha       (..) := = replace(rjoin_per_jaar_expr_template, '@@@var@@@', 'a_wat_ha[ha]'); // wateroppervlak buurt

		//oppervlakte als aandeel
		attribute<ratio> r_land_com  (..) := MakeDefined(r_land, 0[ratio]);//null waarden liggen in zee
		attribute<ratio> r_land      (..) := 1d / a_opp_ha* a_lan_ha;
		attribute<ratio> r_water     (..) := (1d / a_opp_ha)* a_wat_ha;
		attribute<ratio> r_checksum  (..) := r_land + r_water;
	}
}